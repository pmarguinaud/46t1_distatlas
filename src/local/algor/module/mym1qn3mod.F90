MODULE MYM1QN3MOD
!-----------------------------------------------------------------------
!
!     EXAMPLE OF PROGRAM : MINIMIZATION OF X^2+CY^4
!     WHERE C>0 IS A PARAMETER
!
!-----------------------------------------------------------------------
!
USE PARKIND1, ONLY : JPRB, JPIM
USE M1QN3MOD
IMPLICIT NONE


TYPE, EXTENDS (M1QN3CTX) :: MYM1QN3CTX
  REAL (KIND=JPRB),    POINTER :: COEF (:) => NULL ()
  INTEGER (KIND=JPIM), POINTER :: EXPO (:) => NULL ()
  CONTAINS
    PROCEDURE :: SIMUL   => MYSIMUL
    PROCEDURE :: PROSCA  => MYPROSCA
END TYPE

CONTAINS

SUBROUTINE MYPROSCA (YDCTX,N,X,Y,PS)
!
IMPLICIT NONE
CLASS (MYM1QN3CTX) :: YDCTX
INTEGER (KIND=JPIM) N
REAL (KIND=JPRB) X(N),Y(N),PS
REAL (KIND=JPRB) PSP

CALL YDCTX%EUCLID (N,X,Y,PS)

END SUBROUTINE

SUBROUTINE MYSIMUL(YDCTX,INDIC,N,X,F,G,LGRAD)
IMPLICIT NONE
CLASS (MYM1QN3CTX) :: YDCTX
INTEGER (KIND=JPIM) INDIC, N, I
REAL (KIND=JPRB) :: X(N),G(N), F, FP
LOGICAL, OPTIONAL :: LGRAD
!
IF (INDIC.EQ.1) THEN
    WRITE (6,'(/A)') "FREE CALL TO SIMUL"
    RETURN
ENDIF
!
FP = 0.
DO I = 1, N
  IF (YDCTX%EXPO (I) > 0) THEN
    FP = FP + YDCTX%COEF (I) * X (I) ** YDCTX%EXPO (I)
  ELSE
    FP = FP + YDCTX%COEF (I) 
  ENDIF
  IF (YDCTX%EXPO (I) > 0) THEN
    G(I) = YDCTX%COEF (I) * YDCTX%EXPO (I) * X (I) ** (YDCTX%EXPO (I)-1)
  ELSE
    G(I) = 0._JPRB
  ENDIF
ENDDO

CALL YDCTX%REDUCER (1, (/ FP /), 'SUM', F)

END SUBROUTINE

END MODULE
    
