MODULE ATLAS_FMT_AEROSOLS

USE ATLAS_FMT, ONLY : ATLAS_FMT_t

USE PARKIND1, ONLY : JPRB, JPIM
USE ATLAS_MODULE

#include "atlas-abort.h"

IMPLICIT NONE

TYPE, EXTENDS (ATLAS_FMT_t) :: ATLAS_FMT_AEROSOLS_t

  CHARACTER (LEN=256) :: CLFILE

CONTAINS

  PROCEDURE :: OPEN
  PROCEDURE :: CLOSE
  PROCEDURE :: GRID
  PROCEDURE :: READ    
  PROCEDURE :: WRITE   

END TYPE

PRIVATE

PUBLIC :: ATLAS_FMT_AEROSOLS_t

CONTAINS

SUBROUTINE READSLSD (CDFILE, PSLSD)

CHARACTER (LEN=*), INTENT (IN) :: CDFILE
REAL (KIND=JPRB),  ALLOCATABLE :: PSLSD (:, :)

INTEGER (KIND=JPIM) :: ILUN, J
CHARACTER (LEN=256) :: CLLINE

ILUN = 77_JPIM

ALLOCATE (PSLSD (45 * 72, 4))

OPEN (UNIT=ILUN, FILE=TRIM (CDFILE), FORM='FORMATTED', STATUS='OLD')

DO J = 1, 24
  READ (ILUN, '(A)', ERR=999, END=999) CLLINE
ENDDO

READ (ILUN, *) PSLSD

CLOSE (ILUN)

RETURN

999 CONTINUE

  CALL ATLAS_ABORT ('ATLAS_FMT_AEROSOLS:READSLSD: ERROR WHILE READING '//TRIM (CDFILE))

END SUBROUTINE

TYPE (ATLAS_STRUCTUREDGRID) FUNCTION GRID (THIS, CDFILE) RESULT (YLGRID)
CLASS (ATLAS_FMT_AEROSOLS_t), INTENT (IN) :: THIS
CHARACTER (LEN=*),            INTENT (IN) :: CDFILE

INTEGER (KIND=JPIM) :: ILON, ILAT

ILON = 72; ILAT = 45

BLOCK
  TYPE (ATLAS_CONFIG) :: YLCFGR, YLCFDO

  YLCFGR = ATLAS_CONFIG ()
  YLCFDO = ATLAS_CONFIG ()

  CALL YLCFGR%SET ("nx", ILON)
  CALL YLCFGR%SET ("ny", ILAT)
  CALL YLCFGR%SET ("type", "shifted_lonlat")
  CALL YLCFDO%SET ("type", "global") 
  CALL YLCFDO%SET ("west", 0._JPRB)

  CALL YLCFDO%SET ("units", "degrees")
  CALL YLCFGR%SET ("domain", YLCFDO)

  YLGRID = ATLAS_STRUCTUREDGRID (YLCFGR)

  CALL YLCFGR%FINAL ()
  CALL YLCFDO%FINAL ()
ENDBLOCK

CALL YLGRID%RETURN ()

END FUNCTION

SUBROUTINE OPEN (THIS, CDFILE, YDGRID)
CLASS (ATLAS_FMT_AEROSOLS_t),    INTENT (INOUT) :: THIS
CHARACTER (LEN=*),               INTENT (IN)    :: CDFILE
CLASS (ATLAS_STRUCTUREDGRID),    INTENT (IN)    :: YDGRID
THIS%CLFILE = CDFILE
END SUBROUTINE OPEN

SUBROUTINE CLOSE (THIS)
CLASS (ATLAS_FMT_AEROSOLS_t),    INTENT (INOUT) :: THIS
THIS%CLFILE = ''
END SUBROUTINE


SUBROUTINE READ (THIS, CDNAME, PFLDG, LDUNDEF, PUNDEF)
CLASS (ATLAS_FMT_AEROSOLS_t),  INTENT (IN)    :: THIS
CHARACTER (LEN=*),             INTENT (IN)    :: CDNAME
REAL (KIND=JPRB),              INTENT (OUT)   :: PFLDG (:)
LOGICAL,                       INTENT (INOUT) :: LDUNDEF
REAL (KIND=JPRB),              INTENT (INOUT) :: PUNDEF

REAL (KIND=JPRB), ALLOCATABLE :: ZSLSD (:, :)
INTEGER (KIND=JPIM) :: IND, IMONTH
CHARACTER (LEN=4) :: CLMONTH

IF (LCMPM (CDNAME, 'sea', IMONTH)) THEN
  IND = 1
ELSEIF (LCMPM (CDNAME, 'land', IMONTH)) THEN
  IND = 2
ELSEIF (LCMPM (CDNAME, 'soot', IMONTH)) THEN
  IND = 3
ELSEIF (LCMPM (CDNAME, 'desert', IMONTH)) THEN
  IND = 4
ELSE
  CALL ATLAS_ABORT ('ATLAS_FMT_AEROSOLS:READ: UNEXPECTED FIELD '//TRIM (CDNAME))
ENDIF

WRITE (CLMONTH, '(".m",I2.2)') IMONTH

CALL READSLSD (TRIM (THIS%CLFILE)//CLMONTH, ZSLSD)

PFLDG = ZSLSD (:, IND)

CONTAINS

LOGICAL FUNCTION LCMPM (CDNAME1, CDNAME2, IMONTH) 

CHARACTER (LEN=*),   INTENT (IN)  :: CDNAME1, CDNAME2
INTEGER (KIND=JPIM), INTENT (OUT) :: IMONTH

INTEGER (KIND=JPIM) :: ILEN1, ILEN2

IMONTH = -1
LCMPM = .FALSE.

ILEN1 = LEN_TRIM (CDNAME1)
ILEN2 = LEN_TRIM (CDNAME2)

IF (ILEN1 /= ILEN2 + 4) RETURN
IF (CDNAME1 (1:ILEN2) /= TRIM (CDNAME2)) RETURN
IF (CDNAME1 (ILEN2+1:ILEN2+2) /= '.m') RETURN

READ (CDNAME1 (ILEN2+3:ILEN1), *) IMONTH

IF ((IMONTH < 1) .OR. (12 < IMONTH)) RETURN

LCMPM = .TRUE.

END FUNCTION LCMPM


END SUBROUTINE

SUBROUTINE WRITE (THIS, CDNAME, PFLDG, LDUNDEF, PUNDEF)
CLASS (ATLAS_FMT_AEROSOLS_t), INTENT (IN)    :: THIS
CHARACTER (LEN=*),            INTENT (IN)    :: CDNAME
REAL (KIND=JPRB),             INTENT (IN)    :: PFLDG (:)
LOGICAL,                      INTENT (INOUT) :: LDUNDEF
REAL (KIND=JPRB),             INTENT (INOUT) :: PUNDEF

CALL ATLAS_ABORT ('ATLAS_FMT_AEROSOLS:WRITE NOT IMPLEMENTED')

END SUBROUTINE

END MODULE

