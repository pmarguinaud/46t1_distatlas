MODULE ATLAS_FMT_OZONE

USE ATLAS_FMT, ONLY : ATLAS_FMT_t

USE PARKIND1, ONLY : JPRB, JPIM
USE ATLAS_MODULE

#include "atlas-abort.h"

IMPLICIT NONE

TYPE, EXTENDS (ATLAS_FMT_t) :: ATLAS_FMT_OZONE_t

  CHARACTER (LEN=256) :: CLFILE

CONTAINS

  PROCEDURE :: OPEN
  PROCEDURE :: CLOSE
  PROCEDURE :: GRID
  PROCEDURE :: READ    
  PROCEDURE :: WRITE   

END TYPE

PRIVATE

PUBLIC :: ATLAS_FMT_OZONE_t

CONTAINS

SUBROUTINE READLLABC (CDFILE, PLLABC)

CHARACTER (LEN=*), INTENT (IN) :: CDFILE
REAL (KIND=JPRB),  ALLOCATABLE :: PLLABC (:, :)

REAL (KIND=JPRB), POINTER :: ZLLABC (:, :)
REAL (KIND=JPRB) :: ZLON, ZLAT, ZA, ZB, ZC
INTEGER (KIND=JPIM) :: ILUN, ICUR

ILUN = 77_JPIM

ALLOCATE (ZLLABC (5, 500))
ICUR = 0

OPEN (UNIT=ILUN, FILE=TRIM (CDFILE), FORM='FORMATTED', STATUS='OLD')

DO 

  READ (ILUN, *, END=900, ERR=999) ZLON, ZLAT, ZA, ZB, ZC
  IF (ICUR >= SIZE (ZLLABC, 2)) THEN
    BLOCK
      REAL (KIND=JPRB), POINTER :: ZLLABC1 (:, :)
      INTEGER (KIND=JPIM) :: ILEN1, ILEN
     
      ILEN = SIZE (ZLLABC, 2)
      ILEN1 = 2 * ILEN + 1

      ALLOCATE (ZLLABC1 (5, ILEN1))
      ZLLABC1 (1:5, 1:ILEN) = ZLLABC (1:5, 1:ILEN)

      DEALLOCATE (ZLLABC)
      ZLLABC => ZLLABC1

    ENDBLOCK
  ENDIF

  ICUR = ICUR + 1
  
  ZLLABC (:, ICUR) = [ZLON, ZLAT, ZA, ZB, ZC]

ENDDO

900 CONTINUE

  CLOSE (ILUN)

  ALLOCATE (PLLABC (5, ICUR))
  PLLABC (1:5, 1:ICUR) = ZLLABC (1:5, 1:ICUR)

RETURN

999 CONTINUE

  CALL ABORT ('ATLAS_FMT_OZONE:READLLABC: ERROR WHILE READING '//TRIM (CDFILE))

END SUBROUTINE

TYPE (ATLAS_STRUCTUREDGRID) FUNCTION GRID (THIS, CDFILE) RESULT (YLGRID)
CLASS (ATLAS_FMT_OZONE_t), INTENT (IN) :: THIS
CHARACTER (LEN=*),         INTENT (IN) :: CDFILE

INTEGER (KIND=JPIM) :: ILON, ILAT
REAL (KIND=JPRB), ALLOCATABLE :: ZLLABC (:, :)

CALL READLLABC (TRIM (CDFILE)//'.m01', ZLLABC)

ILON = 2

DO 
  IF (ZLLABC (2, ILON-1) /= ZLLABC (2, ILON+1)) EXIT
  ILON = ILON + 1
ENDDO

ILAT = SIZE (ZLLABC, 2) / ILON

BLOCK
  TYPE (ATLAS_CONFIG) :: YLCFGR, YLCFDO

  YLCFGR = ATLAS_CONFIG ()
  YLCFDO = ATLAS_CONFIG ()

  CALL YLCFGR%SET ("nx", ILON)
  CALL YLCFGR%SET ("ny", ILAT)
  CALL YLCFGR%SET ("type", "regular_lonlat")
  CALL YLCFDO%SET ("type", "global") 
  CALL YLCFDO%SET ("west", ZLLABC (1, 1))

  CALL YLCFDO%SET ("units", "degrees")
  CALL YLCFGR%SET ("domain", YLCFDO)

  YLGRID = ATLAS_STRUCTUREDGRID (YLCFGR)

  CALL YLCFGR%FINAL ()
  CALL YLCFDO%FINAL ()
ENDBLOCK

CALL YLGRID%RETURN ()

END FUNCTION

SUBROUTINE OPEN (THIS, CDFILE, YDGRID)
CLASS (ATLAS_FMT_OZONE_t),    INTENT (INOUT) :: THIS
CHARACTER (LEN=*),            INTENT (IN)    :: CDFILE
CLASS (ATLAS_STRUCTUREDGRID), INTENT (IN)    :: YDGRID
THIS%CLFILE = CDFILE
END SUBROUTINE OPEN

SUBROUTINE CLOSE (THIS)
CLASS (ATLAS_FMT_OZONE_t),    INTENT (INOUT) :: THIS
THIS%CLFILE = ''
END SUBROUTINE


SUBROUTINE READ (THIS, CDNAME, PFLDG, LDUNDEF, PUNDEF)
CLASS (ATLAS_FMT_OZONE_t),  INTENT (IN)    :: THIS
CHARACTER (LEN=*),          INTENT (IN)    :: CDNAME
REAL (KIND=JPRB),           INTENT (OUT)   :: PFLDG (:)
LOGICAL,                    INTENT (INOUT) :: LDUNDEF
REAL (KIND=JPRB),           INTENT (INOUT) :: PUNDEF

REAL (KIND=JPRB), ALLOCATABLE :: ZLLABC (:, :)
INTEGER (KIND=JPIM) :: IND, IMONTH
CHARACTER (LEN=4) :: CLMONTH

IF (LCMPM (CDNAME, 'a', IMONTH)) THEN
  IND = 3
ELSEIF (LCMPM (CDNAME, 'b', IMONTH)) THEN
  IND = 4
ELSEIF (LCMPM (CDNAME, 'c', IMONTH)) THEN
  IND = 5
ELSE
  CALL ABORT ('ATLAS_FMT_OZONE:READ: UNEXPECTED FIELD '//TRIM (CDNAME))
ENDIF

WRITE (CLMONTH, '(".m",I2.2)') IMONTH

CALL READLLABC (TRIM (THIS%CLFILE)//CLMONTH, ZLLABC)

PFLDG = ZLLABC (IND, :)

CONTAINS

LOGICAL FUNCTION LCMPM (CDNAME1, CDNAME2, IMONTH) 

CHARACTER (LEN=*),   INTENT (IN)  :: CDNAME1, CDNAME2
INTEGER (KIND=JPIM), INTENT (OUT) :: IMONTH

INTEGER (KIND=JPIM) :: ILEN1, ILEN2

IMONTH = -1
LCMPM = .FALSE.

ILEN1 = LEN_TRIM (CDNAME1)
ILEN2 = LEN_TRIM (CDNAME2)

IF (ILEN1 /= ILEN2 + 4) RETURN
IF (CDNAME1 (1:ILEN2) /= TRIM (CDNAME2)) RETURN
IF (CDNAME1 (ILEN2+1:ILEN2+2) /= '.m') RETURN

READ (CDNAME1 (ILEN2+3:ILEN1), *) IMONTH

IF ((IMONTH < 1) .OR. (12 < IMONTH)) RETURN

LCMPM = .TRUE.

END FUNCTION LCMPM


END SUBROUTINE

SUBROUTINE WRITE (THIS, CDNAME, PFLDG, LDUNDEF, PUNDEF)
CLASS (ATLAS_FMT_OZONE_t), INTENT (IN)    :: THIS
CHARACTER (LEN=*),         INTENT (IN)    :: CDNAME
REAL (KIND=JPRB),          INTENT (IN)    :: PFLDG (:)
LOGICAL,                   INTENT (INOUT) :: LDUNDEF
REAL (KIND=JPRB),          INTENT (INOUT) :: PUNDEF

CALL ABORT ('ATLAS_FMT_OZONE:WRITE NOT IMPLEMENTED')

END SUBROUTINE

END MODULE

