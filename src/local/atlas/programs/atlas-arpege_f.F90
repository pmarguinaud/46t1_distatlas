PROGRAM ATLAS_ARPEGE_F

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATION4_MOD
USE INTERPOLATIONA_MOD
USE ATLAS_ARPEGE_MODULE, ONLY : WFA, GFA
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  
USE ATLAS_TRACE_MODULE
USE XRD_GETOPTIONS

!

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID2
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC2
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST2
TYPE (INTERPOLATION4)                        :: YLINTE4
TYPE (INTERPOLATIONA)                        :: YLINTEA
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2IA
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2I4
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM
TYPE (ATLAS_TRACE)                           :: YLTRAC

INTEGER (KIND=JPIM) :: I
INTEGER (KIND=JPIM) :: NPROC, IRANK

CHARACTER (LEN=128) :: CLGRID1, CLGRID2, CLDIST1, CLDIST2
LOGICAL :: LLWRITE1, LLWRITE2, LLINTERP4, LLINTERPA, LLLIGHT1, LLLIGHT2
INTEGER (KIND=JPIM) :: IBLOCK1, IBLOCK2

CALL INITOPTIONS 

CLGRID1 =        "L80x40"; CALL GETOPTION ('--grid1', CLGRID1)

CLGRID2 =           "N16"; CALL GETOPTION ('--grid2', CLGRID2)

CLDIST1 =  "checkerboard"; CALL GETOPTION ('--dist1', CLDIST1)

CLDIST2 = "equal_regions"; CALL GETOPTION ('--dist2', CLDIST2)

CALL GETOPTION ('--light1',  LLLIGHT1 )
IBLOCK1 = -1
CALL GETOPTION ('--block1',  IBLOCK1  )

CALL GETOPTION ('--light2',  LLLIGHT2 )
IBLOCK2 = -1
CALL GETOPTION ('--block2',  IBLOCK2  )

CALL GETOPTION ('--write1',  LLWRITE1 )
CALL GETOPTION ('--write2',  LLWRITE2 )

CALL GETOPTION ('--interp4', LLINTERP4)
CALL GETOPTION ('--interpA', LLINTERPA)

IF ((.NOT. LLINTERP4) .AND. (.NOT. LLINTERPA)) THEN
  LLINTERP4 = .TRUE.
ENDIF

CALL CHECKOPTIONS

CALL ATLAS_LIBRARY%INITIALISE ()

YLTRAC = ATLAS_TRACE (__FILE__, __LINE__, "MAIN")



YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()
IRANK  = YLCOMM%RANK ()

CALL LINUX_BIND (IRANK, NPROC)

CALL CREATE_GRID (YLGRID1, CLGRID1)
CALL CREATE_GRID (YLGRID2, CLGRID2)
CALL CREATE_DIST (YLGRID1, YLDIST1, CLDIST1, LLLIGHT1, IBLOCK1)
CALL CREATE_DIST (YLGRID2, YLDIST2, CLDIST2, LLLIGHT2, IBLOCK2)

YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1)
YLFSSC2 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID2, YLDIST2)

IF (LLINTERP4) YLINTE4 = INTERPOLATION4 (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)
IF (LLINTERPA) YLINTEA = INTERPOLATIONA (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)

CALL GETXYZ (YLFLDS1, YLFSSC1, YLGRID1)

IF (LLINTERP4) YLFLDS2I4 = YLINTE4%INTERPOLATE (YLFLDS1)
IF (LLINTERPA) YLFLDS2IA = YLINTEA%INTERPOLATE (YLFLDS1)

IF (LLWRITE1) THEN
  CALL WFA ('XYZ1.fa' ,  YLGRID1, YLFSSC1, YLFLDS1  )
ENDIF
IF (LLWRITE2) THEN
  IF (LLINTERP4) CALL WFA ('XYZ2I4.fa', YLGRID2, YLFSSC2, YLFLDS2I4)
  IF (LLINTERPA) CALL WFA ('XYZ2IA.fa', YLGRID2, YLFSSC2, YLFLDS2IA)
ENDIF

CALL YLFLDS1  %FINAL ()
CALL YLFLDS2  %FINAL ()
CALL YLFLDS2I4%FINAL ()
CALL YLFLDS2IA%FINAL ()

IF (LLINTERP4) CALL YLINTE4%FINAL ()
IF (LLINTERPA) CALL YLINTEA%FINAL ()

CALL YLTRAC%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()


WRITE (*, *) "--- STOP ---"

CONTAINS

SUBROUTINE GETXYZ (YDFLDS, YDFSSC, YDGRID)

TYPE (ATLAS_FIELDSET)                        :: YDFLDS
TYPE (ATLAS_STRUCTUREDGRID)                  :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YDFSSC

TYPE (ATLAS_FIELD) :: YLX, YLY, YLZ
REAL (KIND=JPRB) :: LONLAT (2), COSLON, SINLON, COSLAT, SINLAT
REAL (KIND=JPRB), POINTER :: X (:), Y (:), Z (:)
INTEGER (KIND=JPIM) :: IX, IY, JLOC
TYPE (ATLAS_METADATA) :: YLMETX
TYPE (ATLAS_METADATA) :: YLMETY
TYPE (ATLAS_METADATA) :: YLMETZ

YDFLDS = ATLAS_FIELDSET ()

YLX = ATLAS_FIELD (NAME="X", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLY = ATLAS_FIELD (NAME="Y", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLZ = ATLAS_FIELD (NAME="Z", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])

YLMETX = YLX%METADATA ()
YLMETY = YLY%METADATA ()
YLMETZ = YLZ%METADATA ()

CALL YLMETX%SET ("INIVAU", 0); CALL YLMETX%SET ("CLPREF", "SURF"); CALL YLMETX%SET ("CLSUFF", "X")
CALL YLMETY%SET ("INIVAU", 0); CALL YLMETY%SET ("CLPREF", "SURF"); CALL YLMETY%SET ("CLSUFF", "Y")
CALL YLMETZ%SET ("INIVAU", 0); CALL YLMETZ%SET ("CLPREF", "SURF"); CALL YLMETZ%SET ("CLSUFF", "Z")

CALL YLX%DATA (X)
CALL YLY%DATA (Y)
CALL YLZ%DATA (Z)

JLOC = 1
DO IY = YDFSSC%J_BEGIN (), YDFSSC%J_END ()
  DO IX = YDFSSC%I_BEGIN (IY), YDFSSC%I_END (IY)
    LONLAT = YDGRID%LONLAT (IX, IY) * DEG2RAD
    COSLON = COS (LONLAT (1)); SINLON = SIN (LONLAT (1))
    COSLAT = COS (LONLAT (2)); SINLAT = SIN (LONLAT (2))
    X (JLOC) = COSLON * COSLAT
    Y (JLOC) = SINLON * COSLAT
    Z (JLOC) =          SINLAT
    JLOC = JLOC + 1
  ENDDO
ENDDO

CALL YDFLDS%ADD (YLX)
CALL YDFLDS%ADD (YLY)
CALL YDFLDS%ADD (YLZ)

CALL YLMETX%FINAL ()
CALL YLMETY%FINAL ()
CALL YLMETZ%FINAL ()


END SUBROUTINE

SUBROUTINE CREATE_GRID (YDGRID, CDGRID)

TYPE (ATLAS_STRUCTUREDGRID) :: YDGRID
CHARACTER (LEN=*)           :: CDGRID

LOGICAL :: LLEXIST

TYPE GRID_OPTIONS_t
  INTEGER (KIND=JPIM) :: NLOEN (30000) = 0
  INTEGER (KIND=JPIM) :: NDLON = 0, NDGLG = 0
  LOGICAL :: LELAM    = .FALSE.
  LOGICAL :: LROTATED = .FALSE. 
  LOGICAL :: LSTRETCH = .FALSE.
  REAL (KIND=JPRB) :: RSTRETCH = 1._JPRB
  REAL (KIND=JPRB) :: RLONCENT = 0._JPRB  ! Degrees
  REAL (KIND=JPRB) :: RLATCENT = 90._JPRB ! Degrees

  REAL (KIND=JPRB) :: DXINMETRES = 10000._JPRB
  REAL (KIND=JPRB) :: DYINMETRES = 10000._JPRB

  REAL (KIND=JPRB) :: LADINDEGREES    = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN1INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN2INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LOVINDEGREES    =  2.0_JPRB
  INTEGER (KIND=JPIM) :: NUX, NUY

END TYPE GRID_OPTIONS_t

TYPE (GRID_OPTIONS_t) :: YLGROPT

NAMELIST / NAMGRID / YLGROPT

INQUIRE (FILE=CDGRID, EXIST=LLEXIST)

IF (LLEXIST) THEN

  IF (CDGRID (1:6) == "fort.4") THEN
    OPEN (4, FILE=TRIM (CDGRID), FORM="FORMATTED")
    READ (4, NAMGRID)
    CLOSE (4)

    IF (YLGROPT%LELAM) THEN
      YDGRID = ATLAS_LAMBERTREGIONALGRID (YLGROPT%NDLON, YLGROPT%NDGLG, &
                                        & -YLGROPT%NUX / 2 * YLGROPT%DXINMETRES, &
                                        & -YLGROPT%NUY / 2 * YLGROPT%DYINMETRES, &
                                        & YLGROPT%DXINMETRES, YLGROPT%DYINMETRES, &
                                        & YLGROPT%LOVINDEGREES, YLGROPT%LADINDEGREES, &
                                        & YLGROPT%LATIN1INDEGREES, YLGROPT%LATIN2INDEGREES)
    ELSE
      IF (YLGROPT%RLONCENT /= 0._JPRB .OR. YLGROPT%RLATCENT /= 90._JPRB) THEN
        YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG), &
                & [YLGROPT%RLONCENT, YLGROPT%RLATCENT], YLGROPT%RSTRETCH)
      ELSE
        YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG))
      ENDIF
    ENDIF


  ELSE
    CALL GFA (CDGRID, YDGRID)
  ENDIF
ELSE
  IF (CDGRID (1:1) == "X") THEN
    BLOCK
      INTEGER (KIND=JPIM) :: J, NX, NY
      TYPE (ATLAS_CONFIG) :: YLCFGR, YLCFDO

      DO J = 2, LEN_TRIM (CDGRID)
        IF (CDGRID (J:J) == "x") GOTO 100
      ENDDO
      STOP
100   CONTINUE

      READ (CDGRID (2:J-1), *) NX
      READ (CDGRID (J+1: ), *) NY

      YLCFGR = ATLAS_CONFIG ()
      YLCFDO = ATLAS_CONFIG ()
      CALL YLCFGR%SET ("nx", NX)
      CALL YLCFGR%SET ("ny", NY)
      CALL YLCFGR%SET ("type", "shifted_lonlat")
      CALL YLCFDO%SET ("type", "rectangular")
      CALL YLCFDO%SET ("units", "degrees")
      CALL YLCFDO%SET ("xmin", -180.0_JPRB)
      CALL YLCFDO%SET ("xmax", +180.0_JPRB)
      CALL YLCFDO%SET ("ymin", - 90.0_JPRB)
      CALL YLCFDO%SET ("ymax", + 90.0_JPRB)
      CALL YLCFGR%SET ("domain", YLCFDO)

      YDGRID = ATLAS_STRUCTUREDGRID (YLCFGR)
      
      CALL YLCFGR%FINAL ()
      CALL YLCFDO%FINAL ()
    ENDBLOCK
  ELSE
    YDGRID = ATLAS_STRUCTUREDGRID (CDGRID)
  ENDIF
ENDIF

END SUBROUTINE

SUBROUTINE CREATE_DIST (YDGRID, YDDIST, CDDIST, LDLIGHT, KBLOCKSIZE)

TYPE (ATLAS_STRUCTUREDGRID)   :: YDGRID
TYPE (ATLAS_GRIDDISTRIBUTION) :: YDDIST
CHARACTER (LEN=*)             :: CDDIST
LOGICAL, OPTIONAL             :: LDLIGHT
INTEGER (KIND=JPIM), OPTIONAL :: KBLOCKSIZE

#include "abor1.intfb.h"

TYPE (ATLAS_CONFIG) :: YLCONF

LOGICAL             :: LLLIGHT
INTEGER (KIND=JPIM) :: IBLOCKSIZE

LLLIGHT = .FALSE.
IF (PRESENT (LDLIGHT)) THEN
  LLLIGHT = LDLIGHT
  IF (.NOT. PRESENT (KBLOCKSIZE)) STOP
  IBLOCKSIZE = KBLOCKSIZE
ENDIF

YLCONF = ATLAS_CONFIG() 

SELECT CASE (CDDIST)
  CASE ('checkerboard') 
    CALL YLCONF%SET ('type', 'checkerboard')
    IF (LLLIGHT) THEN
      CALL YLCONF%SET ('light', .TRUE.)
      CALL YLCONF%SET ('blocksize', IBLOCKSIZE)
    ELSE
      CALL YLCONF%SET ('nbands',        NPROC)
    ENDIF
  CASE ('equal_regions')
    CALL YLCONF%SET ('type', 'equal_regions')
  CASE DEFAULT
    CALL ABOR1 ('UNKNOWN DISTRIBUTION TYPE :'//TRIM (CDDIST))
END SELECT 

YDDIST = ATLAS_GRIDDISTRIBUTION (YDGRID, YLCONF)

CALL YLCONF%FINAL ()

END SUBROUTINE

END

