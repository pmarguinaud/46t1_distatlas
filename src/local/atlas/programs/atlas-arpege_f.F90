PROGRAM ATLAS_ARPEGE_F

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATION4_MOD
USE INTERPOLATIONA_MOD
USE ATLAS_ARPEGE_MODULE, ONLY : WFA, GFA
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  
USE XRD_GETOPTIONS

!

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID2
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC2
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST2
TYPE (INTERPOLATION4)                        :: YLINTE4
TYPE (INTERPOLATIONA)                        :: YLINTEA
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2I
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM

INTEGER (KIND=JPIM) :: I
INTEGER (KIND=JPIM) :: NPROC

CHARACTER (LEN=64) :: CLGRID1, CLGRID2, CLDIST1, CLDIST2
LOGICAL :: LLWRITE1

CALL INITOPTIONS 

CLGRID1 =        "L80x40"; CALL GETOPTION ('--grid1', CLGRID1)

CLGRID2 =           "N16"; CALL GETOPTION ('--grid2', CLGRID2)

CLDIST1 =  "checkerboard"; CALL GETOPTION ('--dist1', CLDIST1)

CLDIST2 = "equal_regions"; CALL GETOPTION ('--dist2', CLDIST2)

CALL GETOPTION ('--write1', LLWRITE1)

CALL CHECKOPTIONS

CALL ATLAS_LIBRARY%INITIALISE()

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()

CALL CREATE_GRID (YLGRID1, CLGRID1)
CALL CREATE_GRID (YLGRID2, CLGRID2)
CALL CREATE_DIST (YLGRID1, YLDIST1, CLDIST1)
CALL CREATE_DIST (YLGRID2, YLDIST2, CLDIST2)

YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1)
YLFSSC2 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID2, YLDIST2)

YLINTE4 = INTERPOLATION4 (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)
!YLINTEA = INTERPOLATIONA (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)


CALL GETXYZ (YLFLDS1, YLFSSC1, YLGRID1)

!CALL GETXYZ (YLFLDS2, YLFSSC2, YLGRID2)

YLFLDS2I = YLINTE4%INTERPOLATE (YLFLDS1)
!YLFLDS2I = YLINTEA%INTERPOLATE (YLFLDS1)

IF (.FALSE.) THEN
BLOCK
  TYPE (ATLAS_FIELD) :: YLX2,  YLY2,  YLZ2
  TYPE (ATLAS_FIELD) :: YLX2I, YLY2I, YLZ2I
  REAL (KIND=JPRB), POINTER :: X2  (:), Y2  (:), Z2  (:)
  REAL (KIND=JPRB), POINTER :: X2I (:), Y2I (:), Z2I (:)
  REAL (KIND=JPRB) :: ZDX, ZDY, ZDZ
  INTEGER (KIND=JPIM) :: JLOC2

  YLX2  = YLFLDS2 %FIELD ("X")
  YLY2  = YLFLDS2 %FIELD ("Y")
  YLZ2  = YLFLDS2 %FIELD ("Z")

  YLX2I = YLFLDS2I%FIELD ("X")
  YLY2I = YLFLDS2I%FIELD ("Y")
  YLZ2I = YLFLDS2I%FIELD ("Z")

  CALL YLX2 %DATA (X2 )
  CALL YLY2 %DATA (Y2 )
  CALL YLZ2 %DATA (Z2 )
  CALL YLX2I%DATA (X2I)
  CALL YLY2I%DATA (Y2I)
  CALL YLZ2I%DATA (Z2I)
  
  DO JLOC2 = 1, YLFSSC2%SIZE_OWNED ()
    ZDX = X2  (JLOC2) - X2I (JLOC2)
    ZDY = Y2  (JLOC2) - Y2I (JLOC2)
    ZDZ = Z2  (JLOC2) - Z2I (JLOC2)
    WRITE (*, '(I8," > ",3F12.4," | ",3F12.4," | ",F12.4)') JLOC2, &
         & X2  (JLOC2), Y2  (JLOC2), Z2  (JLOC2), &
         & X2I (JLOC2), Y2I (JLOC2), Z2I (JLOC2), &
         & SQRT (ZDX * ZDX + ZDY * ZDY + ZDZ * ZDZ) * RAD2DEG
  ENDDO
ENDBLOCK
ENDIF

IF (LLWRITE1) CALL WFA ('XYZ1.fa' , YLGRID1, YLFSSC1, YLFLDS1 )
CALL WFA ('XYZ2I.fa', YLGRID2, YLFSSC2, YLFLDS2I)

CALL YLFLDS1 %FINAL ()
CALL YLFLDS2 %FINAL ()
CALL YLFLDS2I%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

CONTAINS

SUBROUTINE GETXYZ (YDFLDS, YDFSSC, YDGRID)

TYPE (ATLAS_FIELDSET)                        :: YDFLDS
TYPE (ATLAS_STRUCTUREDGRID)                  :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YDFSSC

TYPE (ATLAS_FIELD) :: YLX, YLY, YLZ
REAL (KIND=JPRB) :: LONLAT (2), COSLON, SINLON, COSLAT, SINLAT
REAL (KIND=JPRB), POINTER :: X (:), Y (:), Z (:)
INTEGER (KIND=JPIM) :: IX, IY, JLOC
TYPE (ATLAS_METADATA) :: YLMETX
TYPE (ATLAS_METADATA) :: YLMETY
TYPE (ATLAS_METADATA) :: YLMETZ

YDFLDS = ATLAS_FIELDSET ()

YLX = ATLAS_FIELD (NAME="X", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLY = ATLAS_FIELD (NAME="Y", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLZ = ATLAS_FIELD (NAME="Z", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])

YLMETX = YLX%METADATA ()
YLMETY = YLY%METADATA ()
YLMETZ = YLZ%METADATA ()

CALL YLMETX%SET ("INIVAU", 0); CALL YLMETX%SET ("CLPREF", "SURF"); CALL YLMETX%SET ("CLSUFF", "X")
CALL YLMETY%SET ("INIVAU", 0); CALL YLMETY%SET ("CLPREF", "SURF"); CALL YLMETY%SET ("CLSUFF", "Y")
CALL YLMETZ%SET ("INIVAU", 0); CALL YLMETZ%SET ("CLPREF", "SURF"); CALL YLMETZ%SET ("CLSUFF", "Z")

CALL YLX%DATA (X)
CALL YLY%DATA (Y)
CALL YLZ%DATA (Z)

JLOC = 1
DO IY = YDFSSC%J_BEGIN (), YDFSSC%J_END ()
  DO IX = YDFSSC%I_BEGIN (IY), YDFSSC%I_END (IY)
    LONLAT = YDGRID%LONLAT (IX, IY) * DEG2RAD
    COSLON = COS (LONLAT (1)); SINLON = SIN (LONLAT (1))
    COSLAT = COS (LONLAT (2)); SINLAT = SIN (LONLAT (2))
    X (JLOC) = COSLON * COSLAT
    Y (JLOC) = SINLON * COSLAT
    Z (JLOC) =          SINLAT
    JLOC = JLOC + 1
  ENDDO
ENDDO

CALL YDFLDS%ADD (YLX)
CALL YDFLDS%ADD (YLY)
CALL YDFLDS%ADD (YLZ)

CALL YLMETX%FINAL ()
CALL YLMETY%FINAL ()
CALL YLMETZ%FINAL ()

END SUBROUTINE

SUBROUTINE CREATE_GRID (YDGRID, CDGRID)

TYPE (ATLAS_STRUCTUREDGRID) :: YDGRID
CHARACTER (LEN=*)           :: CDGRID

LOGICAL :: LLEXIST

TYPE GRID_OPTIONS_t
  INTEGER (KIND=JPIM) :: NLOEN (30000) = 0
  INTEGER (KIND=JPIM) :: NDLON = 0, NDGLG = 0
  LOGICAL :: LELAM    = .FALSE.
  LOGICAL :: LROTATED = .FALSE. 
  LOGICAL :: LSTRETCH = .FALSE.
  REAL (KIND=JPRB) :: RSTRETCH = 1._JPRB
  REAL (KIND=JPRB) :: RLONCENT = 0._JPRB  ! Degrees
  REAL (KIND=JPRB) :: RLATCENT = 90._JPRB ! Degrees

  REAL (KIND=JPRB) :: DXINMETRES = 10000._JPRB
  REAL (KIND=JPRB) :: DYINMETRES = 10000._JPRB

  REAL (KIND=JPRB) :: LADINDEGREES    = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN1INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN2INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LOVINDEGREES    =  2.0_JPRB
  INTEGER (KIND=JPIM) :: NUX, NUY

END TYPE GRID_OPTIONS_t

TYPE (GRID_OPTIONS_t) :: YLGROPT

NAMELIST / NAMGRID / YLGROPT

INQUIRE (FILE=CDGRID, EXIST=LLEXIST)

IF (LLEXIST) THEN
  IF (CDGRID (1:6) == "fort.4") THEN
    OPEN (4, FILE=TRIM (CDGRID), FORM="FORMATTED")
    READ (4, NAMGRID)
    CLOSE (4)

    IF (YLGROPT%LELAM) THEN
      YDGRID = ATLAS_LAMBERTREGIONALGRID (YLGROPT%NDLON, YLGROPT%NDGLG, &
                                        & -YLGROPT%NUX / 2 * YLGROPT%DXINMETRES, &
                                        & -YLGROPT%NUY / 2 * YLGROPT%DYINMETRES, &
                                        & YLGROPT%DXINMETRES, YLGROPT%DYINMETRES, &
                                        & YLGROPT%LOVINDEGREES, YLGROPT%LADINDEGREES, &
                                        & YLGROPT%LATIN1INDEGREES, YLGROPT%LATIN2INDEGREES)
    ELSE
      IF (YLGROPT%RLONCENT /= 0._JPRB .OR. YLGROPT%RLATCENT /= 90._JPRB) THEN
        YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG), &
                & [YLGROPT%RLONCENT, YLGROPT%RLATCENT], YLGROPT%RSTRETCH)
      ELSE
        YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG))
      ENDIF
    ENDIF


  ELSE
    CALL GFA (CDGRID, YDGRID)
  ENDIF
ELSE
  YDGRID = ATLAS_STRUCTUREDGRID (CDGRID)
ENDIF

END SUBROUTINE

SUBROUTINE CREATE_DIST (YDGRID, YDDIST, CDDIST)

TYPE (ATLAS_STRUCTUREDGRID)   :: YDGRID
TYPE (ATLAS_GRIDDISTRIBUTION) :: YDDIST
CHARACTER (LEN=*)             :: CDDIST

#include "abor1.intfb.h"

TYPE (ATLAS_CONFIG) :: YLCONF

YLCONF = ATLAS_CONFIG() 

SELECT CASE (CDDIST)
  CASE ('checkerboard') 
    CALL YLCONF%SET ('type', 'checkerboard')
    CALL YLCONF%SET ('nbands',        NPROC)
  CASE ('equal_regions')
    CALL YLCONF%SET ('type', 'equal_regions')
  CASE DEFAULT
    CALL ABOR1 ('UNKNOWN DISTRIBUTION TYPE :'//TRIM (CDDIST))
END SELECT 

YDDIST = ATLAS_GRIDDISTRIBUTION (YDGRID, YLCONF)

CALL YLCONF%FINAL ()

END SUBROUTINE

END

