PROGRAM ATLAS_ARPEGE_F

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATION4_MOD
USE ATLAS_ARPEGE_MODULE, ONLY : WFA
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  

!

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID2
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC2
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST2
TYPE (ATLAS_CONFIG)                          :: YLCONF1
TYPE (ATLAS_CONFIG)                          :: YLCONF2
TYPE (INTERPOLATION4)                        :: YLINTER
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2I
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM

INTEGER (KIND=JPIM) :: I
INTEGER (KIND=JPIM) :: NPROC

CALL ATLAS_LIBRARY%INITIALISE()

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()

YLGRID1 = ATLAS_STRUCTUREDGRID ("L80x40")
YLGRID2 = ATLAS_STRUCTUREDGRID ("N16")

YLCONF1 = ATLAS_CONFIG(); CALL YLCONF1%SET ('type', 'checkerboard'); CALL YLCONF1%SET ('nbands', NPROC)
YLCONF2 = ATLAS_CONFIG(); CALL YLCONF2%SET ('type', 'equal_regions')

YLDIST1 = ATLAS_GRIDDISTRIBUTION (YLGRID1, YLCONF1)
YLDIST2 = ATLAS_GRIDDISTRIBUTION (YLGRID2, YLCONF2)

YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1)
YLFSSC2 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID2, YLDIST2)

YLINTER = INTERPOLATION4 (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)


CALL GETXYZ (YLFLDS1, YLFSSC1, YLGRID1)
CALL GETXYZ (YLFLDS2, YLFSSC2, YLGRID2)

YLFLDS2I = YLINTER%INTERPOLATE (YLFLDS1)

BLOCK
  TYPE (ATLAS_FIELD) :: YLX2,  YLY2,  YLZ2
  TYPE (ATLAS_FIELD) :: YLX2I, YLY2I, YLZ2I
  REAL (KIND=JPRB), POINTER :: X2  (:), Y2  (:), Z2  (:)
  REAL (KIND=JPRB), POINTER :: X2I (:), Y2I (:), Z2I (:)
  REAL (KIND=JPRB) :: ZDX, ZDY, ZDZ
  INTEGER (KIND=JPIM) :: JLOC2

  YLX2  = YLFLDS2 %FIELD ("X")
  YLY2  = YLFLDS2 %FIELD ("Y")
  YLZ2  = YLFLDS2 %FIELD ("Z")

  YLX2I = YLFLDS2I%FIELD ("X")
  YLY2I = YLFLDS2I%FIELD ("Y")
  YLZ2I = YLFLDS2I%FIELD ("Z")

  CALL YLX2 %DATA (X2 )
  CALL YLY2 %DATA (Y2 )
  CALL YLZ2 %DATA (Z2 )
  CALL YLX2I%DATA (X2I)
  CALL YLY2I%DATA (Y2I)
  CALL YLZ2I%DATA (Z2I)
  
  DO JLOC2 = 1, YLFSSC2%SIZE_OWNED ()
    ZDX = X2  (JLOC2) - X2I (JLOC2)
    ZDY = Y2  (JLOC2) - Y2I (JLOC2)
    ZDZ = Z2  (JLOC2) - Z2I (JLOC2)
    WRITE (*, '(I8," > ",3F12.4," | ",3F12.4," | ",F12.4)') JLOC2, &
         & X2  (JLOC2), Y2  (JLOC2), Z2  (JLOC2), &
         & X2I (JLOC2), Y2I (JLOC2), Z2I (JLOC2), &
         & SQRT (ZDX * ZDX + ZDY * ZDY + ZDZ * ZDZ) * RAD2DEG
  ENDDO


ENDBLOCK

CALL YLFLDS1 %FINAL ()
CALL YLFLDS2 %FINAL ()
CALL YLFLDS2I%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

CONTAINS

SUBROUTINE GETXYZ (YDFLDS, YDFSSC, YDGRID)

TYPE (ATLAS_FIELDSET)                        :: YDFLDS
TYPE (ATLAS_STRUCTUREDGRID)                  :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YDFSSC

TYPE (ATLAS_FIELD) :: YLX, YLY, YLZ
REAL (KIND=JPRB) :: LONLAT (2), COSLON, SINLON, COSLAT, SINLAT
REAL (KIND=JPRB), POINTER :: X (:), Y (:), Z (:)
INTEGER (KIND=JPIM) :: IX, IY, JLOC

YDFLDS = ATLAS_FIELDSET ()

YLX = ATLAS_FIELD (NAME="X", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLY = ATLAS_FIELD (NAME="Y", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])
YLZ = ATLAS_FIELD (NAME="Z", KIND=JPRB, SHAPE=[YDFSSC%SIZE_OWNED ()])

CALL YLX%DATA (X)
CALL YLY%DATA (Y)
CALL YLZ%DATA (Z)

JLOC = 1
DO IY = YDFSSC%J_BEGIN (), YDFSSC%J_END ()
  DO IX = YDFSSC%I_BEGIN (IY), YDFSSC%I_END (IY)
    LONLAT = YDGRID%LONLAT (IX, IY) * DEG2RAD
    COSLON = COS (LONLAT (1)); SINLON = SIN (LONLAT (1))
    COSLAT = COS (LONLAT (2)); SINLAT = SIN (LONLAT (2))
    X (JLOC) = COSLON * COSLAT
    Y (JLOC) = SINLON * COSLAT
    Z (JLOC) =          SINLAT
    JLOC = JLOC + 1
  ENDDO
ENDDO

CALL YDFLDS%ADD (YLX)
CALL YDFLDS%ADD (YLY)
CALL YDFLDS%ADD (YLZ)

END SUBROUTINE


END

