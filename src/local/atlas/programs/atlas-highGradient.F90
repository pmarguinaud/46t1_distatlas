PROGRAM ATLAS_HIGHGRADIENT

USE PARKIND1, ONLY : JPIM, JPRB
USE HIGHGRADIENTREGIONAL_MOD
USE ATLAS_FMT_FA, ONLY : ISMGLO, ISMLAM, ISLTLN
USE ATLAS_IO_GATHSCAT, ONLY : ATLAS_IO_GATHSCAT_t
USE ATLAS_FMT_FA, ONLY : ATLAS_FMT_FA_t
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  
USE ATLAS_TRACE_MODULE
USE XRD_GETOPTIONS

#include "atlas-abort.h"

!

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1GR
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM
TYPE (ATLAS_TRACE)                           :: YLTRAC
TYPE (ATLAS_IO_GATHSCAT_t)                   :: YLFAIO
TYPE (ATLAS_FMT_FA_t), TARGET                :: YLFMFA

INTEGER (KIND=JPIM) :: I
INTEGER (KIND=JPIM) :: NPROC, IRANK

CHARACTER (LEN=128) :: CLGRID1
INTEGER (KIND=JPIM) :: IXY1, IORDER

CALL INITOPTIONS 
IXY1 = 0
CALL GETOPTION ('--xy',    IXY1)
IORDER = 1
CALL GETOPTION ('--order', IORDER)
CALL GETOPTION ('--grid', CLGRID1)
CALL CHECKOPTIONS

YLFAIO = ATLAS_IO_GATHSCAT_t (YLFMFA)

CALL ATLAS_LIBRARY%INITIALISE ()

YLTRAC = ATLAS_TRACE (__FILE__, __LINE__, "MAIN")

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()
IRANK  = YLCOMM%RANK ()

CALL LINUX_BIND (IRANK, NPROC)

CALL CREATE_GRID (YLGRID1, CLGRID1)

CALL CREATE_DIST (YLGRID1, YLDIST1, .FALSE.)

BLOCK
  TYPE (ATLAS_CONFIG) :: YLCONF
  YLCONF = ATLAS_CONFIG ()
  CALL YLCONF%SET ("halo", 5)
  CALL YLCONF%SET ("periodic_x", .TRUE.)
  CALL YLCONF%SET ("periodic_y", .TRUE.)
  YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1, YLCONF)
  CALL YLCONF%FINAL ()
ENDBLOCK

CALL GETXYZ (YLFLDS1, YLFSSC1, YLGRID1, IXY1)

YLFLDS1GR = HIGHGRADIENTREGIONAL (YLFSSC1, YLFLDS1, IORDER)

CALL YLFAIO%WRITE ('XYZ1.fa', YLFLDS1, YLFSSC1)
CALL YLFAIO%WRITE ('XYZ1GR.fa', YLFLDS1GR, YLFSSC1)


CALL YLFLDS1  %FINAL ()
CALL YLFLDS1GR%FINAL ()

CALL YLTRAC%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()


WRITE (*, *) "--- STOP ---"

CONTAINS

SUBROUTINE GETXYZ (YDFLDS, YDFSSC, YDGRID, KXY)

TYPE (ATLAS_FIELDSET)                        :: YDFLDS
TYPE (ATLAS_STRUCTUREDGRID)                  :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YDFSSC
INTEGER (KIND=JPIM)                          :: KXY

TYPE (ATLAS_FIELD) :: YLX, YLY, YLZ
REAL (KIND=JPRB) :: LONLAT (2), ZXMIN, ZXMAX, ZYMIN, ZYMAX, ZX, ZY
REAL (KIND=JPRB) :: TMP1,TMP2, COSLON, SINLON, COSLAT, SINLAT
REAL (KIND=JPRB), POINTER :: X (:), Y (:), Z (:)
INTEGER (KIND=JPIM) :: IX, IY, JLOC, INX, INY

YDFLDS = ATLAS_FIELDSET ()

IF (KXY == 0) THEN
YLX = ATLAS_FIELD (NAME="SURFX", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLX%DATA (X); CALL YDFLDS%ADD (YLX)
YLY = ATLAS_FIELD (NAME="SURFY", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLY%DATA (Y); CALL YDFLDS%ADD (YLY)
YLZ = ATLAS_FIELD (NAME="SURFZ", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLZ%DATA (Z); CALL YDFLDS%ADD (YLZ)
ELSEIF (KXY == 1) THEN
YLX = ATLAS_FIELD (NAME="SURFX", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLX%DATA (X); CALL YDFLDS%ADD (YLX)
YLY = ATLAS_FIELD (NAME="SURFY", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLY%DATA (Y); CALL YDFLDS%ADD (YLY)
ELSE
YLX = ATLAS_FIELD (NAME="SURFX", KIND=JPRB, SHAPE=[YDFSSC%SIZE ()]); CALL YLX%DATA (X); CALL YDFLDS%ADD (YLX)
ENDIF


INX = YDFSSC%I_END (1)
INY = YDFSSC%J_END ()

LONLAT = YDGRID%XY (  1,   1); ZXMIN = LONLAT (1); ZYMIN = LONLAT (2)
LONLAT = YDGRID%XY (INX, INY); ZXMAX = LONLAT (1); ZYMAX = LONLAT (2)

ZXMAX = ZXMAX + (ZXMAX - ZXMIN) / REAL (INX-1, JPRB)
ZYMAX = ZYMAX + (ZYMAX - ZYMIN) / REAL (INY-1, JPRB)

JLOC = 1
DO IY = YDFSSC%J_BEGIN (), YDFSSC%J_END ()
  DO IX = YDFSSC%I_BEGIN (IY), YDFSSC%I_END (IY)
    IF (KXY == 0) THEN
      LONLAT = YDGRID%LONLAT (IX, IY) * DEG2RAD
      LONLAT = YDGRID%LONLAT (IX, IY) * DEG2RAD
      COSLON = COS (LONLAT (1)); SINLON = SIN (LONLAT (1))
      COSLAT = COS (LONLAT (2)); SINLAT = SIN (LONLAT (2))
      X (JLOC) = COSLON * COSLAT
      Y (JLOC) = SINLON * COSLAT
      Z (JLOC) =          SINLAT
    ELSEIF (KXY == 1) THEN
      LONLAT = YDGRID%XY (IX, IY)
      X (JLOC) = LONLAT (1)
      Y (JLOC) = LONLAT (2)
    ELSE
      LONLAT = YDGRID%XY (IX, IY)
      ZX = LONLAT (1)
      ZY = LONLAT (2)
      X (JLOC) = ((ZX - ZXMIN) * (ZXMAX - ZX) * (ZY - ZYMIN) * (ZYMAX - ZY)) ** 2 &
               & / ((ZXMAX - ZXMIN) * (ZYMAX - ZYMIN)) ** 4
    ENDIF
    JLOC = JLOC + 1
  ENDDO
ENDDO


END SUBROUTINE

SUBROUTINE CREATE_GRID (YDGRID, CDGRID)

TYPE (ATLAS_STRUCTUREDGRID) :: YDGRID
CHARACTER (LEN=*)           :: CDGRID

TYPE GRID_OPTIONS_t
  INTEGER (KIND=JPIM) :: NLOEN (30000) = 0
  INTEGER (KIND=JPIM) :: NDLON = 0, NDGLG = 0
  LOGICAL :: LELAM    = .FALSE.
  LOGICAL :: LROTATED = .FALSE. 
  LOGICAL :: LSTRETCH = .FALSE.
  REAL (KIND=JPRB) :: RSTRETCH = 1._JPRB
  REAL (KIND=JPRB) :: RLONCENT = 0._JPRB  ! Degrees
  REAL (KIND=JPRB) :: RLATCENT = 90._JPRB ! Degrees

  REAL (KIND=JPRB) :: DXINMETRES = 10000._JPRB
  REAL (KIND=JPRB) :: DYINMETRES = 10000._JPRB

  REAL (KIND=JPRB) :: LADINDEGREES    = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN1INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN2INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LOVINDEGREES    =  2.0_JPRB
  INTEGER (KIND=JPIM) :: NUX, NUY

END TYPE GRID_OPTIONS_t

TYPE (GRID_OPTIONS_t) :: YLGROPT

NAMELIST / NAMGRID / YLGROPT

IF (INDEX (CDGRID, "fort.4") > 0) THEN
  OPEN (4, FILE=TRIM (CDGRID), FORM="FORMATTED")
  READ (4, NAMGRID)
  CLOSE (4)

  IF (YLGROPT%LELAM) THEN

    YDGRID = ATLAS_REGIONALGRID (NX=YLGROPT%NDLON, NY=YLGROPT%NDGLG,                                      &
           & XY_MIN=[-YLGROPT%NUX / 2 * YLGROPT%DXINMETRES, -YLGROPT%NUY / 2 * YLGROPT%DYINMETRES],       &
           & DX=YLGROPT%DXINMETRES, DY=YLGROPT%DYINMETRES,                                                &
           & PROJECTION=ATLAS_LAMBERTCONFORMALCONICPROJECTION (YLGROPT%LOVINDEGREES,YLGROPT%LADINDEGREES, &
           & YLGROPT%LATIN1INDEGREES, YLGROPT%LATIN2INDEGREES))


  ELSE
    IF (YLGROPT%RLONCENT /= 0._JPRB .OR. YLGROPT%RLATCENT /= 90._JPRB) THEN
      YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG),  &
             & PROJECTION=ATLAS_ROTATEDSCHMIDTPROJECTION (YLGROPT%RSTRETCH, &
             & [YLGROPT%RLONCENT, YLGROPT%RLATCENT], 180._JPRB))
    ELSE
      YDGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG))
    ENDIF
  ENDIF


ELSE
  YDGRID = YLFAIO%GRID (CDGRID)
ENDIF

END SUBROUTINE

SUBROUTINE CREATE_DIST (YDGRID, YDDIST, LDLIGHT)

TYPE (ATLAS_STRUCTUREDGRID)   :: YDGRID
TYPE (ATLAS_GRIDDISTRIBUTION) :: YDDIST
LOGICAL, OPTIONAL             :: LDLIGHT

TYPE (ATLAS_CONFIG) :: YLCONF

CHARACTER (LEN=64)  :: CLDIST
LOGICAL             :: LLLIGHT

IF (ISMGLO (YDGRID)) THEN
  CLDIST = "equal_regions"
ELSE
  CLDIST = "checkerboard"
ENDIF

LLLIGHT = .FALSE.
IF (PRESENT (LDLIGHT)) LLLIGHT = LDLIGHT

YLCONF = ATLAS_CONFIG() 

SELECT CASE (CLDIST)
  CASE ('checkerboard') 
    CALL YLCONF%SET ('type', 'checkerboard')
    IF (LLLIGHT) THEN
      CALL YLCONF%SET ('light', .TRUE.)
      CALL YLCONF%SET ('blocksize', INT (YDGRID%NX (1)))
    ELSE
      CALL YLCONF%SET ('nbands', NPROC)
    ENDIF
  CASE ('equal_regions')
    CALL YLCONF%SET ('type', 'equal_regions')
  CASE DEFAULT
    CALL ABORT ('UNKNOWN DISTRIBUTION TYPE :'//TRIM (CLDIST))
END SELECT 

YDDIST = ATLAS_GRIDDISTRIBUTION (YDGRID, YLCONF)

CALL YLCONF%FINAL ()

END SUBROUTINE

END

