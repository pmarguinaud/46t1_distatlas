PROGRAM ATLAS_ARPEGE_F

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATION4_MOD
USE INTERPOLATIONA_MOD
USE GRADIENT_MOD
USE ATLAS_FMT_FA, ONLY : ISMGLO, ISMLAM, ISLTLN
USE ATLAS_IO_GATHSCAT, ONLY : ATLAS_IO_GATHSCAT_t
USE ATLAS_FMT_FA, ONLY : ATLAS_FMT_FA_t
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  
USE ATLAS_TRACE_MODULE
USE XRD_GETOPTIONS

#include "atlas-abort.h"

!

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID2
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC2
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST2
TYPE (INTERPOLATION4)                        :: YLINTE4
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM
TYPE (ATLAS_IO_GATHSCAT_t)                   :: YLFAIO
TYPE (ATLAS_FMT_FA_t), TARGET                :: YLFMFA

INTEGER (KIND=JPIM) :: NPROC, IRANK

CHARACTER (LEN=16),  POINTER :: CLNOMA (:)

CHARACTER (LEN=128) :: CLFILE1, CLFILE2

CALL INITOPTIONS 

CALL GETOPTION ('--fa-file-1', CLFILE1)

CLNOMA => NULL ()
CALL GETOPTION ('--fields', CLNOMA)

CALL GETOPTION ('--fa-file-2', CLFILE2)

CALL CHECKOPTIONS

YLFAIO = ATLAS_IO_GATHSCAT_t (YLFMFA)

CALL ATLAS_LIBRARY%INITIALISE ()

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()
IRANK  = YLCOMM%RANK ()

CALL LINUX_BIND (IRANK, NPROC)

YLGRID1 = YLFAIO%GRID (CLFILE1)
YLGRID2 = YLFAIO%GRID (CLFILE2)

CALL CREATE_DIST (YLGRID1, YLDIST1)
CALL CREATE_DIST (YLGRID2, YLDIST2)

YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1);
YLFSSC2 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID2, YLDIST2)

YLFLDS1 = YLFAIO%READ (CLFILE1, CLNOMA, YLFSSC1)

YLINTE4 = INTERPOLATION4 (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)

YLFLDS2 = YLINTE4%INTERPOLATE (YLFLDS1)

CALL YLFAIO%WRITE (CLFILE2, YLFLDS2, YLFSSC2)

CALL YLFLDS1%FINAL ()
CALL YLFLDS2%FINAL ()

CALL YLINTE4%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()


WRITE (*, *) "--- STOP ---"

CONTAINS

SUBROUTINE CREATE_DIST (YDGRID, YDDIST)

TYPE (ATLAS_STRUCTUREDGRID)   :: YDGRID
TYPE (ATLAS_GRIDDISTRIBUTION) :: YDDIST

TYPE (ATLAS_CONFIG) :: YLCONF

CHARACTER (LEN=64)  :: CLDIST

IF (ISMGLO (YDGRID)) THEN
  CLDIST = "equal_regions"
ELSE
  CLDIST = "checkerboard"
ENDIF

YLCONF = ATLAS_CONFIG() 

SELECT CASE (CLDIST)
  CASE ('checkerboard') 
    CALL YLCONF%SET ('type', 'checkerboard')
  CASE ('equal_regions')
    CALL YLCONF%SET ('type', 'equal_regions')
  CASE DEFAULT
    CALL ATLAS_ABORT ('UNKNOWN DISTRIBUTION TYPE :'//TRIM (CLDIST))
END SELECT 

YDDIST = ATLAS_GRIDDISTRIBUTION (YDGRID, YLCONF)

CALL YLCONF%FINAL ()

END SUBROUTINE

END

