PROGRAM ATLAS_PGD_POINTS

USE PARKIND1, ONLY : JPIM, JPRB
USE ATLAS_IO_FIELDSET, ONLY : ATLAS_IO_FIELDSET_t
USE ATLAS_DH, ONLY : ATLAS_DH_t
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE FCKIT_MPI_MODULE, ONLY : FCKIT_MPI_SETCOMMDEFAULT
USE ATLAS_MODULE  
USE ATLAS_TRACE_MODULE

!

IMPLICIT NONE

#include "atlas-pgd-impl.h"

TYPE (ATLAS_TRACE)             :: YLTRAC

INTEGER (KIND=JPIM) :: NPROC, IRANK, JLOC
INTEGER (KIND=JPIM) :: ISIZE
TYPE (FCKIT_MPI_COMM) :: YLWORLD, YLSPLIT
TYPE (ATLAS_FIELDSET) :: YLFLDSET

CALL ATLAS_LIBRARY%INITIALISE ()

YLFLDSET = ATLAS_FIELDSET ()

YLTRAC = ATLAS_TRACE (__FILE__, __LINE__, "ATLAS_PGD")

YLWORLD = FCKIT_MPI_COMM ()

NPROC  = YLWORLD%SIZE ()
IRANK  = YLWORLD%RANK ()

CALL LINUX_BIND (IRANK, NPROC)

YLSPLIT = YLWORLD%SPLIT (IRANK, "SPLIT")

CALL FCKIT_MPI_SETCOMMDEFAULT ("SPLIT")


ISIZE = 100


! Process first point without OpenMP, in order to initialise fieldset

JLOC = 1
CALL PROCESS_POINT (JLOC)


! Does not work with OpenMP yet

!#OMP PARALLEL DO PRIVATE (JLOC)
DO JLOC = 2, ISIZE
  CALL PROCESS_POINT (JLOC)
ENDDO
!#OMP END PARALLEL DO


BLOCK
INTEGER (KIND=JPIM) :: JFLD
TYPE (ATLAS_FIELD) :: YLFLD
CHARACTER (LEN=:), ALLOCATABLE :: CLNAME 
REAL (KIND=JPRB), POINTER :: ZDATA (:)
DO JFLD = 1, YLFLDSET%SIZE ()
  YLFLD = YLFLDSET%FIELD (JFLD)
  CLNAME = YLFLD%NAME ()
  CALL YLFLD%DATA (ZDATA)
  PRINT *, CLNAME 
  PRINT *, ZDATA
  PRINT *, "----"
  DEALLOCATE (CLNAME)
  CALL YLFLD%FINAL ()
ENDDO
ENDBLOCK


CALL YLTRAC%FINAL ()

CALL YLWORLD%FINAL ()
CALL YLSPLIT%FINAL ()
CALL YLFLDSET%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

WRITE (*, *) "--- STOP ---"

CONTAINS

TYPE (ATLAS_STRUCTUREDGRID) FUNCTION GRID_FROM_NAMELIST (KLOC) RESULT (YLGRID)

INTEGER (KIND=JPIM), INTENT (IN) :: KLOC

REAL (KIND=JPRB) :: ZLON (5), ZLAT (5), ZEPS
INTEGER (KIND=JPIM) :: I

ZLAT = [  46.2_JPRB, 0._JPRB, +62._JPRB, +10._JPRB, +40._JPRB ]
ZLON = [   2.0_JPRB, 0._JPRB, -46._JPRB,   0._JPRB,   4._JPRB ]
ZEPS =  0.1_JPRB

I = 1 + MODULO (KLOC-1, SIZE (ZLON))

YLGRID = ATLAS_REGIONALGRID (NX=4_JPIM, NY=4_JPIM,    &
       & NORTH=ZLAT (I) + ZEPS, WEST=ZLON (I) - ZEPS, &
       & SOUTH=ZLAT (I) - ZEPS, EAST=ZLON (I) + ZEPS)


CALL YLGRID%RETURN ()

END FUNCTION

SUBROUTINE PROCESS_POINT (KLOC)

INTEGER (KIND=JPIM), INTENT (IN) :: KLOC
TYPE (ATLAS_IO_FIELDSET_t)  :: YLIOFS
TYPE (ATLAS_STRUCTUREDGRID) :: YLGRID2
TYPE (ATLAS_DH_t)           :: YLDHIO

CALL SETBLOCK (KLOC)
CALL YLIOFS%SETUP (KSIZE=ISIZE, YDFLDSET=YLFLDSET)
YLIOFS%ILOC2 = KLOC
YLIOFS%ILOC1 = 6
YLGRID2 = GRID_FROM_NAMELIST (KLOC)
YLDHIO = ATLAS_DH_t (YLGRID2)

CALL ATLAS_PGD_IMPL (YLDHIO, YLIOFS, YLGRID2, LDOPENMP=.FALSE.)

CALL YLGRID2%FINAL ()
CALL YLIOFS%CLEAR ()

END SUBROUTINE

END

