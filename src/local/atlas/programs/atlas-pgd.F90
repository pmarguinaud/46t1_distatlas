PROGRAM ATLAS_PGD

USE PARKIND1, ONLY : JPIM, JPRB
USE ATLAS_FA, ONLY : ATLAS_FA_t
USE ATLAS_DH, ONLY : ATLAS_DH_t
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE ATLAS_MODULE  
USE ATLAS_TRACE_MODULE

!

IMPLICIT NONE

#include "atlas-pgd-impl.h"

TYPE (ATLAS_FA_t)              :: YLFAIO
TYPE (ATLAS_DH_t)              :: YLDHIO
TYPE (ATLAS_STRUCTUREDGRID)    :: YLGRID2
TYPE (ATLAS_TRACE)             :: YLTRAC

INTEGER (KIND=JPIM) :: NPROC, IRANK
TYPE (FCKIT_MPI_COMM) :: YLCOMM

CALL ATLAS_LIBRARY%INITIALISE ()

YLTRAC = ATLAS_TRACE (__FILE__, __LINE__, "ATLAS_PGD")

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()
IRANK  = YLCOMM%RANK ()

CALL LINUX_BIND (IRANK, NPROC)

YLGRID2 = GRID_FROM_NAMELIST ()

YLDHIO = ATLAS_DH_t (YLGRID2)

CALL ATLAS_PGD_IMPL (YLDHIO, YLFAIO, YLGRID2)

CALL YLTRAC%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

WRITE (*, *) "--- STOP ---"

CONTAINS

TYPE (ATLAS_STRUCTUREDGRID) FUNCTION GRID_FROM_NAMELIST () RESULT (YLGRID)

TYPE GRID_OPTIONS_t
  INTEGER (KIND=JPIM) :: NLOEN (30000) = 0
  INTEGER (KIND=JPIM) :: NDLON = 0, NDGLG = 0
  LOGICAL :: LATLON   = .FALSE.

  REAL (KIND=JPRB) :: ZLONW =   0._JPRB, ZLONE = 360._JPRB
  REAL (KIND=JPRB) :: ZLATS = -90._JPRB, ZLATN = +90._JPRB

  LOGICAL :: LELAM    = .FALSE.
  LOGICAL :: LROTATED = .FALSE. 
  LOGICAL :: LSTRETCH = .FALSE.
  REAL (KIND=JPRB) :: RSTRETCH = 1._JPRB
  REAL (KIND=JPRB) :: RLONCENT = 0._JPRB  ! Degrees
  REAL (KIND=JPRB) :: RLATCENT = 90._JPRB ! Degrees

  REAL (KIND=JPRB) :: DXINMETRES = 10000._JPRB
  REAL (KIND=JPRB) :: DYINMETRES = 10000._JPRB

  REAL (KIND=JPRB) :: LADINDEGREES    = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN1INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LATIN2INDEGREES = 46.2_JPRB
  REAL (KIND=JPRB) :: LOVINDEGREES    =  2.0_JPRB
  INTEGER (KIND=JPIM) :: NUX, NUY
END TYPE GRID_OPTIONS_t

TYPE (GRID_OPTIONS_t) :: YLGROPT

NAMELIST / NAMGRID / YLGROPT

OPEN (4, FORM="FORMATTED")
READ (4, NAMGRID)
CLOSE (4)

IF (YLGROPT%LATLON) THEN
BLOCK
  LOGICAL :: LLGLOBAL
  TYPE (ATLAS_CONFIG) :: YLCFGR, YLCFDO
  REAL (KIND=JPRB) :: ZLOND

  ZLOND = MODULO (YLGROPT%ZLONE-YLGROPT%ZLONW, 360._JPRB) / (YLGROPT%NDLON-1)

  LLGLOBAL = ABS (MOD (YLGROPT%ZLONE+ZLOND-YLGROPT%ZLONW-360._JPRB, 360._JPRB)) < 1E-6_JPRB

  YLCFGR = ATLAS_CONFIG ()
  YLCFDO = ATLAS_CONFIG ()

  IF (LLGLOBAL) THEN ! Global lat/lon
    CALL YLCFGR%SET ("nx", YLGROPT%NDLON)
    CALL YLCFGR%SET ("ny", YLGROPT%NDGLG)
    CALL YLCFGR%SET ("type", "regular_lonlat")
    IF (YLGROPT%ZLONE+ZLOND < YLGROPT%ZLONW) THEN
      YLGROPT%ZLONW = YLGROPT%ZLONW - 360._JPRB
    ENDIF
    CALL YLCFDO%SET ("type", "global") 
    CALL YLCFDO%SET ("xmin", YLGROPT%ZLONW)
    CALL YLCFDO%SET ("xmax", YLGROPT%ZLONE+ZLOND)
    CALL YLCFDO%SET ("ymin", YLGROPT%ZLATS)
    CALL YLCFDO%SET ("ymax", YLGROPT%ZLATN)
    CALL YLCFDO%SET ("units", "degrees")
    CALL YLCFGR%SET ("domain", YLCFDO)
    YLGRID = ATLAS_STRUCTUREDGRID (YLCFGR)
  ELSE ! Regional lat/lon
    IF (YLGROPT%ZLONE < YLGROPT%ZLONW) THEN
      YLGROPT%ZLONW = YLGROPT%ZLONW - 360._JPRB
    ENDIF
    YLGRID = ATLAS_LONLATREGIONALGRID (YLGROPT%NDLON, YLGROPT%NDGLG, &
                                     & YLGROPT%ZLONW, YLGROPT%ZLONE, &
                                     & YLGROPT%ZLATS, YLGROPT%ZLATN)
  ENDIF

  CALL YLCFGR%FINAL ()
  CALL YLCFDO%FINAL ()

ENDBLOCK
ELSEIF (YLGROPT%LELAM) THEN
  YLGRID = ATLAS_LAMBERTREGIONALGRID (YLGROPT%NDLON, YLGROPT%NDGLG, &
                                    & -YLGROPT%NUX / 2 * YLGROPT%DXINMETRES, &
                                    & -YLGROPT%NUY / 2 * YLGROPT%DYINMETRES, &
                                    & YLGROPT%DXINMETRES, YLGROPT%DYINMETRES, &
                                    & YLGROPT%LOVINDEGREES, YLGROPT%LADINDEGREES, &
                                    & YLGROPT%LATIN1INDEGREES, YLGROPT%LATIN2INDEGREES)
ELSE IF (YLGROPT%RLONCENT /= 0._JPRB .OR. YLGROPT%RLATCENT /= 90._JPRB) THEN
  YLGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG), &
          & [YLGROPT%RLONCENT, YLGROPT%RLATCENT], YLGROPT%RSTRETCH)
ELSE
  YLGRID = ATLAS_REDUCEDGAUSSIANGRID (YLGROPT%NLOEN (1:YLGROPT%NDGLG))
ENDIF

CALL YLGRID%RETURN ()

END FUNCTION

END

