FUNCTION ATLAS_COMPUTE_AOS (YDFSSC1, YDFSSC2, YDOROG1, YDOROG2, YDINTEA, YDINTE4, LDOPENMP) RESULT (YLFLDSAOS2)

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATIONA_MOD
USE INTERPOLATION4_MOD
USE GRADIENT_MOD
USE ATLAS_MODULE  
USE ATLAS_HELPER
IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RA = 6371229._JPRB

TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC2
TYPE (ATLAS_FIELD),                           INTENT (IN) :: YDOROG1
TYPE (ATLAS_FIELD),                           INTENT (IN) :: YDOROG2
TYPE (INTERPOLATIONA),                        INTENT (IN) :: YDINTEA
TYPE (INTERPOLATION4),                        INTENT (IN) :: YDINTE4
LOGICAL,                                      INTENT (IN) :: LDOPENMP

TYPE (ATLAS_FIELD) :: YLAOSIP1, YLAOSIM1, YLAOSJP1, YLAOSJM1
TYPE (ATLAS_FIELD) :: YLHO2IP1, YLHO2IM1, YLHO2JP1, YLHO2JM1
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZAOSIP1, ZAOSIM1, ZAOSJP1, ZAOSJM1
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZHO2IP1, ZHO2IM1, ZHO2JP1, ZHO2JM1
TYPE (ATLAS_FIELD) :: YLOROG1INT4, YLOROG1MINT4
TYPE (ATLAS_FIELD) :: YLOROG1IP, YLOROG1IM, YLOROG1JP, YLOROG1JM
TYPE (ATLAS_FIELD) :: YLIP, YLIM, YLJP, YLJM
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZOROG2
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZOROG1, ZOROG1INT4, ZOROG1MINT4
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZOROG1IP, ZOROG1IM, ZOROG1JP, ZOROG1JM
REAL (KIND=JPRB), POINTER, DIMENSION (:) :: ZIP, ZIM, ZJP, ZJM
TYPE (ATLAS_FIELDSET) :: YLFLDSHALF, YLFLDSAOS1, YLFLDSAOS2
INTEGER (KIND=JPIM) :: JFLD, JLOC1
REAL (KIND=JPRB) :: ZUNDEF
TYPE (ATLAS_METADATA) :: YLMETA

! Get undef value

YLMETA = YDOROG1%METADATA ()
CALL YLMETA%GET ("undef", ZUNDEF)
CALL YLMETA%FINAL ()

!

CALL YDOROG1%DATA (ZOROG1)
CALL YDOROG2%DATA (ZOROG2)

! Interpolate from target geometry to origin geometry (fine lat/lon)

YLOROG1MINT4 = NEWFLD (YDFSSC1, "ZS.MINT4", ZUNDEF)

YLOROG1INT4  = YDINTE4%INTERPOLATE (YDOROG2)

CALL YLOROG1INT4 %DATA (ZOROG1INT4 )
CALL YLOROG1MINT4%DATA (ZOROG1MINT4)

! Difference of original orography with interpolated orography

!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF (ZOROG1 (JLOC1) /= ZUNDEF) THEN
    ZOROG1MINT4 (JLOC1) = ZOROG1 (JLOC1) - ZOROG1INT4 (JLOC1)
  ELSE
    ZOROG1MINT4 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO

YLFLDSHALF = ATLAS_FIELDSET ()
CALL YLFLDSHALF%ADD (YLOROG1MINT4)


! Compute half gradients

YLFLDSAOS1 = HALFDIFF (YDFSSC1, YLFLDSHALF)

YLOROG1IP = YLFLDSAOS1%FIELD ("ZS.MINT4.XP"); CALL YLOROG1IP%DATA (ZOROG1IP)
YLOROG1IM = YLFLDSAOS1%FIELD ("ZS.MINT4.XM"); CALL YLOROG1IM%DATA (ZOROG1IM)
YLOROG1JP = YLFLDSAOS1%FIELD ("ZS.MINT4.YP"); CALL YLOROG1JP%DATA (ZOROG1JP)
YLOROG1JM = YLFLDSAOS1%FIELD ("ZS.MINT4.YM"); CALL YLOROG1JM%DATA (ZOROG1JM)

YLIP = YLFLDSAOS1%FIELD ("XP"); CALL YLIP%DATA (ZIP)
YLIM = YLFLDSAOS1%FIELD ("XM"); CALL YLIM%DATA (ZIM)
YLJP = YLFLDSAOS1%FIELD ("YP"); CALL YLJP%DATA (ZJP)
YLJM = YLFLDSAOS1%FIELD ("YM"); CALL YLJM%DATA (ZJM)

CALL YLFLDSAOS1%FINAL ()

YLAOSIP1 = NEWFLD (YDFSSC1, "AOSIP", ZUNDEF); CALL YLAOSIP1%DATA (ZAOSIP1)
YLAOSIM1 = NEWFLD (YDFSSC1, "AOSIM", ZUNDEF); CALL YLAOSIM1%DATA (ZAOSIM1)
YLAOSJP1 = NEWFLD (YDFSSC1, "AOSJP", ZUNDEF); CALL YLAOSJP1%DATA (ZAOSJP1)
YLAOSJM1 = NEWFLD (YDFSSC1, "AOSJM", ZUNDEF); CALL YLAOSJM1%DATA (ZAOSJM1)

! Work with data provided by the half gradient operator

!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF (ZOROG1 (JLOC1) /= ZUNDEF) THEN
    ZAOSIP1 (JLOC1) = + MAX (0._JPRB, ZOROG1IP (JLOC1) / (RA * ZIP (JLOC1)))
    ZAOSIM1 (JLOC1) = - MIN (0._JPRB, ZOROG1IM (JLOC1) / (RA * ZIM (JLOC1)))
    ZAOSJP1 (JLOC1) = + MAX (0._JPRB, ZOROG1JP (JLOC1) / (RA * ZJP (JLOC1)))
    ZAOSJM1 (JLOC1) = - MIN (0._JPRB, ZOROG1JM (JLOC1) / (RA * ZJM (JLOC1)))
  ELSE
    ZAOSIP1 (JLOC1) = ZUNDEF
    ZAOSIM1 (JLOC1) = ZUNDEF
    ZAOSJP1 (JLOC1) = ZUNDEF
    ZAOSJM1 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO

YLHO2IP1 = NEWFLD (YDFSSC1, "HO2IP", ZUNDEF); CALL YLHO2IP1%DATA (ZHO2IP1)
YLHO2IM1 = NEWFLD (YDFSSC1, "HO2IM", ZUNDEF); CALL YLHO2IM1%DATA (ZHO2IM1)
YLHO2JP1 = NEWFLD (YDFSSC1, "HO2JP", ZUNDEF); CALL YLHO2JP1%DATA (ZHO2JP1)
YLHO2JM1 = NEWFLD (YDFSSC1, "HO2JM", ZUNDEF); CALL YLHO2JM1%DATA (ZHO2JM1)

!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF ((ZOROG1 (JLOC1) /= ZUNDEF) .AND. (ZOROG1IP (JLOC1) > 0._JPRB)) THEN
    ZHO2IP1 (JLOC1) = + ZOROG1IP (JLOC1) * 0.5_JPRB      
  ELSE
    ZHO2IP1 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO

!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF ((ZOROG1 (JLOC1) /= ZUNDEF) .AND. (ZOROG1IM (JLOC1) < 0._JPRB)) THEN
    ZHO2IM1 (JLOC1) = - ZOROG1IM (JLOC1) * 0.5_JPRB       
  ELSE
    ZHO2IM1 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO
  
!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF ((ZOROG1 (JLOC1) /= ZUNDEF) .AND. (ZOROG1JP (JLOC1) > 0._JPRB)) THEN
    ZHO2JP1 (JLOC1) = + ZOROG1JP (JLOC1) * 0.5_JPRB       
  ELSE
    ZHO2JP1 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO
  
!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF ((ZOROG1 (JLOC1) /= ZUNDEF) .AND. (ZOROG1JM (JLOC1) < 0._JPRB)) THEN
    ZHO2JM1 (JLOC1) = - ZOROG1JM (JLOC1) * 0.5_JPRB      
  ELSE
    ZHO2JM1 (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO

! Interpolate AOS & HO2 

YLFLDSAOS1 = ATLAS_FIELDSET ()

CALL YLFLDSAOS1%ADD (YLAOSIP1); CALL YLFLDSAOS1%ADD (YLAOSIM1)
CALL YLFLDSAOS1%ADD (YLAOSJP1); CALL YLFLDSAOS1%ADD (YLAOSJM1)
CALL YLFLDSAOS1%ADD (YLHO2IP1); CALL YLFLDSAOS1%ADD (YLHO2IM1)
CALL YLFLDSAOS1%ADD (YLHO2JP1); CALL YLFLDSAOS1%ADD (YLHO2JM1)

YLFLDSAOS2 = YDINTEA%INTERPOLATE (YLFLDSAOS1)

DO JFLD = 1, YLFLDSAOS2%SIZE ()
BLOCK
  REAL (KIND=JPRB), POINTER :: ZDATA (:)
  TYPE (ATLAS_FIELD) :: YLFLD2
  YLFLD2 = YLFLDSAOS2%FIELD (JFLD)
  CALL YLFLD2%DATA (ZDATA)
!$OMP WORKSHARE 
  WHERE ((ZOROG2 /= ZUNDEF) .AND. (ZDATA == ZUNDEF))
    ZDATA = 0._JPRB
  ENDWHERE
!$OMP END WORKSHARE
ENDBLOCK
ENDDO

CALL YLAOSIP1%FINAL ()
CALL YLAOSIM1%FINAL ()
CALL YLAOSJP1%FINAL ()
CALL YLAOSJM1%FINAL ()
CALL YLHO2IP1%FINAL ()
CALL YLHO2IM1%FINAL ()
CALL YLHO2JP1%FINAL ()
CALL YLHO2JM1%FINAL ()

CALL YLOROG1IP%FINAL ()
CALL YLOROG1IM%FINAL ()
CALL YLOROG1JP%FINAL ()
CALL YLOROG1JM%FINAL ()

CALL YLIP%FINAL ()
CALL YLIM%FINAL ()
CALL YLJP%FINAL ()
CALL YLJM%FINAL ()

CALL YLOROG1INT4%FINAL ()
CALL YLFLDSHALF %FINAL ()
CALL YLFLDSAOS1 %FINAL ()
CALL YLFLDSAOS2 %RETURN ()

END FUNCTION ATLAS_COMPUTE_AOS

