FUNCTION ATLAS_COMPUTE_COVERS (YDFSSC1, YDFSSC2, YDCOVR1, YDINTEA, LDOPENMP) RESULT (YLFLDS2)

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATIONA_MOD
USE ATLAS_MODULE  
USE ATLAS_HELPER

#include "atlas-abort.h"

IMPLICIT NONE

TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC2
TYPE (ATLAS_FIELD),                           INTENT (IN) :: YDCOVR1
TYPE (INTERPOLATIONA),                        INTENT (IN) :: YDINTEA
LOGICAL,                                      INTENT (IN) :: LDOPENMP

INTEGER (KIND=JPIM), PARAMETER :: INCOVR = 256_JPIM

TYPE (ATLAS_FIELD) :: YLCOVR2E
INTEGER (KIND=JPIM), ALLOCATABLE :: IOFF (:), ICNT (:)
REAL (KIND=JPRB), POINTER :: ZCOVR2E (:)
INTEGER (KIND=JPIM) :: JLOC2, ICOV, II
CHARACTER (LEN=16) :: CLNOMA
TYPE (ATLAS_FIELDSET) :: YLFLDS2
TYPE (ATLAS_FIELD) :: YLFLD2 
TYPE (PTR) :: YLPT2 (INCOVR)

! Create covers

YLFLDS2 = ATLAS_FIELDSET ()

! Get shuffle metadata : for each point of the target geometry
! we get an offset and a length which allow us to access 
! reshuffled data of source geometry

IOFF = YDINTEA%GETOFF ()
ICNT = YDINTEA%GETCNT ()

! Reshuffle covers from source geometry to target geometry

YLCOVR2E = YDINTEA%SHUFFLE (YDCOVR1)

CALL YLCOVR2E%DATA (ZCOVR2E)

! Create cover fields on target geometry

DO ICOV = 1, INCOVR
  WRITE (CLNOMA, '("COVER",I3.3)') ICOV
  YLFLD2 = NEWFLD (YDFSSC2, TRIM (CLNOMA))
  CALL YLFLDS2%ADD (YLFLD2)
  CALL YLFLD2%DATA (YLPT2 (ICOV)%ZDATA)
  YLPT2 (ICOV)%ZDATA = 0._JPRB
ENDDO

! Compute cover fields on target geometry

DO JLOC2 = 1, YDFSSC2%SIZE_OWNED ()
  DO II = IOFF (JLOC2)+1, IOFF (JLOC2)+ICNT (JLOC2)
    ICOV = INT (ZCOVR2E (II))
    IF ((ICOV < 1) .OR. (ICOV > INCOVR)) THEN
      CALL ABORT ('ATLAS_PGD: UNEXPECTED COVER VALUE')
    ENDIF
    YLPT2 (ICOV)%ZDATA (JLOC2) = YLPT2 (ICOV)%ZDATA (JLOC2) + 1._JPRB
  ENDDO
  DO ICOV = 1, INCOVR
    YLPT2 (ICOV)%ZDATA (JLOC2) = YLPT2 (ICOV)%ZDATA (JLOC2) / REAL (ICNT (JLOC2), JPRB)
  ENDDO
ENDDO

! Dispose of reshuffled data

CALL YLCOVR2E%FINAL ()

CALL YLFLDS2%RETURN ()

END FUNCTION
