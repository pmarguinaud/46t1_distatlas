FUNCTION ATLAS_COMPUTE_SSO (YDFSSC1, YDFSSC2, YDOROG1, YDINTEA, LDOPENMP, LDROTATE) RESULT (YLFLDS2)

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATIONA_MOD
USE GRADIENT_MOD
USE ATLAS_MODULE  
USE ATLAS_HELPER

IMPLICIT NONE

TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC2
TYPE (ATLAS_FIELD),                           INTENT (IN) :: YDOROG1
TYPE (INTERPOLATIONA),                        INTENT (IN) :: YDINTEA
LOGICAL,                                      INTENT (IN) :: LDOPENMP
LOGICAL,                                      INTENT (IN) :: LDROTATE

REAL (KIND=JPRB), PARAMETER :: RA = 6371229._JPRB
REAL (KIND=JPRB), PARAMETER :: XPI = 2.0_JPRB * ASIN (1.0_JPRB)

LOGICAL :: OSSO (YDFSSC2%SIZE ()), OSSO_ANIS (YDFSSC2%SIZE ())
REAL (KIND=JPRB) :: ZK (YDFSSC2%SIZE ()), ZL (YDFSSC2%SIZE ()), ZM (YDFSSC2%SIZE ())


TYPE (ATLAS_FIELDSET) :: YLFSGR1
TYPE (ATLAS_FIELD) :: YLOROG1DX, YLOROG1DY
TYPE (ATLAS_STRUCTUREDGRID) :: YLGRID2
REAL (KIND=JPRB), POINTER :: ZOROG1DX (:), ZOROG1DY (:), ZOROG1 (:)
REAL (KIND=JPRB) :: ZUNDEF
INTEGER (KIND=JPIM) :: JLOC1
TYPE (ATLAS_METADATA) :: YLMETA
CHARACTER (LEN=:), ALLOCATABLE :: CLNAME 

TYPE (ATLAS_FIELD) :: YLOROG1DXDX; REAL (KIND=JPRB), POINTER :: ZOROG1DXDX (:)
TYPE (ATLAS_FIELD) :: YLOROG1DYDY; REAL (KIND=JPRB), POINTER :: ZOROG1DYDY (:)
TYPE (ATLAS_FIELD) :: YLOROG1DXDY; REAL (KIND=JPRB), POINTER :: ZOROG1DXDY (:)
TYPE (ATLAS_FIELD) :: YLOROG1SQUA; REAL (KIND=JPRB), POINTER :: ZOROG1SQUA (:)

TYPE (ATLAS_FIELD) :: YLOROG2    ; REAL (KIND=JPRB), POINTER :: ZOROG2     (:)
TYPE (ATLAS_FIELD) :: YLOROG2DXDX; REAL (KIND=JPRB), POINTER :: ZOROG2DXDX (:)
TYPE (ATLAS_FIELD) :: YLOROG2DYDY; REAL (KIND=JPRB), POINTER :: ZOROG2DYDY (:)
TYPE (ATLAS_FIELD) :: YLOROG2DXDY; REAL (KIND=JPRB), POINTER :: ZOROG2DXDY (:)
TYPE (ATLAS_FIELD) :: YLOROG2SQUA; REAL (KIND=JPRB), POINTER :: ZOROG2SQUA (:)

TYPE (ATLAS_FIELD) :: YLSSO_DIR  ; REAL (KIND=JPRB), POINTER :: ZSSO_DIR   (:)
TYPE (ATLAS_FIELD) :: YLSSO_SLOPE; REAL (KIND=JPRB), POINTER :: ZSSO_SLOPE (:)
TYPE (ATLAS_FIELD) :: YLSSO_ANIS ; REAL (KIND=JPRB), POINTER :: ZSSO_ANIS  (:)
TYPE (ATLAS_FIELD) :: YLSSO_STDEV; REAL (KIND=JPRB), POINTER :: ZSSO_STDEV (:)

TYPE (ATLAS_FIELDSET) :: YLFLDS1, YLFLDS2I, YLFLDS2

! Get undef value

YLMETA = YDOROG1%METADATA ()
CALL YLMETA%GET ("undef", ZUNDEF)
CALL YLMETA%FINAL ()

! Compute orography gradient

YLFSGR1 = GRADIENT (YDFSSC1, YDOROG1)

CLNAME = YDOROG1%NAME ()

YLOROG1DX = YLFSGR1%FIELD (CLNAME//".DX")
YLOROG1DY = YLFSGR1%FIELD (CLNAME//".DY")

CALL YLOROG1DX%DATA (ZOROG1DX)
CALL YLOROG1DY%DATA (ZOROG1DY)
CALL YDOROG1  %DATA (ZOROG1  )

! Rotate gradient on target projection

IF (LDROTATE) THEN
  YLGRID2 = YDFSSC2%GRID ()
  CALL ROTATE (YDFSSC1, YLGRID2, YLFSGR1)
  CALL YLGRID2%FINAL ()
ENDIF

! Gradient wrt meters on Earth

!$OMP WORKSHARE 
WHERE (ZOROG1DX /= ZUNDEF) ZOROG1DX = ZOROG1DX / RA
!$OMP END WORKSHARE

!$OMP WORKSHARE 
WHERE (ZOROG1DY /= ZUNDEF) ZOROG1DY = ZOROG1DY / RA
!$OMP END WORKSHARE

! Compute tensor fields

YLOROG1DXDX = NEWFLD (YDFSSC1, CLNAME//".DXDX", ZUNDEF); CALL YLOROG1DXDX%DATA (ZOROG1DXDX)
YLOROG1DYDY = NEWFLD (YDFSSC1, CLNAME//".DYDY", ZUNDEF); CALL YLOROG1DYDY%DATA (ZOROG1DYDY)
YLOROG1DXDY = NEWFLD (YDFSSC1, CLNAME//".DXDY", ZUNDEF); CALL YLOROG1DXDY%DATA (ZOROG1DXDY)
YLOROG1SQUA = NEWFLD (YDFSSC1, CLNAME//".SQUA", ZUNDEF); CALL YLOROG1SQUA%DATA (ZOROG1SQUA)

!$OMP PARALLEL DO PRIVATE (JLOC1) IF (LDOPENMP)
DO JLOC1 = 1, SIZE (ZOROG1)
  IF (ZOROG1 (JLOC1) /= ZUNDEF) THEN
    ZOROG1DXDX (JLOC1) = ZOROG1DX (JLOC1) * ZOROG1DX (JLOC1)
    ZOROG1DYDY (JLOC1) = ZOROG1DY (JLOC1) * ZOROG1DY (JLOC1)
    ZOROG1DXDY (JLOC1) = ZOROG1DX (JLOC1) * ZOROG1DY (JLOC1)
    ZOROG1SQUA (JLOC1) = ZOROG1   (JLOC1) * ZOROG1   (JLOC1)
  ELSE
    ZOROG1DXDX (JLOC1) = ZUNDEF
    ZOROG1DYDY (JLOC1) = ZUNDEF
    ZOROG1DXDY (JLOC1) = ZUNDEF
    ZOROG1SQUA (JLOC1) = ZUNDEF
  ENDIF
ENDDO
!$OMP END PARALLEL DO

CALL YLOROG1DX  %FINAL ()
CALL YLOROG1DY  %FINAL ()

! Interpolate fields

YLFLDS1 = ATLAS_FIELDSET ()

CALL YLFLDS1%ADD (YDOROG1    )
CALL YLFLDS1%ADD (YLOROG1DXDX)
CALL YLFLDS1%ADD (YLOROG1DYDY)
CALL YLFLDS1%ADD (YLOROG1DXDY)
CALL YLFLDS1%ADD (YLOROG1SQUA)

YLFLDS2I = YDINTEA%INTERPOLATE (YLFLDS1)

CALL YLFLDS1%FINAL ()

CALL YLOROG1DXDX%FINAL ()
CALL YLOROG1DYDY%FINAL ()
CALL YLOROG1DXDY%FINAL ()
CALL YLOROG1SQUA%FINAL ()

! Compute tensor fields

YLOROG2     = YLFLDS2I%FIELD (CLNAME         ); CALL YLOROG2    %DATA (ZOROG2    )
YLOROG2DXDX = YLFLDS2I%FIELD (CLNAME//".DXDX"); CALL YLOROG2DXDX%DATA (ZOROG2DXDX)
YLOROG2DYDY = YLFLDS2I%FIELD (CLNAME//".DYDY"); CALL YLOROG2DYDY%DATA (ZOROG2DYDY)
YLOROG2DXDY = YLFLDS2I%FIELD (CLNAME//".DXDY"); CALL YLOROG2DXDY%DATA (ZOROG2DXDY)
YLOROG2SQUA = YLFLDS2I%FIELD (CLNAME//".SQUA"); CALL YLOROG2SQUA%DATA (ZOROG2SQUA)

YLSSO_DIR   = NEWFLD (YDFSSC2, "SSO_DIR"  , ZUNDEF); CALL YLSSO_DIR  %DATA (ZSSO_DIR  )
YLSSO_SLOPE = NEWFLD (YDFSSC2, "SSO_SLOPE", ZUNDEF); CALL YLSSO_SLOPE%DATA (ZSSO_SLOPE)
YLSSO_ANIS  = NEWFLD (YDFSSC2, "SSO_ANIS" , ZUNDEF); CALL YLSSO_ANIS %DATA (ZSSO_ANIS )
YLSSO_STDEV = NEWFLD (YDFSSC2, "SSO_STDEV", ZUNDEF); CALL YLSSO_STDEV%DATA (ZSSO_STDEV)

YLFLDS2 = ATLAS_FIELDSET ()

CALL YLFLDS2%ADD (YLSSO_DIR  ); CALL YLFLDS2%ADD (YLSSO_SLOPE)
CALL YLFLDS2%ADD (YLSSO_ANIS ); CALL YLFLDS2%ADD (YLSSO_STDEV)

OSSO = ZOROG2 /= ZUNDEF
OSSO_ANIS = OSSO 

! 1D computations; directly taken from SURFEX

!$OMP WORKSHARE 
WHERE (OSSO) 
  ZK=0.5*(ZOROG2DXDX+ZOROG2DYDY)
  ZL=0.5*(ZOROG2DXDX-ZOROG2DYDY)
  ZM=     ZOROG2DXDY
ELSE WHERE
  ZK = ZUNDEF
  ZL = ZUNDEF
  ZM = ZUNDEF
  ZSSO_DIR   = ZUNDEF
  ZSSO_SLOPE = ZUNDEF
  ZSSO_ANIS  = ZUNDEF
END WHERE
!$OMP END WORKSHARE
!
!*    8.     S.S.O. characteristics
!            ----------------------
!
!*    8.1    S.S.O. direction of main axis
!            -----------------------------
!
!$OMP WORKSHARE 
WHERE (OSSO)
  ZSSO_DIR = 0.5_JPRB * ATAN2 (ZM, ZL) * (180._JPRB / XPI) 
END WHERE
!$OMP END WORKSHARE

!
!*    8.2    S.S.O. slope
!            ------------
!
!$OMP WORKSHARE 
WHERE (OSSO)
  ZSSO_SLOPE = SQRT (ZK + SQRT (ZL*ZL + ZM*ZM))
END WHERE
!$OMP END WORKSHARE

!
!*    8.3    S.S.O. anisotropy
!            -----------------
!
!$OMP WORKSHARE 
WHERE (OSSO_ANIS .AND. (ZK+SQRT(ZL*ZL+ZM*ZM)) > 0._JPRB)
  ZSSO_ANIS = SQRT (MAX (ZK-SQRT(ZL*ZL+ZM*ZM), 0._JPRB) / (ZK+SQRT (ZL*ZL+ZM*ZM)))
END WHERE
!$OMP END WORKSHARE
!
!$OMP WORKSHARE 
WHERE (OSSO_ANIS .AND. (ZK+SQRT (ZL*ZL+ZM*ZM)) == 0._JPRB)
  ZSSO_ANIS = 1._JPRB
END WHERE
!$OMP END WORKSHARE

! Orography standard deviation

!$OMP WORKSHARE 
WHERE (OSSO)
  ZSSO_STDEV = SQRT (MAX (ZOROG2SQUA - ZOROG2**2, 0._JPRB))
ELSE WHERE
  ZSSO_STDEV = ZUNDEF
END WHERE
!$OMP END WORKSHARE

CALL YLSSO_DIR  %FINAL ()
CALL YLSSO_SLOPE%FINAL ()
CALL YLSSO_ANIS %FINAL ()
CALL YLSSO_STDEV%FINAL ()

CALL YLFLDS2%RETURN ()

END FUNCTION ATLAS_COMPUTE_SSO

