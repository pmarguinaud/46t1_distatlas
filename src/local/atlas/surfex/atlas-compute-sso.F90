SUBROUTINE ATLAS_COMPUTE_SSO (YDFSSC, PUNDEF, PZS, PZS2, PZS_DXDX, PZS_DYDY, PZS_DXDY, &
                            & PSSO_DIR, PSSO_SLOPE, PSSO_ANIS, PSSO_STDEV)
USE ATLAS_MODULE  
USE PARKIND1, ONLY : JPIM, JPRB
IMPLICIT NONE
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS), INTENT (IN) :: YDFSSC
REAL (KIND=JPRB), INTENT (IN)  :: PUNDEF
REAL (KIND=JPRB), INTENT (IN)  :: PZS (:), PZS2 (:), PZS_DXDX (:), PZS_DYDY (:), PZS_DXDY (:)
REAL (KIND=JPRB), INTENT (OUT) :: PSSO_DIR (:), PSSO_SLOPE (:), PSSO_ANIS (:), PSSO_STDEV (:)

REAL (KIND=JPRB), PARAMETER :: XPI = 2.0_JPRB * ASIN (1.0_JPRB)

LOGICAL :: OSSO (YDFSSC%SIZE ()), OSSO_ANIS (YDFSSC%SIZE ())
REAL (KIND=JPRB) :: ZK (YDFSSC%SIZE ()), ZL (YDFSSC%SIZE ()), ZM (YDFSSC%SIZE ())

OSSO = PZS /= PUNDEF
OSSO_ANIS = OSSO 

! 1D computations; directly taken from SURFEX

!$OMP WORKSHARE 
WHERE (OSSO) 
  ZK=0.5*(PZS_DXDX+PZS_DYDY)
  ZL=0.5*(PZS_DXDX-PZS_DYDY)
  ZM=     PZS_DXDY
ELSE WHERE
  ZK = PUNDEF
  ZL = PUNDEF
  ZM = PUNDEF
  PSSO_DIR   = PUNDEF
  PSSO_SLOPE = PUNDEF
  PSSO_ANIS  = PUNDEF
END WHERE
!$OMP END WORKSHARE
!
!*    8.     S.S.O. characteristics
!            ----------------------
!
!*    8.1    S.S.O. direction of main axis
!            -----------------------------
!
!$OMP WORKSHARE 
WHERE (OSSO)
  PSSO_DIR = 0.5_JPRB * ATAN2 (ZM, ZL) * (180._JPRB / XPI) 
END WHERE
!$OMP END WORKSHARE

!
!*    8.2    S.S.O. slope
!            ------------
!
!$OMP WORKSHARE 
WHERE (OSSO)
  PSSO_SLOPE = SQRT (ZK + SQRT (ZL*ZL + ZM*ZM))
END WHERE
!$OMP END WORKSHARE

!
!*    8.3    S.S.O. anisotropy
!            -----------------
!
!$OMP WORKSHARE 
WHERE (OSSO_ANIS .AND. (ZK+SQRT(ZL*ZL+ZM*ZM)) > 0._JPRB)
  PSSO_ANIS = SQRT (MAX (ZK-SQRT(ZL*ZL+ZM*ZM), 0._JPRB) / (ZK+SQRT (ZL*ZL+ZM*ZM)))
END WHERE
!$OMP END WORKSHARE
!
!$OMP WORKSHARE 
WHERE (OSSO_ANIS .AND. (ZK+SQRT (ZL*ZL+ZM*ZM)) == 0._JPRB)
  PSSO_ANIS = 1._JPRB
END WHERE
!$OMP END WORKSHARE

! Orography standard deviation

!$OMP WORKSHARE 
WHERE (OSSO)
  PSSO_STDEV = SQRT (MAX (PZS2 - PZS**2, 0._JPRB))
ELSE WHERE
  PSSO_STDEV = PUNDEF
END WHERE
!$OMP END WORKSHARE

END SUBROUTINE

