SUBROUTINE ATLAS_PREP_IMPL (YDIO1, YDIO2)

USE PARKIND1, ONLY : JPIM, JPRB
USE INTERPOLATIONA_MOD
USE INTERPOLATION4_MOD
USE ATLAS_IO, ONLY : ATLAS_IO_t
USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM
USE FCKIT_MPI_MODULE, ONLY : FCKIT_MPI_SUM
USE ATLAS_MODULE  
USE ATLAS_HELPER

#include "atlas-abort.h"

!

IMPLICIT NONE

CLASS (ATLAS_IO_t)                           :: YDIO1
CLASS (ATLAS_IO_t)                           :: YDIO2

TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1PRE
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1PGD
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID1
TYPE (ATLAS_STRUCTUREDGRID)                  :: YLGRID2

TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC1
TYPE (ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS) :: YLFSSC2
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST1
TYPE (ATLAS_GRIDDISTRIBUTION)                :: YLDIST2
TYPE (INTERPOLATIONA)                        :: YLINTEA
TYPE (INTERPOLATION4)                        :: YLINTE4
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1FRC
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2FRC
TYPE (ATLAS_FIELDSET)                        :: YLFLDS1PRE  
TYPE (ATLAS_FIELDSET)                        :: YLFLDS2PRE
TYPE (FCKIT_MPI_COMM)                        :: YLCOMM

INTEGER (KIND=JPIM) :: I, JFLD
INTEGER (KIND=JPIM) :: NPROC, IRANK

INTEGER (KIND=JPIM), ALLOCATABLE :: IFLD2FRC (:)

TYPE (PTR), ALLOCATABLE :: YLPT1PGD (:), YLPT2PGD (:)

CHARACTER (LEN=16), DIMENSION (31) :: CLPRENAME = &
[ 'SFX.SST         ', 'SFX.Z0SEA       ', 'SFX.SSS         ', 'SFX.TS_WATER    ', &
& 'SFX.Z0WATER     ', 'X001TG1         ', 'X001TG2         ', 'X001WG1         ', &
& 'X001WG2         ', 'X001WG3         ', 'X001WGI1        ', 'X001WGI2        ', &
& 'X001WR          ', 'X001ICE_STO     ', 'X001WSN_VEG1    ', 'X001RSN_VEG1    ', &
& 'X001ASN_VEG     ', 'SFX.TSRAD_NAT   ', 'X001RESA        ', 'X001LAI         ', &
& 'X001VEG         ', 'X001DG2         ', 'X001RSMIN       ', 'X001ALBNIR_S    ', &
& 'X001ALBVIS_S    ', 'X001ALBUV_S     ', 'X001ALBNIR_ISBA ', 'X001ALBVIS_ISBA ', &
& 'X001ALBUV_ISBA  ', 'SFX.T2M         ', 'SFX.HU2M        ']
CHARACTER (LEN=16), DIMENSION (4)  :: CLPGDNAME = &
[ 'SFX.FRAC_SEA    ', 'SFX.FRAC_NATURE ', 'SFX.FRAC_WATER  ', 'SFX.FRAC_TOWN   ' ]

REAL (KIND=JPRB), PARAMETER :: ZUNDEF = HUGE (1._JPRB)


LOGICAL, PARAMETER :: LGARDEN = .FALSE., LWATER_TO_NATURE = .FALSE., LTOWN_TO_ROCK = .TRUE.

YLCOMM = FCKIT_MPI_COMM ()
NPROC  = YLCOMM%SIZE ()
IRANK  = YLCOMM%RANK ()

YLGRID1PRE = YDIO1%GRID ('PRE1')
YLGRID1PGD = YDIO1%GRID ('PGD1')

IF (.NOT. GRID_EQ (YLGRID1PRE, YLGRID1PGD)) THEN
  CALL ABORT ('GEOMETRY MISMATCH')
ENDIF

YLGRID1 = YLGRID1PRE

CALL YLGRID1PRE%FINAL ()
CALL YLGRID1PGD%FINAL ()

YLGRID2 = YDIO2%GRID ('PGD2')

CALL CREATE_DIST (YLGRID1, YLDIST1)
CALL CREATE_DIST (YLGRID2, YLDIST2)

YLFSSC1 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID1, YLDIST1)
YLFSSC2 = ATLAS_FUNCTIONSPACE_STRUCTUREDCOLUMNS (YLGRID2, YLDIST2)

YLFLDS1PRE = YDIO1%READ ('PRE1', CLPRENAME, YLFSSC1, ZUNDEF)
YLFLDS1FRC = YDIO1%READ ('PGD1', CLPGDNAME, YLFSSC1, ZUNDEF)
YLFLDS2FRC = YDIO1%READ ('PGD2', CLPGDNAME, YLFSSC2, ZUNDEF)

YLINTEA = INTERPOLATIONA (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)
YLINTE4 = INTERPOLATION4 (YLDIST1, YLFSSC1, YLDIST2, YLFSSC2)

BLOCK
  TYPE (ATLAS_FIELDSET) :: YLFLDS2PRE4
  TYPE (ATLAS_FIELD) :: YLFLDA, YLFLD4
  REAL (KIND=JPRB), POINTER :: ZDATA (:), ZDAT4 (:)
  YLFLDS2PRE  = YLINTEA%INTERPOLATE (YLFLDS1PRE)
  YLFLDS2PRE4 = YLINTE4%INTERPOLATE (YLFLDS1PRE)

  DO JFLD = 1, YLFLDS2PRE%SIZE ()

    YLFLDA = YLFLDS2PRE %FIELD (JFLD)
    YLFLD4 = YLFLDS2PRE4%FIELD (JFLD)

    IF (YLFLDA%NAME () /= YLFLD4%NAME ()) &
      & CALL ABORT ('FIELD MISMATCH')

    CALL YLFLDA%DATA (ZDATA)
    CALL YLFLD4%DATA (ZDAT4)
   
    ! Take the result of 4-point interpolation where averaging is not available
!$OMP WORKSHARE
    WHERE (ZDATA == ZUNDEF)
      ZDATA = ZDAT4
    ENDWHERE
!$OMP END WORKSHARE

    CALL YLFLDA%FINAL ()
    CALL YLFLD4%FINAL ()

  ENDDO

  CALL YLFLDS2PRE4%FINAL ()

ENDBLOCK


! Find mask of each field

ALLOCATE (IFLD2FRC (YLFLDS1PRE%SIZE ()))

DO JFLD = 1, YLFLDS1PRE%SIZE ()
  BLOCK
    TYPE (ATLAS_FIELD) :: YLFLD, YLFRC
    REAL (KIND=JPRB), POINTER :: ZDATA (:), ZFRAC (:)
    INTEGER (KIND=JPIM) :: JFRC
    INTEGER (KIND=JPIM) :: ICNT 
    YLFLD = YLFLDS1PRE%FIELD (JFLD)
    CALL YLFLD%DATA (ZDATA)

    IFLD2FRC (JFLD) = 0

    DO JFRC = 1, YLFLDS1FRC%SIZE ()
      YLFRC = YLFLDS1FRC%FIELD (JFRC)
      CALL YLFRC%DATA (ZFRAC)
      ICNT = COUNT (((ZDATA == ZUNDEF) .AND. (ZFRAC >  0._JPRB)) &
             & .OR. ((ZDATA /= ZUNDEF) .AND. (ZFRAC == 0._JPRB)))
      CALL YLCOMM%ALLREDUCE (ICNT, FCKIT_MPI_SUM ())

      IF (ICNT == 0) THEN
        IFLD2FRC (JFLD) = JFRC
        EXIT
      ENDIF

      WRITE (*, '(A16," | ",A16," | ",I16)') YLFLD%NAME (), YLFRC%NAME (), ICNT

      CALL YLFRC%FINAL ()

    ENDDO

    CALL YLFLD%FINAL ()
  ENDBLOCK
ENDDO

! Fix fields on output grid: use field average 

DO JFLD = 1, YLFLDS2PRE%SIZE ()
  BLOCK
    TYPE (ATLAS_FIELD) :: YLFLD, YLFRC
    REAL (KIND=JPRB), POINTER :: ZDATA (:), ZFRAC (:)
    INTEGER (KIND=JPIM) :: JFRC
    INTEGER (KIND=JPIM) :: ICNT 
    REAL (KIND=JPRB) :: ZSUM, ZAVG

    YLFLD = YLFLDS2PRE%FIELD (JFLD)
    CALL YLFLD%DATA (ZDATA)

    JFRC = IFLD2FRC (JFLD)

    IF (JFRC > 0) THEN
      YLFRC = YLFLDS2FRC%FIELD (JFRC)
      CALL YLFRC%DATA (ZFRAC)

      ICNT = COUNT ((ZDATA /= ZUNDEF) .AND. (ZFRAC >  0._JPRB))
      ZSUM = SUM (ZDATA, MASK =(ZDATA /= ZUNDEF) .AND. (ZFRAC > 0._JPRB))

      CALL YLCOMM%ALLREDUCE (ZSUM, FCKIT_MPI_SUM ())
      CALL YLCOMM%ALLREDUCE (ICNT, FCKIT_MPI_SUM ())

      ZAVG = ZSUM / REAL (ICNT, JPRB)

!$OMP WORKSHARE
      WHERE ((ZDATA == ZUNDEF) .AND. (ZFRAC > 0._JPRB))
        ZDATA = ZAVG
      ENDWHERE
!$OMP END WORKSHARE

      CALL YLFRC%FINAL ()

    ELSE

      ICNT = COUNT (ZDATA /= ZUNDEF)
      ZSUM = SUM (ZDATA, MASK =(ZDATA /= ZUNDEF))
      
      CALL YLCOMM%ALLREDUCE (ZSUM, FCKIT_MPI_SUM ())
      CALL YLCOMM%ALLREDUCE (ICNT, FCKIT_MPI_SUM ())

      ZAVG = ZSUM / REAL (ICNT, JPRB)

!$OMP WORKSHARE
      WHERE (ZDATA == ZUNDEF)
        ZDATA = ZAVG
      ENDWHERE
!$OMP END WORKSHARE

    ENDIF


    CALL YLFLD%FINAL ()
  ENDBLOCK
ENDDO


CALL YDIO2%WRITE ('PRE2', YLFLDS2PRE, YLFSSC2)

CALL YLINTE4%FINAL ()
CALL YLINTEA%FINAL ()
CALL YLGRID1%FINAL ()
CALL YLGRID2%FINAL ()
CALL YLDIST1%FINAL ()
CALL YLDIST2%FINAL ()
CALL YLFLDS1PRE%FINAL ()
CALL YLFLDS2PRE%FINAL ()
CALL YLFSSC1%FINAL ()
CALL YLFSSC2%FINAL ()

END

