SUBROUTINE FAIGRA_MT64 (FA)

USE FA_MOD, ONLY : NGRIB2_GLO_SH, NGRIB2_GLO_GP, NGRIB2_LAM_GP, &
                 & NGRIB2_LAM_BF, NGRIB2_LATLON, NGRIB1_LATLON, &
                 & LGRIB2_LAM_EX, LGRIB2_LAM_BF, LGRIB2_INIT,   &
                 & FA_COM
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
USE GRIB_API_INTERFACE
USE GRIB_API

IMPLICIT NONE

!****
!      Sous-programme de lecture des templates GRIB2.
!**
!

TYPE (FA_COM) :: FA

INTEGER :: IGRIB2_LAM_BF, IRET, IGRIBH

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('FAIGRA_MT',0,ZHOOK_HANDLE)

#define FAGRIB2
#ifdef FAGRIB2
!$OMP CRITICAL
IF (.NOT. LGRIB2_INIT) THEN
  CALL IGRIB_NEW_FROM_SAMPLES (NGRIB1_LATLON,  'regular_ll_pl_grib1')
  CALL IGRIB_NEW_FROM_SAMPLES (NGRIB2_LATLON,  'regular_ll_pl_grib2')
  CALL IGRIB_NEW_FROM_SAMPLES (NGRIB2_GLO_SH,  'sh_ml_grib2')
  CALL IGRIB_NEW_FROM_SAMPLES (NGRIB2_GLO_GP,  'reduced_gg_ml_grib2')
  CALL IGRIB_NEW_FROM_SAMPLES (NGRIB2_LAM_GP,  'reduced_gg_ml_grib2')
  CALL IGRIB_NEW_FROM_SAMPLES (IGRIB2_LAM_BF,  'lambert_bf_grib2', KRET=IRET)
  IF (IRET == 0) THEN
    NGRIB2_LAM_BF = IGRIB2_LAM_BF
    LGRIB2_LAM_BF = .TRUE.
  ELSE
    WRITE (FA%NULOUT, '(A)') "FAIGRA: grib_api template `lambert_bf_grib2' was not found; &
                            &it will not be possible to encode spectral LAM fields with grib_api"
  ENDIF

! See if lambert_lam is available
  CALL IGRIB_CLONE (NGRIB2_LAM_GP, IGRIBH)
  CALL IGRIB_SET_VALUE (IGRIBH, 'gridType', 'lambert_lam', KRET=IRET)
  LGRIB2_LAM_EX = IRET == 0
  CALL IGRIB_RELEASE (IGRIBH)

  LGRIB2_INIT = .TRUE.
ENDIF
!$OMP END CRITICAL
#endif

IF (LHOOK) CALL DR_HOOK('FAIGRA_MT',1,ZHOOK_HANDLE)

END SUBROUTINE

SUBROUTINE FAIGRA
USE FA_MOD, ONLY : FA => FA_COM_DEFAULT, &
&                  FA_COM_DEFAULT_INIT,  &
&                  NEW_FA_DEFAULT
IF (.NOT. FA_COM_DEFAULT_INIT) CALL NEW_FA_DEFAULT ()
CALL FAIGRA_MT64 (FA)
END SUBROUTINE

SUBROUTINE FAIGRA64
USE FA_MOD, ONLY : FA => FA_COM_DEFAULT, &
&                  FA_COM_DEFAULT_INIT,  &
&                  NEW_FA_DEFAULT
IF (.NOT. FA_COM_DEFAULT_INIT) CALL NEW_FA_DEFAULT ()
CALL FAIGRA_MT64 (FA)
END SUBROUTINE

SUBROUTINE FAIGRA_MT (FA)
USE FA_MOD, ONLY : FA_COM
TYPE (FA_COM) :: FA
CALL FAIGRA_MT64 (FA)
END SUBROUTINE

