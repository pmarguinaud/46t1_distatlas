SUBROUTINE FADIFF

USE PARKIND1, ONLY : JPIM, JPRB, JPIA, JPIB
USE LFI_PRECISION
USE XRD_GETOPTIONS
#ifdef UNDEF
USE MODD_IO_SURF_ARO, ONLY : SURFEX_FIELD_BUF_READ_MISC, &
                           & SURFEX_FIELD_BUF_CACHE
#endif
USE FA_MOD, ONLY : FA_COM
USE YOMHOOK, ONLY : DR_HOOK, LHOOK
                           

IMPLICIT NONE

#include "fadoco.h"

INTEGER, PARAMETER :: ILUN1 = 77, ILUN2 = 88
INTEGER :: ILUNR = 99
CHARACTER (LEN=*), PARAMETER :: CLNOMCA = 'cadreA', CLNOMCB = 'cadreB'
CHARACTER (LEN=16) :: CLNOMC1, CLNOMC2
CHARACTER (LEN=16), PARAMETER :: CLSKIP (10) = &
& (/ 'CADRE-DIMENSIONS', 'CADRE-FRANKSCHMI',   &
&    'CADRE-REDPOINPOL', 'CADRE-SINLATITUD',   &
&    'CADRE-FOCOHYBRID', 'FULLPOS         ',   &
&    'DATE-DES-DONNEES', 'ARP             ',   &
&    'DATX-DES-DONNEES', 'ALD             ' /)
CHARACTER (LEN=16), POINTER :: CLSKIP1 (:) => NULL ()
CHARACTER (LEN=16), POINTER :: CLONLY1 (:) => NULL ()

REAL (KIND=JPRB), ALLOCATABLE :: ZVALCO (:)
CHARACTER (LEN=128) :: CLF1, CLF2, CLFR
INTEGER (KIND=JPIM) :: INBARP, INBARI, IREP
INTEGER (KIND=JPIM) :: ILONGD, IPOSEX, INALDO, INTROU, INARES, INAMAX, ILNOMA
INTEGER (KIND=JPIM) :: IARTL, INIVAU, ILCHAM, INBITS, ISTRON, IPUILA
INTEGER (KIND=JPIM) :: INGRIB1, INGRIB2
INTEGER (KIND=JPIM) :: I, INIMES
CHARACTER (LEN=16),  ALLOCATABLE :: CLNOMS (:)
CHARACTER (LEN=48),  ALLOCATABLE :: CLSORT (:)
INTEGER (KIND=JPIM), ALLOCATABLE :: ISORT (:)
CHARACTER (LEN=16) :: CLPREF, CLSUFF, CLNOMA
REAL (KIND=JPRB), ALLOCATABLE :: ZCHAMP1 (:), ZCHAMP2 (:), ZCHAMPD (:)
REAL (KIND=JPRB) :: ZUNDF1, ZUNDF2, ZUNDF1_OPT, ZUNDF2_OPT
REAL (KIND=JPRB) :: ZMIN, ZMAX, ZAVG, ZRMS
LOGICAL :: LLSURFEX, LLUNDF1, LLUNDF2, LLUNDF1_OPT, LLUNDF2_OPT, LLEXIS, LLUNDF2FROMUNDF1, LLCPLONLY, &
         & LLCOSP, LLCOSP1, LLCOSP2, LLGRIBEXDECODING1, LLGRIBEXDECODING2, LLCHECK
INTEGER (KIND=JPIB) :: INGRIB_8
#ifdef UNDEF
TYPE (SURFEX_FIELD_BUF_CACHE) :: YLSC
#endif
LOGICAL :: LLVERBOSE

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FADIFF',0,ZHOOK_HANDLE)

CALL INITOPTIONS (KOPTMIN=1)
CALL GETOPTION ('--fa-file-1', CLF1, MND = .TRUE., USE = "FA file 1")
CALL GETOPTION ('--fa-file-2', CLF2, MND = .TRUE., USE = "FA file 2")
CLFR = ''
CALL GETOPTION ('--result-file', CLFR)
CALL GETOPTION ('--skip', CLSKIP1)
CALL GETOPTION ('--only', CLONLY1)
CALL GETOPTION ('--coupling', LLCPLONLY)
ZUNDF1_OPT = 0.; CALL GETOPTION ('--zundf1', ZUNDF1_OPT); LLUNDF1_OPT = ZUNDF1_OPT /= 0.
ZUNDF2_OPT = 0.; CALL GETOPTION ('--zundf2', ZUNDF2_OPT); LLUNDF2_OPT = ZUNDF2_OPT /= 0.
CALL GETOPTION ('--undf2-from-undf1', LLUNDF2FROMUNDF1)
CALL GETOPTION ('--gribex-decoding1', LLGRIBEXDECODING1, USE = "Decode file 1 using GRIBEX when possible")
CALL GETOPTION ('--gribex-decoding2', LLGRIBEXDECODING2, USE = "Decode file 2 using GRIBEX when possible")
LLCHECK = .FALSE.
CALL GETOPTION ('--check-cadre', LLCHECK)
CALL GETOPTION ('--verbose', LLVERBOSE)
CALL CHECKOPTIONS ()

IF (LLCHECK) THEN
  CLNOMC1 = CLNOMCA
  CLNOMC2 = CLNOMCA
ELSE
  CLNOMC1 = CLNOMCA
  CLNOMC2 = CLNOMCB
ENDIF

IF (LLVERBOSE) THEN
  INIMES = 2
ELSE
  INIMES = 0
ENDIF

IF (CLFR /= '') THEN
  OPEN (ILUNR, FILE=TRIM (CLFR), FORM='FORMATTED', STATUS='UNKNOWN')
ELSE
  ILUNR = 6
ENDIF


WRITE (ILUNR, '(A16," ",A16," ",A16," ",A30," ",A30," ",A30," ",A30)') &
     & 'PREFIX', 'LEVEL', 'SUFFIX', 'MIN', 'MAX', 'AVG', 'RMS'

INBARP=0
INBARI=0
CALL FAITOU (IREP, ILUN1, .TRUE., CLF1, 'OLD', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMC1)
CALL LFINFO (IREP, ILUN1, 'SFX._FBUF_NAME', ILONGD, IPOSEX)

LLSURFEX = ILONGD > 0
LLSURFEX = .FALSE.

INBARP=0
INBARI=0
CALL FAITOU (IREP, ILUN2, .TRUE., CLF2, 'OLD', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMC2)

IF (LLSURFEX) THEN
#ifdef UNDEF
  CALL SURFEX_FIELD_BUF_READ_MISC (YLSC, ILUN1, LDMPI=.FALSE.)

  INALDO = 0

  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
      CASE ('X2')
        INALDO = INALDO + YLSC%P (IARTL)%NDIM (2)
    END SELECT
  ENDDO
  
  ALLOCATE (CLNOMS (INALDO))
  
  INALDO = 0

  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
        CLNOMS (INALDO) = 'SFX.'//YLSC%P (IARTL)%NAME
      CASE ('X2')
        DO I = 1, YLSC%P (IARTL)%NDIM (2)
          INALDO = INALDO + 1
          WRITE (CLNOMS (INALDO), '("X",I3.3,A)') I, TRIM (YLSC%P (IARTL)%NAME)
        ENDDO
    END SELECT
  ENDDO
#endif
ELSE

  CALL LFINAF (IREP, ILUN1, INALDO, INTROU, INARES, INAMAX)

  ALLOCATE (CLNOMS (INALDO))

  CALL LFIPOS (IREP, ILUN1)

  DO IARTL = 1, INALDO

    CALL LFICAS (IREP, ILUN1, CLNOMS (IARTL), ILONGD, IPOSEX, .TRUE.)

    IF (ILONGD == 0) EXIT

  ENDDO

ENDIF




! Sort fields by suffix, prefix, level 
ALLOCATE (ISORT (INALDO), CLSORT (INALDO))
DO IARTL = 1, INALDO
  CLNOMA = CLNOMS (IARTL)
  IF (ANY (CLSKIP == CLNOMA)) CYCLE
  IF (ASSOCIATED (CLSKIP1)) THEN
    IF (ANY (CLSKIP1 == CLNOMA)) CYCLE
  ENDIF
  IF (ASSOCIATED (CLONLY1)) THEN
    IF (.NOT. ANY (CLONLY1 == CLNOMA)) CYCLE
  ENDIF
  CALL FAQUIN (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, &
             & CLNOMA, LEN_TRIM (CLNOMA))
  WRITE (CLSORT (IARTL), '(A16,A16,I16.16)') CLPREF, CLSUFF, INIVAU
ENDDO
CALL QSORTC (INALDO, ISORT, CLSORT)
CLNOMS = CLNOMS (ISORT)
DEALLOCATE (ISORT, CLSORT)
!

DO IARTL = 1, INALDO

  CLNOMA = CLNOMS (IARTL)

  IF (ANY (CLSKIP == CLNOMA)) CYCLE

  IF (ASSOCIATED (CLSKIP1)) THEN
    IF (ANY (CLSKIP1 == CLNOMA)) CYCLE
  ENDIF

  IF (ASSOCIATED (CLONLY1)) THEN
    IF (.NOT. ANY (CLONLY1 == CLNOMA)) CYCLE
  ENDIF

  CALL FAQUIN (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, &
             & CLNOMA, LEN_TRIM (CLNOMA))

! Read field features
  CALL FANION (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, LLEXIS, LLCOSP1, INGRIB1, &
             & INBITS, ISTRON, IPUILA)
  CALL FANION (IREP, ILUN2, CLPREF, INIVAU, CLSUFF, LLEXIS, LLCOSP2, INGRIB2, &
             & INBITS, ISTRON, IPUILA)

  IF (LLCOSP1 .NEQV. LLCOSP2) THEN
    STOP "TRYING TO COMPARE GRID-POINT AND SPECTRAL FIELD"
  ENDIF

  LLCOSP = LLCOSP1

  CALL FATCHA (IREP, CLNOMC1, LLCOSP, ILCHAM)
  ALLOCATE (ZCHAMP1 (ILCHAM), ZCHAMP2 (ILCHAM), ZCHAMPD (ILCHAM))


! First input file
  IF (LLCPLONLY .AND. (INGRIB1 == 4)) THEN
    LLUNDF1 = .TRUE.
    ZUNDF1  = HUGE (ZUNDF1)
  ELSE
    LLUNDF1 = .FALSE.
  ENDIF
  CALL LFINFO (IREP, ILUN1, CLNOMA, ILONGD, IPOSEX)
  ALLOCATE (ZVALCO (ILONGD))
  CALL LFILEC (IREP, ILUN1, CLNOMA, ZVALCO, ILONGD)
  IF (LLGRIBEXDECODING1) THEN
    INGRIB_8 = TRANSFER (ZVALCO (1), INGRIB_8)
    IF (FALGRA (INGRIB_8)                  .AND. &
      & (INGRIB_8 == FALGRA_GP (INGRIB_8)) .AND. &
      & (FALGRA_ED (INGRIB_8) == 1)) THEN
      INGRIB_8 = 3
      ZVALCO (1) = TRANSFER (INGRIB_8, ZVALCO (1))
    ENDIF
  ENDIF
  CALL FADOCO (IREP, ILUN1, CLPREF, INIVAU, CLSUFF,    &
             & LLCOSP, CLNOMA, ILNOMA, ZVALCO, ILONGD, &
             & ZCHAMP1, LDUNDF=LLUNDF1, PUNDF=ZUNDF1)
  DEALLOCATE (ZVALCO)

  IF ((.NOT. LLUNDF1) .AND. LLUNDF1_OPT) THEN
    LLUNDF1 = LLUNDF1_OPT
    ZUNDF1  = ZUNDF1_OPT
  ENDIF
  IF (.NOT. LLUNDF1) ZUNDF1 = HUGE (ZUNDF1)

! Second input file
  IF (LLCPLONLY .AND. (INGRIB2 == 4)) THEN
    LLUNDF2 = .TRUE.
    ZUNDF2  = HUGE (ZUNDF2)
  ELSE
    LLUNDF2 = .FALSE.
  ENDIF
  CALL LFINFO (IREP, ILUN2, CLNOMA, ILONGD, IPOSEX)
  ALLOCATE (ZVALCO (ILONGD))
  CALL LFILEC (IREP, ILUN2, CLNOMA, ZVALCO, ILONGD)
  IF (LLGRIBEXDECODING2) THEN
    INGRIB_8 = TRANSFER (ZVALCO (1), INGRIB_8)
    IF (FALGRA (INGRIB_8)                  .AND. &
      & (INGRIB_8 == FALGRA_GP (INGRIB_8)) .AND. &
      & (FALGRA_ED (INGRIB_8) == 1)) THEN
      INGRIB_8 = 3
      ZVALCO (1) = TRANSFER (INGRIB_8, ZVALCO (1))
    ENDIF
  ENDIF
  CALL FADOCO (IREP, ILUN2, CLPREF, INIVAU, CLSUFF,    &
             & LLCOSP, CLNOMA, ILNOMA, ZVALCO, ILONGD, &
             & ZCHAMP2, LDUNDF=LLUNDF2, PUNDF=ZUNDF2)
  DEALLOCATE (ZVALCO)

  IF ((.NOT. LLUNDF2) .AND. LLUNDF2_OPT) THEN
    LLUNDF2 = LLUNDF2_OPT
    ZUNDF2  = ZUNDF2_OPT
  ENDIF
  IF (.NOT. LLUNDF2) ZUNDF2 = HUGE (ZUNDF2)


! Compare
  DO I = 1, ILCHAM
    IF (LLUNDF2FROMUNDF1) THEN
      IF (ZCHAMP1 (I) == ZUNDF1) THEN
        ZCHAMPD (I) = 0._JPRB
      ELSE
        ZCHAMPD (I) = ZCHAMP1 (I) - ZCHAMP2 (I)
      ENDIF
    ELSE
      IF (ZCHAMP1 (I) == ZUNDF1 .AND. ZCHAMP2 (I) == ZUNDF2) THEN
        ZCHAMPD (I) = 0._JPRB
      ELSEIF (ZCHAMP1 (I) /= ZUNDF1 .AND. ZCHAMP2 (I) /= ZUNDF2) THEN
        ZCHAMPD (I) = ZCHAMP1 (I) - ZCHAMP2 (I)
      ELSE
        ZCHAMPD (I) = HUGE (ZCHAMPD (I))
      ENDIF
    ENDIF
  ENDDO

  IF (LLCOSP) THEN
    ZAVG = ZCHAMPD (1) 
    ZRMS = SQRT (SUM (ZCHAMPD**2))

    WRITE (ILUNR, '(A16," ",I16," ",A16," ",A30," ",A30," ",E30.20," ",E30.20)') &
         & CLPREF, INIVAU, CLSUFF, '-', '-', ZAVG, ZRMS

  ELSE

    ZMIN = MINVAL (ZCHAMPD)
    ZMAX = MAXVAL (ZCHAMPD)
    ZAVG = SUM (ZCHAMPD) / ILCHAM
    ZRMS = SQRT (SUM (ZCHAMPD**2) / ILCHAM)

    DO I = 1, LEN_TRIM (CLSUFF)
      IF (CLSUFF (I:I) == ' ') CLSUFF (I:I) = '_'
    ENDDO

    DO I = 1, LEN_TRIM (CLPREF)
      IF (CLPREF (I:I) == ' ') CLPREF (I:I) = '_'
    ENDDO

    WRITE (ILUNR, '(A16," ",I16," ",A16," ",E30.20," ",E30.20," ",E30.20," ",E30.20)') &
         & CLPREF, INIVAU, CLSUFF, ZMIN, ZMAX, ZAVG, ZRMS

  ENDIF

  DEALLOCATE (ZCHAMPD, ZCHAMP1, ZCHAMP2)

ENDDO

CALL FAIRME (IREP, ILUN1, 'KEEP')
CALL FAIRME (IREP, ILUN2, 'KEEP')

DEALLOCATE (CLNOMS)

IF (CLFR /= '') THEN
  CLOSE (ILUNR)
ENDIF

IF (LHOOK) CALL DR_HOOK ('FADIFF',1,ZHOOK_HANDLE)

CONTAINS

#include "falgra.h"

END SUBROUTINE FADIFF

