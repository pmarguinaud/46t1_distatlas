SUBROUTINE FASTAT

USE PARKIND1, ONLY : JPIM, JPRB, JPIA, JPIB
USE LFI_PRECISION
USE XRD_GETOPTIONS
USE MODD_IO_SURF_ARO, ONLY : SURFEX_FIELD_BUF_READ_MISC, &
                           & SURFEX_FIELD_BUF_CACHE
USE FA_MOD, ONLY : FA_COM
USE YOMHOOK, ONLY : DR_HOOK, LHOOK
                           

IMPLICIT NONE

#include "facilo.h"

INTEGER, PARAMETER :: ILUN = 77
INTEGER :: ILUNR = 99
CHARACTER (LEN=16) :: CLNOMC = 'cadre'
CHARACTER (LEN=16), PARAMETER :: CLSKIP (10) = &
& (/ 'CADRE-DIMENSIONS', 'CADRE-FRANKSCHMI',   &
&    'CADRE-REDPOINPOL', 'CADRE-SINLATITUD',   &
&    'CADRE-FOCOHYBRID', 'FULLPOS         ',   &
&    'DATE-DES-DONNEES', 'ARP             ',   &
&    'DATX-DES-DONNEES', 'ALD             ' /)
CHARACTER (LEN=16), POINTER :: CLSKIP1 (:) => NULL ()
CHARACTER (LEN=16), POINTER :: CLONLY1 (:) => NULL ()

CHARACTER (LEN=128) :: CLF, CLFR
INTEGER (KIND=JPIM) :: INBARP, INBARI, IREP, INGRIB
INTEGER (KIND=JPIM) :: ILONGD, IPOSEX, INALDO, INTROU, INARES, INAMAX, ILNOMA
INTEGER (KIND=JPIM) :: IARTL, INIVAU, ILCHAM, INBITS, ISTRON, IPUILA
INTEGER (KIND=JPIM) :: I, INIMES
CHARACTER (LEN=16),  ALLOCATABLE :: CLNOMS (:)
CHARACTER (LEN=48),  ALLOCATABLE :: CLSORT (:)
INTEGER (KIND=JPIM), ALLOCATABLE :: ISORT (:)
CHARACTER (LEN=16) :: CLPREF, CLSUFF, CLNOMA
REAL (KIND=JPRB), ALLOCATABLE :: ZCHAMP (:)
REAL (KIND=JPRB) :: ZUNDF, ZUNDF_OPT
REAL (KIND=JPRB) :: ZMIN, ZMAX, ZAVG, ZRMS
LOGICAL :: LLSURFEX, LLUNDF, LLEXIS, LLCOSP, LLCHECK, LLUNDF_OPT
INTEGER (KIND=JPIB) :: INGRIB_8
TYPE (SURFEX_FIELD_BUF_CACHE) :: YLSC
LOGICAL :: LLVERBOSE

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('FASTAT',0,ZHOOK_HANDLE)

CALL INITOPTIONS (KOPTMIN=1)
CALL GETOPTION ('--fa-file', CLF, MND = .TRUE., USE = "FA file")
CLFR = ''
CALL GETOPTION ('--result-file', CLFR)
CALL GETOPTION ('--skip', CLSKIP1)
CALL GETOPTION ('--only', CLONLY1)
ZUNDF_OPT = 0.; CALL GETOPTION ('--zundf', ZUNDF_OPT); LLUNDF_OPT = ZUNDF_OPT /= 0.
LLCHECK = .FALSE.
CALL GETOPTION ('--verbose', LLVERBOSE)
CALL CHECKOPTIONS ()

IF (LLVERBOSE) THEN
  INIMES = 2
ELSE
  INIMES = 0
ENDIF

IF (CLFR /= '') THEN
  OPEN (ILUNR, FILE=TRIM (CLFR), FORM='FORMATTED', STATUS='UNKNOWN')
ELSE
  ILUNR = 6
ENDIF


WRITE (ILUNR, '(A16," ",A16," ",A16," ",A30," ",A30," ",A30," ",A30)') &
     & 'PREFIX', 'LEVEL', 'SUFFIX', 'MIN', 'MAX', 'AVG', 'RMS'

INBARP=0
INBARI=0
CALL FAITOU (IREP, ILUN, .TRUE., CLF, 'OLD', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMC)
CALL LFINFO (IREP, ILUN, 'SFX._FBUF_NAME', ILONGD, IPOSEX)

LLSURFEX = ILONGD > 0

IF (LLSURFEX) THEN
  CALL SURFEX_FIELD_BUF_READ_MISC (YLSC, ILUN, LDMPI=.FALSE.)

  INALDO = 0

  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
      CASE ('X2')
        INALDO = INALDO + YLSC%P (IARTL)%NDIM (2)
    END SELECT
  ENDDO
  
  ALLOCATE (CLNOMS (INALDO))
  
  INALDO = 0


  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
        CLNOMS (INALDO) = 'SFX.'//YLSC%P (IARTL)%NAME
      CASE ('X2')
        DO I = 1, YLSC%P (IARTL)%NDIM (2)
          INALDO = INALDO + 1
          WRITE (CLNOMS (INALDO), '("X",I3.3,A)') I, TRIM (YLSC%P (IARTL)%NAME)
        ENDDO
    END SELECT
  ENDDO

ELSE

  CALL LFINAF (IREP, ILUN, INALDO, INTROU, INARES, INAMAX)

  ALLOCATE (CLNOMS (INALDO))

  CALL LFIPOS (IREP, ILUN)

  DO IARTL = 1, INALDO

    CALL LFICAS (IREP, ILUN, CLNOMS (IARTL), ILONGD, IPOSEX, .TRUE.)

    IF (ILONGD == 0) EXIT

  ENDDO

ENDIF




! Sort fields by suffix, prefix, level 
ALLOCATE (ISORT (INALDO), CLSORT (INALDO))
DO IARTL = 1, INALDO
  CLNOMA = CLNOMS (IARTL)
  IF (ANY (CLSKIP == CLNOMA)) CYCLE
  IF (ASSOCIATED (CLSKIP1)) THEN
    IF (ANY (CLSKIP1 == CLNOMA)) CYCLE
  ENDIF
  IF (ASSOCIATED (CLONLY1)) THEN
    IF (.NOT. ANY (CLONLY1 == CLNOMA)) CYCLE
  ENDIF
  CALL FAQUIN (IREP, ILUN, CLPREF, INIVAU, CLSUFF, &
             & CLNOMA, LEN_TRIM (CLNOMA))
  WRITE (CLSORT (IARTL), '(A16,A16,I16.16)') CLPREF, CLSUFF, INIVAU
ENDDO
CALL QSORTC (INALDO, ISORT, CLSORT)
CLNOMS = CLNOMS (ISORT)
DEALLOCATE (ISORT, CLSORT)
!

DO IARTL = 1, INALDO

  CLNOMA = CLNOMS (IARTL)

  IF (ANY (CLSKIP == CLNOMA)) CYCLE

  IF (ASSOCIATED (CLSKIP1)) THEN
    IF (ANY (CLSKIP1 == CLNOMA)) CYCLE
  ENDIF

  IF (ASSOCIATED (CLONLY1)) THEN
    IF (.NOT. ANY (CLONLY1 == CLNOMA)) CYCLE
  ENDIF

  CALL FAQUIN (IREP, ILUN, CLPREF, INIVAU, CLSUFF, &
             & CLNOMA, LEN_TRIM (CLNOMA))

! Read field features
  CALL FANION (IREP, ILUN, CLPREF, INIVAU, CLSUFF, LLEXIS, LLCOSP, INGRIB, &
             & INBITS, ISTRON, IPUILA)

  IF (LLCOSP) CYCLE

  CALL FATCHA (IREP, CLNOMC, LLCOSP, ILCHAM)
  ALLOCATE (ZCHAMP (ILCHAM))


  CALL LFINFO (IREP, ILUN, CLNOMA, ILONGD, IPOSEX)

  LLUNDF = .TRUE.
  CALL FACILO (IREP, ILUN, CLPREF, INIVAU, CLSUFF,    &
             & ZCHAMP, LLCOSP, LDUNDF=LLUNDF, PUNDF=ZUNDF)

  IF ((.NOT. LLUNDF) .AND. LLUNDF_OPT) THEN
    LLUNDF = LLUNDF_OPT
    ZUNDF  = ZUNDF_OPT
  ENDIF
  IF (.NOT. LLUNDF) ZUNDF = HUGE (ZUNDF)

  ZMIN = MINVAL (ZCHAMP, MASK=ZCHAMP/=ZUNDF)
  ZMAX = MAXVAL (ZCHAMP, MASK=ZCHAMP/=ZUNDF)
  ZAVG = SUM (ZCHAMP, MASK=ZCHAMP/=ZUNDF) / COUNT (ZCHAMP/=ZUNDF)
  ZRMS = SQRT (SUM (ZCHAMP**2,MASK=ZCHAMP/=ZUNDF)) / COUNT (ZCHAMP/=ZUNDF)

  DO I = 1, LEN_TRIM (CLSUFF)
    IF (CLSUFF (I:I) == ' ') CLSUFF (I:I) = '_'
  ENDDO

  DO I = 1, LEN_TRIM (CLPREF)
    IF (CLPREF (I:I) == ' ') CLPREF (I:I) = '_'
  ENDDO

  WRITE (ILUNR, '(A16," ",I16," ",A16," ",E30.20," ",E30.20," ",E30.20," ",E30.20)') &
       & CLPREF, INIVAU, CLSUFF, ZMIN, ZMAX, ZAVG, ZRMS

  DEALLOCATE (ZCHAMP)

ENDDO

CALL FAIRME (IREP, ILUN, 'KEEP')

DEALLOCATE (CLNOMS)

IF (CLFR /= '') THEN
  CLOSE (ILUNR)
ENDIF

IF (LHOOK) CALL DR_HOOK ('FASTAT',1,ZHOOK_HANDLE)

END SUBROUTINE FASTAT

