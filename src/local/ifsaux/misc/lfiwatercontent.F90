SUBROUTINE LFIWATERCONTENT

USE PARKIND1, ONLY : JPRB, JPIM
USE XRD_GETOPTIONS
USE LFI_PRECISION
USE FA_MOD, ONLY : FA => FA_COM_DEFAULT

IMPLICIT NONE

#include "faieno.h"
#include "facilo.h"
#include "abor1.intfb.h"

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
CHARACTER (LEN=16), DIMENSION (5) :: CLNOMA = [ 'SURFTOT.RAIN    ', 'SURFTOT.SNOW    ', &
                                            &   'SURFTOT.GRAUPEL ', 'SURFTOT.LIQUID  ', &
                                            &   'SURFTOT.ICE     ' ]
CHARACTER (LEN=16) :: CLPREF, CLSUFF
INTEGER, PARAMETER :: ILUN = 77
CHARACTER (LEN=128) :: CLF
REAL (KIND=JPRB), ALLOCATABLE :: ZCHAMP1 (:), ZCHAMPA (:)
INTEGER :: INIVAU, JFLD, IGP
INTEGER :: INBARP, INBARI, IREP, ILCHAM
INTEGER :: INGRIB, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL, INBITS
LOGICAL :: LLEXIS, LLCOSP

REAL (KIND=JPRB) :: ZWMAX, ZWMIN
LOGICAL :: LLUNDEF



CALL INITOPTIONS (KOPTMIN=1)
CALL GETOPTION ('--fa-file', CLF, MND = .TRUE.)
ZWMAX = 1.000_JPRB; CALL GETOPTION ('--zwmax', ZWMAX)
ZWMIN = 0.001_JPRB; CALL GETOPTION ('--zwmin', ZWMIN)
CALL CHECKOPTIONS ()

INBARI=0
CALL FAITOU (IREP, ILUN, .TRUE., CLF, 'OLD', .TRUE., .FALSE., 0_JPIM, INBARP, INBARI, CLNOMC)

CALL FAREGU (ILUN, 'CMODEL=arpege-4dvarfr-assim-oper-fc', 1, 1)

CALL FATCHA (IREP, CLNOMC, .FALSE., ILCHAM)

ALLOCATE (ZCHAMP1 (ILCHAM), ZCHAMPA (ILCHAM))

ZCHAMPA = 0._JPRB

DO JFLD = 1, SIZE (CLNOMA)
  CLPREF = CLNOMA (JFLD) (1:4)
  CLSUFF = CLNOMA (JFLD) (5:16)
  INIVAU = 0
  CALL FACILO (IREP, ILUN, CLPREF, INIVAU, CLSUFF, ZCHAMP1, .FALSE.)
  ZCHAMPA = ZCHAMPA + ZCHAMP1
ENDDO

DO IGP = 1, ILCHAM
  ZCHAMPA (IGP) = LOG (CLAMP (ZCHAMPA (IGP), ZWMIN, ZWMAX) / ZWMIN) / LOG (ZWMAX / ZWMIN)
ENDDO


CALL FAVEUR (IREP, ILUN, INGRIB, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL)
CALL FANION (IREP, ILUN, CLNOMA (1)(1:4), 0, CLNOMA (1)(5:16), LLEXIS, &
           & LLCOSP, INGRIB, INBITS, ISTRON, IPUILA)
CALL FAGOTE (IREP, ILUN, INGRIB, INBITS, INBCSP, ISTRON, IPUILA, IDMOPL)
CALL FAIENO (IREP, ILUN, 'SURF', 0, 'TOT.WATER', ZCHAMPA, .FALSE.)

CALL FAIRME (IREP, ILUN, 'KEEP')

CONTAINS

REAL (KIND=JPRB) FUNCTION CLAMP (P, PMIN, PMAX)

REAL (KIND=JPRB), INTENT (IN) :: P, PMIN, PMAX

CLAMP = MIN (PMAX, MAX (PMIN, P))

END FUNCTION

END SUBROUTINE LFIWATERCONTENT

