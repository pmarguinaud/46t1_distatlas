MODULE READCOVERS_MOD

USE PARKIND1, ONLY : JPIM, JPRB

IMPLICIT NONE

TYPE DATE_t
  INTEGER (KIND=JPIM) :: YEAR = 0
  INTEGER (KIND=JPIM) :: MONTH = 0
  INTEGER (KIND=JPIM) :: DAY = 0
END TYPE DATE_t

TYPE DATE_TIME_t
  TYPE (DATE_t) :: TDATE
  REAL (KIND=JPRB) :: TIME = 0.
END TYPE DATE_TIME_t

TYPE COVERS_t
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_TOWN, XDATA_NATURE, XDATA_SEA, XDATA_WATER
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_VEGTYPE 
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:,:) :: XDATA_LAI
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_H_TREE
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_ROOT_DEPTH, XDATA_GROUND_DEPTH, XDATA_DICE
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:,:) :: XDATA_LAI_ALL_YEARS
  TYPE (DATE_TIME_t),  ALLOCATABLE, DIMENSION (:,:)   :: TDATA_SEED, TDATA_REAP
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:,:) :: XDATA_ALB_SOIL_NIR, XDATA_ALB_SOIL_VIS
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:,:) :: XDATA_ALB_VEG_NIR, XDATA_ALB_VEG_VIS
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_WATSUP, XDATA_IRRIG
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_Z0_TOWN, XDATA_BLD_HEIGHT, XDATA_WALL_O_HOR
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_BLD, XDATA_GARDEN
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_ALB_ROOF, XDATA_ALB_ROAD, XDATA_ALB_WALL
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_EMIS_ROOF, XDATA_EMIS_ROAD, XDATA_EMIS_WALL
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_HC_ROOF, XDATA_HC_ROAD, XDATA_HC_WALL
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_TC_ROOF, XDATA_TC_ROAD, XDATA_TC_WALL
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:,:)   :: XDATA_D_ROOF, XDATA_D_ROAD, XDATA_D_WALL
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_H_TRAFFIC, XDATA_LE_TRAFFIC
  REAL (KIND=JPRB),    ALLOCATABLE, DIMENSION (:)     :: XDATA_H_INDUSTRY, XDATA_LE_INDUSTRY
  INTEGER (KIND=JPIM) :: NECO2_START_YEAR = 2002, NECO2_END_YEAR = 2006
  INTEGER (KIND=JPIM) :: NVEGTYPE = 19, JPCOVER = 573, NCOVER_ECO1_END = 255, NCOVER_ECO2_START = 301, &
                       & NDATA_ROOF_LAYER = 3, NDATA_ROAD_LAYER = 3, NDATA_WALL_LAYER = 3
END TYPE COVERS_t

CONTAINS

SUBROUTINE READCOVERS (YDCOVER, KFILE, LDGARDEN, LDWATER_TO_NATURE, LDTOWN_TO_ROCK)

IMPLICIT NONE

INTEGER (KIND=JPIM), PARAMETER :: NVT_ROCK = 2   ! 2  ! no vegetation (rocks)

TYPE (COVERS_t), INTENT (INOUT) :: YDCOVER
INTEGER (KIND=JPIM), INTENT (IN) :: KFILE
LOGICAL, INTENT (IN) :: LDGARDEN, LDWATER_TO_NATURE, LDTOWN_TO_ROCK

INTEGER (KIND=JPIM) :: IERR_OPEN
INTEGER (KIND=JPIM) :: INB_COVER, INB_AN
INTEGER (KIND=JPIM) :: ICOVER, IREC
INTEGER (KIND=JPIM) :: JCOVER, JVEGTYPE
REAL(KIND=8)        :: ZTEMP1, ZTEMP2, ZTEMP3, ZTEMP4, ZTEMP5
REAL(KIND=8)        :: ZDATA_ROOF (YDCOVER%NDATA_ROOF_LAYER)
REAL(KIND=8)        :: ZDATA_ROAD (YDCOVER%NDATA_ROAD_LAYER)
REAL(KIND=8)        :: ZDATA_WALL (YDCOVER%NDATA_WALL_LAYER)
REAL (KIND=JPRB)    :: ZWORK


ALLOCATE (YDCOVER%XDATA_TOWN (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_NATURE (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_SEA (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_WATER (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_VEGTYPE (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_LAI (YDCOVER%JPCOVER,36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_H_TREE (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_ROOT_DEPTH (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_GROUND_DEPTH (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_DICE (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_LAI_ALL_YEARS (YDCOVER%JPCOVER,5*36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%TDATA_SEED (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%TDATA_REAP (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_ALB_SOIL_NIR (YDCOVER%JPCOVER,36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_ALB_SOIL_VIS (YDCOVER%JPCOVER,36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_ALB_VEG_NIR (YDCOVER%JPCOVER,36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_ALB_VEG_VIS (YDCOVER%JPCOVER,36,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_WATSUP (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_IRRIG (YDCOVER%JPCOVER,YDCOVER%NVEGTYPE))
ALLOCATE (YDCOVER%XDATA_Z0_TOWN (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_BLD_HEIGHT (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_WALL_O_HOR (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_BLD (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_GARDEN (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_ALB_ROOF (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_ALB_ROAD (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_ALB_WALL (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_EMIS_ROOF (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_EMIS_ROAD (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_EMIS_WALL (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_HC_ROOF (YDCOVER%JPCOVER,YDCOVER%NDATA_ROOF_LAYER))
ALLOCATE (YDCOVER%XDATA_HC_ROAD (YDCOVER%JPCOVER,YDCOVER%NDATA_ROAD_LAYER))
ALLOCATE (YDCOVER%XDATA_HC_WALL (YDCOVER%JPCOVER,YDCOVER%NDATA_WALL_LAYER))
ALLOCATE (YDCOVER%XDATA_TC_ROOF (YDCOVER%JPCOVER,YDCOVER%NDATA_ROOF_LAYER))
ALLOCATE (YDCOVER%XDATA_TC_ROAD (YDCOVER%JPCOVER,YDCOVER%NDATA_ROAD_LAYER))
ALLOCATE (YDCOVER%XDATA_TC_WALL (YDCOVER%JPCOVER,YDCOVER%NDATA_WALL_LAYER))
ALLOCATE (YDCOVER%XDATA_D_ROOF  (YDCOVER%JPCOVER,YDCOVER%NDATA_ROOF_LAYER))
ALLOCATE (YDCOVER%XDATA_D_ROAD  (YDCOVER%JPCOVER,YDCOVER%NDATA_ROAD_LAYER))
ALLOCATE (YDCOVER%XDATA_D_WALL  (YDCOVER%JPCOVER,YDCOVER%NDATA_WALL_LAYER))
ALLOCATE (YDCOVER%XDATA_H_TRAFFIC (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_LE_TRAFFIC (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_H_INDUSTRY (YDCOVER%JPCOVER))
ALLOCATE (YDCOVER%XDATA_LE_INDUSTRY (YDCOVER%JPCOVER))

IF (KFILE==1) THEN
  OPEN(41,FILE='ecoclimapI_covers_param.bin',FORM='UNFORMATTED',ACCESS='DIRECT', &
          recl=20*8,STATUS='OLD',IOSTAT=IERR_OPEN)  
  IF (IERR_OPEN /= 0 ) THEN
          STOP 'ERROR WHILE OPENING ''ecoclimapI_covers_param.bin'' THIS FILE IS NEEDED AND MUST BE'// &
                  ' IN (OR LINKED TO) THE RUN DIRECTORY'
  ENDIF        
  INB_COVER = YDCOVER%NCOVER_ECO1_END
  INB_AN = 1
ELSEIF (KFILE==2) THEN
  OPEN(41,FILE='ecoclimapII_eu_covers_param.bin',FORM='UNFORMATTED',ACCESS='DIRECT',  &
          recl=20*8,STATUS='OLD',IOSTAT=IERR_OPEN)
  IF (IERR_OPEN /= 0 ) THEN
          STOP 'ERROR WHILE OPENING ''ecoclimapII_eu_covers_param.bin'' THIS FILE IS NEEDED AND MUST BE'// &
                  ' IN (OR LINKED TO) THE RUN DIRECTORY'
  ENDIF
  INB_COVER = YDCOVER%JPCOVER - YDCOVER%NCOVER_ECO2_START + 1
  INB_AN = YDCOVER%NECO2_END_YEAR - YDCOVER%NECO2_START_YEAR + 1
ENDIF
!
IREC=0
DO JCOVER = 1,INB_COVER
  IREC = IREC+1
  READ(41,REC=IREC) ICOVER
  !fractions of tiles
  IREC=IREC+1
  READ(41,REC=IREC) ZTEMP1, ZTEMP2, ZTEMP3, ZTEMP4
  YDCOVER%XDATA_TOWN   (ICOVER) = REAL (ZTEMP1)
  YDCOVER%XDATA_NATURE (ICOVER) = REAL (ZTEMP2)
  YDCOVER%XDATA_WATER  (ICOVER) = REAL (ZTEMP3)
  YDCOVER%XDATA_SEA    (ICOVER) = REAL (ZTEMP4)
  !natural part
  IF (YDCOVER%XDATA_NATURE(ICOVER).NE.0.) CALL READ_NATURE
  !urban part
  IF (YDCOVER%XDATA_TOWN(ICOVER).NE.0.) THEN
    !main town parameters
    IREC=IREC+1
    READ(41,REC=IREC) ZTEMP1, ZTEMP2, ZTEMP3, ZTEMP4, ZTEMP5
    YDCOVER%XDATA_Z0_TOWN    (ICOVER) = REAL (ZTEMP1)
    YDCOVER%XDATA_BLD_HEIGHT (ICOVER) = REAL (ZTEMP2)
    YDCOVER%XDATA_WALL_O_HOR (ICOVER) = REAL (ZTEMP3)
    YDCOVER%XDATA_BLD        (ICOVER) = REAL (ZTEMP4)
    YDCOVER%XDATA_GARDEN     (ICOVER) = REAL (ZTEMP5)
    !town albedos
    IREC=IREC+1
    READ(41,rec=IREC) ZTEMP1, ZTEMP2, ZTEMP3
    YDCOVER%XDATA_ALB_ROOF (ICOVER) = REAL (ZTEMP1)
    YDCOVER%XDATA_ALB_ROAD (ICOVER) = REAL (ZTEMP2)
    YDCOVER%XDATA_ALB_WALL (ICOVER) = REAL (ZTEMP3)
    !town emissivities
    IREC=IREC+1
    READ(41,rec=IREC) ZTEMP1, ZTEMP2, ZTEMP3
    YDCOVER%XDATA_EMIS_ROOF (ICOVER) = REAL (ZTEMP1)
    YDCOVER%XDATA_EMIS_ROAD (ICOVER) = REAL (ZTEMP2)
    YDCOVER%XDATA_EMIS_WALL (ICOVER) = REAL (ZTEMP3)
    !town heat capacity
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROOF; YDCOVER%XDATA_HC_ROOF(ICOVER,:) = REAL (ZDATA_ROOF)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROAD; YDCOVER%XDATA_HC_ROAD(ICOVER,:) = REAL (ZDATA_ROAD)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_WALL; YDCOVER%XDATA_HC_WALL(ICOVER,:) = REAL (ZDATA_WALL)
    !town thermal conductivity
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROOF; YDCOVER%XDATA_TC_ROOF(ICOVER,:) = REAL (ZDATA_ROOF)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROAD; YDCOVER%XDATA_TC_ROAD(ICOVER,:) = REAL (ZDATA_ROAD)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_WALL; YDCOVER%XDATA_TC_WALL(ICOVER,:) = REAL (ZDATA_WALL)
    !town depths
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROOF; YDCOVER%XDATA_D_ROOF(ICOVER,:) = REAL (ZDATA_ROOF)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_ROAD; YDCOVER%XDATA_D_ROAD(ICOVER,:) = REAL (ZDATA_ROAD)
    IREC=IREC+1
    READ(41,rec=IREC) ZDATA_WALL; YDCOVER%XDATA_D_WALL(ICOVER,:) = REAL (ZDATA_WALL)
    !traffic and industry fluxes
    IREC=IREC+1
    READ(41,rec=IREC) ZTEMP1, ZTEMP2, ZTEMP3, ZTEMP4
    YDCOVER%XDATA_H_TRAFFIC   (ICOVER) = REAL (ZTEMP1)
    YDCOVER%XDATA_LE_TRAFFIC  (ICOVER) = REAL (ZTEMP2)
    YDCOVER%XDATA_H_INDUSTRY  (ICOVER) = REAL (ZTEMP3)
    YDCOVER%XDATA_LE_INDUSTRY (ICOVER) = REAL (ZTEMP4)
    IF (YDCOVER%XDATA_GARDEN(ICOVER).NE.0. .AND. YDCOVER%XDATA_NATURE(ICOVER).EQ.0.) CALL READ_NATURE
  ENDIF
ENDDO
CLOSE(41)

!-------------------------------------------------------------------------------
! Change water (not lake) to nature                                             
!-------------------------------------------------------------------------------
IF(LDWATER_TO_NATURE)THEN
  DO JCOVER=1,YDCOVER%JPCOVER
     IF(YDCOVER%XDATA_WATER(JCOVER)>0.0_JPRB.AND.YDCOVER%XDATA_WATER(JCOVER)<1.0_JPRB)THEN
       YDCOVER%XDATA_NATURE(JCOVER)=YDCOVER%XDATA_NATURE(JCOVER)+YDCOVER%XDATA_WATER(JCOVER)
       YDCOVER%XDATA_WATER (JCOVER)=0.0_JPRB
     ENDIF
  ENDDO
ENDIF
!-------------------------------------------------------------------------------
! Change town to rock but keep other natural fraction                           
!-------------------------------------------------------------------------------
IF(LDTOWN_TO_ROCK)THEN
  DO JCOVER=1,YDCOVER%JPCOVER
     IF(YDCOVER%XDATA_TOWN(JCOVER)>0.0_JPRB.OR.YDCOVER%XDATA_GARDEN(JCOVER)>0.0_JPRB)THEN
       YDCOVER%XDATA_NATURE(JCOVER) = YDCOVER%XDATA_NATURE(JCOVER) + YDCOVER%XDATA_GARDEN(JCOVER) * YDCOVER%XDATA_TOWN(JCOVER)
       YDCOVER%XDATA_TOWN  (JCOVER) = YDCOVER%XDATA_TOWN  (JCOVER) * ( 1.0_JPRB - YDCOVER%XDATA_GARDEN(JCOVER))
       YDCOVER%XDATA_GARDEN(JCOVER) = 0.0_JPRB
       ZWORK=YDCOVER%XDATA_NATURE(JCOVER)+YDCOVER%XDATA_TOWN(JCOVER)
       DO JVEGTYPE=1,YDCOVER%NVEGTYPE
             YDCOVER%XDATA_VEGTYPE(JCOVER,JVEGTYPE)=YDCOVER%XDATA_VEGTYPE(JCOVER,JVEGTYPE)*YDCOVER%XDATA_NATURE(JCOVER)/ZWORK
       ENDDO
       YDCOVER%XDATA_VEGTYPE(JCOVER,NVT_ROCK) = YDCOVER%XDATA_VEGTYPE(JCOVER,NVT_ROCK)+YDCOVER%XDATA_TOWN(JCOVER)/ZWORK
       YDCOVER%XDATA_NATURE(JCOVER)=YDCOVER%XDATA_NATURE(JCOVER)+YDCOVER%XDATA_TOWN(JCOVER)
       YDCOVER%XDATA_TOWN  (JCOVER)=0.0_JPRB
!      Initialise some variables
       YDCOVER%XDATA_LAI          (JCOVER,:,NVT_ROCK) = 0.0_JPRB
       YDCOVER%XDATA_LAI_ALL_YEARS(JCOVER,:,NVT_ROCK) = 0.0_JPRB
       YDCOVER%XDATA_ROOT_DEPTH   (JCOVER,  NVT_ROCK) = 0.2_JPRB
       YDCOVER%XDATA_GROUND_DEPTH (JCOVER,  NVT_ROCK) = 0.2_JPRB
       YDCOVER%XDATA_DICE         (JCOVER,  NVT_ROCK) = 0.2_JPRB
       YDCOVER%XDATA_ALB_VEG_NIR  (JCOVER,:,NVT_ROCK) = 0.3_JPRB
       YDCOVER%XDATA_ALB_VEG_VIS  (JCOVER,:,NVT_ROCK) = 0.1_JPRB
       YDCOVER%XDATA_ALB_SOIL_NIR (JCOVER,:,NVT_ROCK) = 0.3_JPRB
       YDCOVER%XDATA_ALB_SOIL_VIS (JCOVER,:,NVT_ROCK) = 0.1_JPRB
     ENDIF
  ENDDO
ELSE
!-------------------------------------------------------------------------------
! Town is kept, but if gardens are not treated specifically,                    
! they are included into nature fraction.                                       
!-------------------------------------------------------------------------------
  IF (.NOT. LDGARDEN) THEN
    YDCOVER%XDATA_NATURE     = YDCOVER%XDATA_NATURE + YDCOVER%XDATA_GARDEN * YDCOVER%XDATA_TOWN
    YDCOVER%XDATA_TOWN       = YDCOVER%XDATA_TOWN   * (1._JPRB - YDCOVER%XDATA_GARDEN)
    YDCOVER%XDATA_GARDEN     = 0._JPRB
    YDCOVER%XDATA_BLD        = YDCOVER%XDATA_BLD / (1._JPRB - YDCOVER%XDATA_GARDEN)
    YDCOVER%XDATA_WALL_O_HOR = YDCOVER%XDATA_WALL_O_HOR / (1._JPRB - YDCOVER%XDATA_GARDEN)
  ENDIF
ENDIF
CONTAINS
!
SUBROUTINE READ_NATURE
!
REAL(KIND=8) :: ZINTER (12)
REAL(KIND=8) :: ZDATA_VEGTYPE (SIZE (YDCOVER%XDATA_VEGTYPE, 2))

INTEGER                     :: JVEGTYPE, JLAI
!
!fractions of vegtypes
IREC=IREC+1
READ(41,REC=IREC) ZDATA_VEGTYPE
YDCOVER%XDATA_VEGTYPE (ICOVER, :) = REAL (ZDATA_VEGTYPE)
!
!albedos for the soil
IF (KFILE<=2 .AND. YDCOVER%XDATA_NATURE(ICOVER)/=0.) THEN
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:)
  YDCOVER%XDATA_ALB_SOIL_NIR(ICOVER, 1:12,1) = REAL (ZINTER(:))
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:)
  YDCOVER%XDATA_ALB_SOIL_NIR(ICOVER,13:24,1) = REAL (ZINTER(:))
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:) 
  YDCOVER%XDATA_ALB_SOIL_NIR(ICOVER,25:36,1) = REAL (ZINTER(:))
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:)
  YDCOVER%XDATA_ALB_SOIL_VIS(ICOVER, 1:12,1) = REAL (ZINTER(:))
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:) 
  YDCOVER%XDATA_ALB_SOIL_VIS(ICOVER,13:24,1) = REAL (ZINTER(:))
  IREC=IREC+1
  READ(41,REC=IREC) ZINTER(:) 
  YDCOVER%XDATA_ALB_SOIL_VIS(ICOVER,25:36,1) = REAL (ZINTER(:))
ENDIF
!
DO JVEGTYPE=1,YDCOVER%NVEGTYPE
  !not null fraction of vegtype
  IF (YDCOVER%XDATA_VEGTYPE(ICOVER,JVEGTYPE).NE.0.) THEN
    !root and soil depths
    IREC=IREC+1      
    READ(41,REC=IREC) ZTEMP1, ZTEMP2, ZTEMP3
    YDCOVER%XDATA_ROOT_DEPTH   (ICOVER,JVEGTYPE) = REAL (ZTEMP1)
    YDCOVER%XDATA_GROUND_DEPTH (ICOVER,JVEGTYPE) = REAL (ZTEMP2)
    YDCOVER%XDATA_DICE         (ICOVER,JVEGTYPE) = REAL (ZTEMP3)
    IF (JVEGTYPE.GT.3) THEN
      !LAI
      DO JLAI=1,INB_AN*3
        IREC=IREC+1
        IF (KFILE==1) THEN
          READ(41,REC=IREC) ZINTER(:)
          YDCOVER%XDATA_LAI(ICOVER,(JLAI-1)*12+1:JLAI*12,JVEGTYPE) = REAL (ZINTER(:))
        ELSEIF (KFILE==2) THEN
          READ(41,REC=IREC) ZINTER(:)
          YDCOVER%XDATA_LAI_ALL_YEARS(ICOVER,(JLAI-1)*12+1:JLAI*12,JVEGTYPE) = REAL (ZINTER(:))
        ENDIF
      ENDDO
      !Heights of trees
      IF ((JVEGTYPE < 7) .OR. (JVEGTYPE > 12 .AND. JVEGTYPE /= 18)) THEN
        IREC=IREC+1
        READ(41,REC=IREC) ZTEMP1
        YDCOVER%XDATA_H_TREE(ICOVER,JVEGTYPE) = ZTEMP1
      ENDIF
      !albedos for the vegetation
      IF (KFILE<=2 .AND. YDCOVER%XDATA_NATURE(ICOVER)/=0.) THEN
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_NIR(ICOVER, 1:12,JVEGTYPE) = REAL (ZINTER(:))
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_NIR(ICOVER,13:24,JVEGTYPE) = REAL (ZINTER(:))
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_NIR(ICOVER,25:36,JVEGTYPE) = REAL (ZINTER(:))
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_VIS(ICOVER, 1:12,JVEGTYPE) = REAL (ZINTER(:))
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_VIS(ICOVER,13:24,JVEGTYPE) = REAL (ZINTER(:))
        IREC=IREC+1
        READ(41,REC=IREC) ZINTER(:) 
        YDCOVER%XDATA_ALB_VEG_VIS(ICOVER,25:36,JVEGTYPE) = REAL (ZINTER(:))
      ENDIF
    ELSE
      !LAI for bare areas
      IF (KFILE==1) THEN
        YDCOVER%XDATA_LAI(ICOVER,:,JVEGTYPE) = 0.
      ELSEIF (KFILE==2) THEN
        YDCOVER%XDATA_LAI_ALL_YEARS(ICOVER,:,JVEGTYPE) = 0.
      ENDIF
      YDCOVER%XDATA_ALB_VEG_NIR(ICOVER,:,JVEGTYPE) = 0.3
      YDCOVER%XDATA_ALB_VEG_VIS(ICOVER,:,JVEGTYPE) = 0.1
    ENDIF
    !irrigation
    IF (JVEGTYPE.EQ.8 .AND. KFILE.EQ.1 .OR. JVEGTYPE.EQ.9 .AND. KFILE.EQ.2) THEN
      IREC=IREC+1
      READ(41,REC=IREC) YDCOVER%TDATA_SEED(ICOVER,JVEGTYPE)%TDATE%MONTH, YDCOVER%TDATA_SEED(ICOVER,JVEGTYPE)%TDATE%DAY, &
        YDCOVER%TDATA_REAP(ICOVER,JVEGTYPE)%TDATE%MONTH, YDCOVER%TDATA_REAP(ICOVER,JVEGTYPE)%TDATE%DAY, &
        ZTEMP1, ZTEMP2
      YDCOVER%XDATA_WATSUP (ICOVER,JVEGTYPE) = REAL (ZTEMP1)
      YDCOVER%XDATA_IRRIG  (ICOVER,JVEGTYPE) = REAL (ZTEMP2)
    ENDIF
  ENDIF
ENDDO
END SUBROUTINE 

END SUBROUTINE 

END MODULE

