SUBROUTINE FACONVGRIB

USE PARKIND1, ONLY : JPIM, JPRB, JPIA, JPIB
USE XRD_GETOPTIONS
#ifdef UNDEF
USE MODD_IO_SURF_ARO, ONLY : SURFEX_FIELD_BUF_READ_MISC, &
                           & SURFEX_FIELD_BUF_CACHE
#endif
USE FADUP_MOD, ONLY : FADUPU1, FADUPU2, FADUPU3, FADUPU4, FADUP_PARAMS
USE FA_MOD, ONLY : FA_COM
USE OML_MOD, ONLY : OML_MY_THREAD, OML_NUM_THREADS
USE YOMHOOK, ONLY : DR_HOOK, LHOOK
USE LFI_PRECISION
                           

IMPLICIT NONE

#include "facilo.h"
#include "facono.h"
#include "fadoco.h"
#include "facono_mt.h"

INTEGER, PARAMETER :: ILUN1 = 77, ILUN2 = 88, ILUNF = 99, ILUNSIZETIME = 66
CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'cadre', CLNOMCF = 'cadre.f'
CHARACTER (LEN=16), PARAMETER :: CLSKIP (11) = &
& (/ 'CADRE-DIMENSIONS', 'CADRE-FRANKSCHMI',   &
&    'CADRE-REDPOINPOL', 'CADRE-SINLATITUD',   &
&    'CADRE-FOCOHYBRID', 'FULLPOS         ',   &
&    'DATE-DES-DONNEES', 'ARP             ',   &
&    'DATX-DES-DONNEES', 'ALD             ',   &
&    'GEOMASK.FPOS    ' /)
CHARACTER (LEN=16), POINTER :: CLSKIP1 (:) => NULL ()
CHARACTER (LEN=16), POINTER :: CLONLY1 (:) => NULL ()
CHARACTER (LEN=32), POINTER :: CLBITS  (:) => NULL ()

CHARACTER (LEN=128) :: CLF1, CLF2, CLFF
INTEGER (KIND=JPIM) :: INBARP, INBARI, IREP
INTEGER (KIND=JPIM) :: ILONG, IPOSEX, INALDO, INTROU, INARES, INAMAX
INTEGER (KIND=JPIM) :: IARTL, INIVAU
CHARACTER (LEN=16),  ALLOCATABLE :: CLNOMS (:)
CHARACTER (LEN=48),  ALLOCATABLE :: CLSORT (:)
INTEGER (KIND=JPIM), ALLOCATABLE :: ISORT (:)
CHARACTER (LEN=16) :: CLPREF, CLSUFF
INTEGER (KIND=JPIM) :: INGRIB, INBITS, ISTRON, IPUILA
INTEGER (KIND=JPIM) :: INBPDG, INBCSP, IDMOPL
LOGICAL :: LLMASK, LLCOSP, LLEXIS, LLCOSP_F, LLEXIS_F
INTEGER (KIND=JPIM) :: IDATEF (22)
REAL (KIND=JPRB) :: ZCHAMP (10000**2), ZMASK (10000**2), ZVALCO (10000**2)
INTEGER :: INGRIB_OPT, INGRIB_OUT
INTEGER :: INBITS_OPT, ISTRON_OPT
INTEGER :: ILONGD, ILNOMA
CHARACTER(LEN=16) :: CLNOMA
LOGICAL :: LLFAGRTW, LLFAGRTR, LLUNDF1_OPT, LLUNDF1, LLUNDF2, LLUNDF3, LLEXTERN
REAL (KIND=JPRB) :: ZUNDF1_OPT, ZUNDF1, ZUNDF2, ZUNDF3
CHARACTER(LEN=16) :: CLIDEN
LOGICAL :: LLGRIB
INTEGER (KIND=JPIB) :: I, J
INTEGER (KIND=JPIM) :: INUMOD
LOGICAL :: LLPROGRID
LOGICAL :: LLSURFEX
#ifdef UNDEF
TYPE (SURFEX_FIELD_BUF_CACHE) :: YLSC
#endif
TYPE (FADUP_PARAMS) :: YLFAPARAM
TYPE (FA_COM), ALLOCATABLE, TARGET :: YLFABYTID (:)
TYPE (FA_COM), POINTER     :: YLFA
INTEGER :: IITID, INTID, IOPENMP, JFLD
INTEGER :: ICOGRIF, ILCHAM
CHARACTER (LEN=32) :: CLCLEF
INTEGER :: IFACDEC, INIMES
LOGICAL :: LLDUMPSIZETIME, LLVERBOSE
CHARACTER (LEN=256) :: CLFILESIZETIME, CLKIND
CHARACTER (LEN=256) :: CLMODEL

REAL (KIND=JPRB) :: ZHOOK_HANDLE
REAL (KIND=JPRB) :: ZHOOK_HANDLE_AUX1
REAL (KIND=JPRB) :: ZHOOK_HANDLE_AUX2
REAL (KIND=JPRB) :: ZTIME1, ZTIME2, ZTIMEC, ZTIMED, ZTIMEE
LOGICAL :: LLSORT
INTEGER :: IDMOD

IF (LHOOK) CALL DR_HOOK ('FACONVGRIB',0,ZHOOK_HANDLE)

IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:INIT',0,ZHOOK_HANDLE_AUX1)

CALL INITOPTIONS (KOPTMIN=1)
CALL GETOPTION ('--fa-file-1', CLF1, MND = .TRUE., USE = "FA input file")
CALL GETOPTION ('--fa-file-2', CLF2, MND = .TRUE., USE = "FA output file")
INGRIB_OPT = -2
CALL GETOPTION ('--ingrib', INGRIB_OPT, USE = "Grib encoding method; 0=IEEE64, &
                                             &2=GRIB0, 3=GRIB1 (GRIBEX), 121=GRIB2 simple packing, &
                                             &141=GRIB2 second order packing, &
                                             &161=GRIB1 simple packing (grib_api), &
                                             &181=GRIB1 second order packing (grib_api)")
INBITS_OPT = -2
CALL GETOPTION ('--inbits', INBITS_OPT, USE = "Number of bits used for packing")
ISTRON_OPT = -2
CALL GETOPTION ('--istron', ISTRON_OPT, USE = "Truncation not to be packed")
CALL GETOPTION ('--skip', CLSKIP1, USE = "List of LFI articles to skip")
CALL GETOPTION ('--only', CLONLY1, USE = "List of LFI articles to process")
CALL GETOPTION ('--fagrtw', LLFAGRTW)
CALL GETOPTION ('--fagrtr', LLFAGRTR)
ZUNDF1_OPT = 0._JPRB
CALL GETOPTION ('--zundf-in', ZUNDF1_OPT, USE = "Undefined value for input file")
LLUNDF1_OPT = ZUNDF1_OPT /= 0._JPRB
ZUNDF2 = 0._JPRB
CALL GETOPTION ('--zundf-out', ZUNDF2, USE = "Undefined value for output file (for IEEE64)")
LLUNDF2 = ZUNDF2 /= 0._JPRB
CALL GETOPTION ('--grib', LLGRIB)
CALL GETOPTION ('--bits', CLBITS)
CLFF=''
CALL GETOPTION ('--bits-from', CLFF, USE = "Take number of bits and unpacked sub-truncation from this file")
INUMOD=221
CALL GETOPTION ('--inumod', INUMOD, USE = "Model id")
CALL GETOPTION ('--progrid', LLPROGRID)
IOPENMP = 0
CALL GETOPTION ('--openmp', IOPENMP)
CLCLEF = ''
CALL GETOPTION ('--clef', CLCLEF)
IFACDEC = 1
CALL GETOPTION ('--facdec', IFACDEC)
CLFILESIZETIME = ''
CALL GETOPTION ('--dump-size-time', CLFILESIZETIME, USE = "Dump size and CPU time for each encoded field")
LLDUMPSIZETIME = CLFILESIZETIME /= ''
CALL GETOPTION ('--sort', LLSORT, USE = "Sort fields before processing them")
CALL GETOPTION ('--extern', LLEXTERN)
IDMOD = 0
CALL GETOPTION ('--idmod', IDMOD)
CLMODEL = ''
CALL GETOPTION ('--cmodel', CLMODEL)
CALL GETOPTION ('--verbose', LLVERBOSE)
CALL CHECKOPTIONS ()

IF (LLDUMPSIZETIME) THEN
  OPEN (ILUNSIZETIME, FILE=TRIM (CLFILESIZETIME), FORM="FORMATTED", STATUS="UNKNOWN")
  WRITE (ILUNSIZETIME, '(A16," ",A16," ",A16," ",A16," ",A16," ",A16," ",A16," ",A16)') &
       & 'PREFIX', 'LEVEL', 'SUFFIX', 'ENCODE', 'DECODE', 'WRITE', 'SIZE', 'KIND'
ENDIF

IF (LLVERBOSE) THEN
  INIMES = 2
ELSE
  INIMES = 0
ENDIF


IF (ASSOCIATED (CLBITS)) THEN
  DO I = 1, SIZE (CLBITS)
    DO J = 1, LEN (CLBITS)
      IF (CLBITS (I) (J:J) == '*') CLBITS (I) (J:J) = ' '
    ENDDO
  ENDDO
ENDIF

CALL SYSTEM ('rm -f '//TRIM (CLF2))
IF (LLEXTERN) &
CALL SYSTEM ('rm -f '//TRIM (CLF2)//'.grib')

CALL FAIGRA

IF (LLFAGRTR) CALL FAGRTR (IREP, 12)

INBARP=0
INBARI=0
CALL FAITOU (IREP, ILUN1, .TRUE., CLF1, 'OLD', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMC)
CALL FALSIF (IREP, ILUN1, CLIDEN)
CALL FADIEX (IREP, ILUN1, IDATEF)

CALL LFINFO (IREP, ILUN1, 'GEOMASK.FPOS', ILONG, IPOSEX)
LLMASK = ILONG > 0

IF (LLMASK) THEN
  CALL FACILE (IREP, ILUN1, 'GEO', 0, 'MASK.FPOS', ZMASK, .FALSE.)
  CALL FATCHA (IREP, CLNOMC, .FALSE., ILCHAM)
  LLMASK = .NOT. (ILCHAM == COUNT (ZMASK (1:ILCHAM) > 0))
ENDIF

CALL LFINFO (IREP, ILUN1, 'SFX._FBUF_NAME', ILONG, IPOSEX)

LLSURFEX = ILONG > 0

INBARP=0
INBARI=0
CALL FAITOU (IREP, ILUN2, .TRUE., CLF2, 'NEW', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMC)
CALL FAUTIF (IREP, ILUN2, CLIDEN)
CALL FANDAX (IREP, ILUN2, IDATEF)

IF (CLCLEF /= '') CALL FAREGU (ILUN2, TRIM (CLCLEF), 1, 1)
CALL FAREGU (ILUN2, 'FACDEC', IFACDEC, 1)
IF (LLEXTERN) CALL FAREGU (ILUN2, 'EXTERN', 1, 1)
IF (IDMOD /= 0) CALL FAREGU (ILUN2, 'IDMOD', IDMOD, 1)
IF (CLMODEL /= '') CALL FAREGU (ILUN2, 'CMODEL='//TRIM (CLMODEL), 1, 1)

IF (CLFF /= '') THEN
  INBARP=0
  INBARI=0
  CALL FAITOU (IREP, ILUNF, .TRUE., CLFF, 'OLD', .TRUE., LLVERBOSE, INIMES, INBARP, INBARI, CLNOMCF)
ENDIF


LLSURFEX = .FALSE.
IF (LLSURFEX) THEN
#ifdef UNDEF

  CALL SURFEX_FIELD_BUF_READ_MISC (YLSC, ILUN1, LDMPI=.FALSE.)

  INALDO = 0

  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
      CASE ('X2')
        INALDO = INALDO + YLSC%P (IARTL)%NDIM (2)
    END SELECT
  ENDDO
  
  ALLOCATE (CLNOMS (INALDO))
  
  INALDO = 0

  DO IARTL = 1, SIZE (YLSC%P)
    SELECT CASE (YLSC%P (IARTL)%TYPE)
      CASE ('X1')
        INALDO = INALDO + 1
        CLNOMS (INALDO) = 'SFX.'//YLSC%P (IARTL)%NAME
      CASE ('X2')
        DO I = 1, YLSC%P (IARTL)%NDIM (2)
          INALDO = INALDO + 1
          WRITE (CLNOMS (INALDO), '("X",I3.3,A)') I, TRIM (YLSC%P (IARTL)%NAME)
        ENDDO
    END SELECT
  ENDDO

#endif
ELSE

  CALL LFINAF (IREP, ILUN1, INALDO, INTROU, INARES, INAMAX)

  ALLOCATE (CLNOMS (INALDO))

  CALL LFIPOS (IREP, ILUN1)

  DO IARTL = 1, INALDO

    CALL LFICAS (IREP, ILUN1, CLNOMS (IARTL), ILONG, IPOSEX, .TRUE.)

    IF (ILONG == 0) EXIT

  ENDDO

ENDIF


IF (LLSORT) THEN
! Sort fields by suffix, prefix, level 
  ALLOCATE (ISORT (INALDO), CLSORT (INALDO))
  DO IARTL = 1, INALDO
    CLNOMA = CLNOMS (IARTL)
    IF (ANY (CLSKIP == CLNOMA)) CYCLE
    IF (ASSOCIATED (CLSKIP1)) THEN
      IF (ANY (CLSKIP1 == CLNOMA)) CYCLE
    ENDIF
    IF (ASSOCIATED (CLONLY1)) THEN
      IF (.NOT. ANY (CLONLY1 == CLNOMA)) CYCLE
    ENDIF
    CALL FAQUIN (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, &
               & CLNOMA, LEN_TRIM (CLNOMA))
    WRITE (CLSORT (IARTL), '(A16,A16,I16.16)') CLPREF, CLSUFF, INIVAU
  ENDDO
  CALL QSORTC (INALDO, ISORT, CLSORT)
  CLNOMS = CLNOMS (ISORT)
  DEALLOCATE (ISORT, CLSORT)
ENDIF


IF (LLGRIB) THEN
  CALL FAREGU (ILUN2, 'EXTERN', 1_JPIM, 1_JPIM)
ENDIF
IF (LLPROGRID) THEN
  CALL FAPRST (IREP, ILUN2, INUMOD, 0_JPIM, 'L', LLPROGRID)
  INGRIB_OPT = 3
ENDIF

IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:INIT',1,ZHOOK_HANDLE_AUX1)
!

IF (IOPENMP > 0) THEN

  CALL FADUPU1 (YLFAPARAM, ILUN2)

  YLFAPARAM%LOUVR = .FALSE. 
  YLFAPARAM%LNOMM = .FALSE.

  INTID = OML_NUM_THREADS ()
  ALLOCATE (YLFABYTID (INTID))

!$OMP PARALLEL PRIVATE (IITID)
    
  IITID = OML_MY_THREAD ()
  CALL FADUPU2 (YLFABYTID (IITID), YLFAPARAM, ILUN2)
    
!$OMP END PARALLEL

ENDIF

IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:IARTL_LOOP',0,ZHOOK_HANDLE_AUX1)

DO IARTL = 1, INALDO

  IF (ANY (CLSKIP == CLNOMS (IARTL))) CYCLE

  IF (ASSOCIATED (CLSKIP1)) THEN
    IF (ANY (CLSKIP1 == CLNOMS (IARTL))) CYCLE
  ENDIF

  IF (ASSOCIATED (CLONLY1)) THEN
    IF (.NOT. ANY (CLONLY1 == CLNOMS (IARTL))) CYCLE
  ENDIF

  CALL FAQUIN (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, &
             & CLNOMS (IARTL), LEN_TRIM (CLNOMS (IARTL)))

  CALL FAVEUR (IREP, ILUN1, INGRIB, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL)

  IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:FACILE',0,ZHOOK_HANDLE_AUX2)

  CALL FANION (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, LLEXIS, LLCOSP, INGRIB, INBITS, ISTRON, IPUILA)

  IF (CLFF /= '') THEN
    CALL FAVEUR (IREP, ILUNF, INGRIB, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL)
    CALL FANION (IREP, ILUNF, CLPREF, INIVAU, CLSUFF, LLEXIS_F, LLCOSP_F, INGRIB, INBITS, ISTRON, IPUILA)
  ENDIF

! Read input file & reorder to model layout
  ZCHAMP = 0

  CALL FATCHA (IREP, CLNOMC, LLCOSP, ILCHAM)
  LLUNDF1 = .FALSE.
  CALL FACILO (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, ZCHAMP, LLCOSP, &
             & LDUNDF=LLUNDF1, PUNDF=ZUNDF1)

  IF (LLMASK .AND. (.NOT. LLCOSP) .AND. (.NOT. LLUNDF1)) THEN
    LLUNDF1 = .TRUE.
    ZUNDF1  = HUGE (ZUNDF1)
    WHERE (.NOT. (ZMASK (1:ILCHAM) > 0))
      ZCHAMP = ZUNDF1
    END WHERE
  ELSEIF (LLUNDF1_OPT) THEN
    LLUNDF1 = LLUNDF1_OPT
    ZUNDF1  = ZUNDF1_OPT
  ENDIF

  IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:FACILE',1,ZHOOK_HANDLE_AUX2)

! Set options for writing
  IF ((INGRIB_OPT >= 0) .AND. ((INGRIB >= 1 .AND. INGRIB <= 5) .OR. (INBITS_OPT > -2))) THEN
    INGRIB_OUT = INGRIB_OPT
   ELSEIF (FALGRA (INT (INGRIB_OPT, JPLIKB)) .AND. (INGRIB /= 0)) THEN
     INGRIB_OUT = INGRIB_OPT
  ELSEIF (FALGRA (INT (INGRIB_OPT, JPLIKB)) .AND. (INGRIB == 0)) THEN
! grib_api can encode with full precision
    INGRIB_OUT = INGRIB_OPT
    INBITS     = 64
  ELSE
    INGRIB_OUT = INGRIB
  ENDIF

  IF (INBITS_OPT > -2) THEN
    INBITS = INBITS_OPT
  ENDIF

  IF (ISTRON_OPT > -2) THEN
    ISTRON = ISTRON_OPT
  ENDIF

  IF (ASSOCIATED (CLBITS)) THEN
    DO J = 1, SIZE (CLBITS)
      IF (CLBITS (J) (1:16) == CLNOMS (IARTL)) THEN
        READ (CLBITS (J) (17:32), *) INGRIB_OUT, INBITS 
      ENDIF
    ENDDO
  ENDIF

  INBPDG=INBITS 
  INBCSP=INBITS

  IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:FACOND',0,ZHOOK_HANDLE_AUX2)

  IF (IOPENMP > 0) THEN
!$OMP PARALLEL PRIVATE (IITID, JFLD, YLFA, CLNOMA, ILNOMA, ILONGD, IREP, ZVALCO)
    IITID = OML_MY_THREAD ()
    YLFA => YLFABYTID (IITID)
!$OMP DO
  DO JFLD = 1, IOPENMP

    CALL FAGOTE_MT (YLFA, IREP, ILUN2, INGRIB_OUT, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL)
   
    ILONGD = SIZE (ZVALCO)
    IF (LLUNDF1) THEN
      CALL FACONO_MT (YLFA, IREP, ILUN2, CLPREF, INIVAU, CLSUFF, ZCHAMP, LLCOSP, &
                    & CLNOMA, ILNOMA, ZVALCO, ILONGD, LDUNDF=LLUNDF1, PUNDF=ZUNDF1)
    ELSE
      CALL FACONO_MT (YLFA, IREP, ILUN2, CLPREF, INIVAU, CLSUFF, ZCHAMP, LLCOSP, &
                    & CLNOMA, ILNOMA, ZVALCO, ILONGD, LDUNDF=LLUNDF2, PUNDF=ZUNDF2)
    ENDIF

    IF (JFLD == 1) THEN
      CALL FAISAN (IREP, ILUN2, CLNOMA, ZVALCO, ILONGD)
    ENDIF
   
  ENDDO
!$OMP END DO
  
!$OMP END PARALLEL

  ELSE

    CALL FAGOTE (IREP, ILUN2, INGRIB_OUT, INBPDG, INBCSP, ISTRON, IPUILA, IDMOPL)
   
    CALL FI_GETTIMEOFDAY (ZTIME1)
    ILONGD = SIZE (ZVALCO)
    IF (LLUNDF1) THEN
      CALL FACONO (IREP, ILUN2, CLPREF, INIVAU, CLSUFF, ZCHAMP, LLCOSP, &
                 & CLNOMA, ILNOMA, ZVALCO, ILONGD, LDUNDF=LLUNDF1, PUNDF=ZUNDF1)
    ELSE
      CALL FACONO (IREP, ILUN2, CLPREF, INIVAU, CLSUFF, ZCHAMP, LLCOSP, &
                 & CLNOMA, ILNOMA, ZVALCO, ILONGD, LDUNDF=LLUNDF2, PUNDF=ZUNDF2)
    ENDIF
    CALL FI_GETTIMEOFDAY (ZTIME2)

    ZTIMEC = ZTIME2-ZTIME1

    CALL FI_GETTIMEOFDAY (ZTIME1)
    IF (LLDUMPSIZETIME) CALL FLUSH (ILUN2)
    CALL FAISAN (IREP, ILUN2, CLNOMA, ZVALCO, ILONGD)
    IF (LLDUMPSIZETIME) CALL FLUSH (ILUN2)
    CALL FI_GETTIMEOFDAY (ZTIME2)

    ZTIMEE = ZTIME2-ZTIME1


    IF (LLDUMPSIZETIME) THEN
      LLUNDF3 = .FALSE.
      CALL FI_GETTIMEOFDAY (ZTIME1)
      CALL FADOCO (IREP, ILUN1, CLPREF, INIVAU, CLSUFF, LLCOSP, CLNOMA, &
                 & ILNOMA, ZVALCO, ILONGD, ZCHAMP, LLUNDF3, ZUNDF3)
      CALL FI_GETTIMEOFDAY (ZTIME2)
      ZTIMED = ZTIME2-ZTIME1

      DO I = 1, LEN_TRIM (CLSUFF)
        IF (CLSUFF (I:I) == ' ') CLSUFF (I:I) = '_'
      ENDDO

      DO I = 1, LEN_TRIM (CLPREF)
        IF (CLPREF (I:I) == ' ') CLPREF (I:I) = '_'
      ENDDO

      IF (LLCOSP) THEN
        CLKIND = 'SPECTRAL'
      ELSE
        CLKIND = 'GRID-POINT'
      ENDIF

      WRITE (ILUNSIZETIME, '(A16," ",I16," ",A16," ",E16.8," ",E16.8," ",E16.8," ",I16," ",A16)') &
           & CLPREF, INIVAU, CLSUFF, ZTIMEC, ZTIMED, ZTIMEE, ILONGD*8, TRIM (CLKIND)


    ENDIF
   
  ENDIF
  IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:FACOND',1,ZHOOK_HANDLE_AUX2)

ENDDO
IF (LHOOK) CALL DR_HOOK ('FACONVGRIB:IARTL_LOOP',1,ZHOOK_HANDLE_AUX1)

IF (IOPENMP > 0) THEN

!$OMP PARALLEL PRIVATE (IITID)

  IITID = OML_MY_THREAD ()
  CALL FADUPU3 (YLFABYTID (IITID), YLFAPARAM)

!$OMP END PARALLEL

  CALL FADUPU4 (YLFAPARAM)

ENDIF


CALL FAIRME (IREP, ILUN1, 'KEEP')
CALL FAIRME (IREP, ILUN2, 'KEEP')

IF (CLFF /= '') THEn
  CALL FAIRME (IREP, ILUNF, 'KEEP')
ENDIF

DEALLOCATE (CLNOMS)

IF (LLFAGRTW) CALL FAGRTW (IREP, 12)

IF (LLDUMPSIZETIME) THEN
  CLOSE (ILUNSIZETIME)
ENDIF

IF (LHOOK) CALL DR_HOOK ('FACONVGRIB',1,ZHOOK_HANDLE)

CONTAINS

#include "falgra.h"

END SUBROUTINE FACONVGRIB



