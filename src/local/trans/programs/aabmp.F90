PROGRAM TEST

USE MPL_END_MOD, ONLY : MPL_END
USE MPL_INIT_MOD, ONLY : MPL_INIT
USE PARKIND1, ONLY : JPIM, JPRB
USE MPL_MODULE, ONLY : MPL_MYRANK, MPL_GROUPS_CREATE, MPL_NUMPROC
USE MPL_BARRIER_MOD, ONLY : MPL_BARRIER
USE YOMHOOK, ONLY : DR_HOOK, LHOOK

USE HADES
USE atlas_module  , ONLY:atlas_griddistribution,atlas_structuredgrid,atlas_reducedgaussiangrid,atlas_structuredgrid
IMPLICIT NONE

#include "setup_trans0.h"
#include "trans_inq.h"
#include "dist_grid.h"
#include "gath_grid.h"
#include "dir_trans.h"
#include "inv_trans.h"
#include "posnam.intfb.h"
#include "abor1.intfb.h"

TYPE HDR_t
  INTEGER (KIND=JPIM) :: ICOLS = -999, IROWS = -999, IOFF = -999
END TYPE HDR_t

INTEGER(KIND=JPIM) :: NPRGPNS, NPRGPEW, NPRTRW, NPRTRV, NPRINTLEV
INTEGER(KIND=JPIM) :: MYPROC, NPROC

INTEGER (KIND=JPIM) :: IA, IB

TYPE (GRID_OPTIONS_t), TARGET :: YLGROPT1, YLGROPT2
TYPE (GRID_OPTIONS_t), POINTER :: YLGROPT
TYPE (GRID_t) :: YLGRID1, YLGRID2
TYPE (DIST_t) :: YLDIST1, YLDIST2

REAL (KIND=JPRB), ALLOCATABLE, TARGET :: ZFLD1 (:,:), ZFLD2 (:,:)
TYPE (SHUFFLE_t) :: YLSHFL12
TYPE (HDR_t) :: YLHDR

INTEGER (KIND=JPIM) :: NGPTOT1, NGPTOT2
CHARACTER (LEN=*), PARAMETER :: CLDATA = 'world.bmp'

NAMELIST / NAMGRID / YLGROPT


REAL (KIND=JPRB) :: ZHOOK_HANDLE

type(atlas_structuredgrid):: dGrid
type(atlas_griddistribution):: dDist
type(atlas_structuredgrid):: eGrid
type(atlas_griddistribution):: eDist
type (SHUFFLE_t)  :: ylshflatlas




OPEN (4, FORM='FORMATTED')

NPRINTLEV = 0

CALL POSNAM (4, 'NAMGRID')
YLGROPT => YLGROPT2
READ (4, NAMGRID)

NULLIFY (YLGROPT)

CALL MPL_INIT (LDENV = .FALSE.)

MYPROC = MPL_MYRANK ()
NPROC  = MPL_NUMPROC

CALL SQUARE (NPROC, IA, IB)

NPRGPNS = IA
NPRGPEW = IB
NPRTRW  = IA
NPRTRV  = IB

CALL MPL_GROUPS_CREATE (NPRTRW, NPRTRV)



CALL SETUP_TRANS0 (KOUT=0, KERR=0, KPRINTLEV=NPRINTLEV, KMAX_RESOL=3,  &
                 & KPRGPNS=NPRGPNS, KPRGPEW=NPRGPEW, KPRTRW=NPRTRW,    &
                 & LDEQ_REGIONS=.TRUE.)

CALL MPL_BARRIER()

IF (LHOOK) CALL DR_HOOK ('AAINTERP',0,ZHOOK_HANDLE)

CALL HDR (CLDATA, YLHDR)

! Create grids & distributions

YLGROPT1%LSHIFTLON = .FALSE.
YLGROPT1%LZONAL    = .TRUE.   ! Force zonal distribution
YLGROPT1%LATLON    = .TRUE.
YLGROPT1%NDLON     = YLHDR%ICOLS
YLGROPT1%NDGLG     = YLHDR%IROWS
YLGROPT1%RLONOFF   = -180._JPRB

CALL CREATE_GRID_DIST (YLGRID1, YLDIST1, YLGROPT1)
CALL CREATE_GRID_DIST (YLGRID2, YLDIST2, YLGROPT2)

NGPTOT1 = YLDIST1%NGPTOTL (MYPROC)
NGPTOT2 = YLDIST2%NGPTOTL (MYPROC)

ALLOCATE (ZFLD1 (NGPTOT1, 3))
ALLOCATE (ZFLD2 (NGPTOT2, 3))

! Read input data

CALL BMP (CLDATA, YLHDR, ZFLD1, YLGRID1, YLDIST1)

CALL CREATE_SHUFFLE (YLDIST1,dDist, YLGRID1,dGrid, YLDIST2,eDist, YLGRID2,eGrid, YLSHFL12,ylshflatlas)

! Interpolate fields

CALL DO_INTERPOLATION (YLDIST1, YLGRID1, YLDIST2, YLGRID2, ZFLD1, &
                     & ZFLD2, YDSHFL12=YLSHFL12)

! Write fields

CALL FA (ZFLD2, NGPTOT2, 3, "RGB.fa",  &
       & (/ 'SURF', 'SURF', 'SURF' /), &
       & (/ 0_JPIM, 0_JPIM, 0_JPIM /), &
       & (/ '.R',   '.G',   '.B'   /), &
       & YLGRID2, YLDIST2)

IF (LHOOK) CALL DR_HOOK ('AAINTERP',1,ZHOOK_HANDLE)

CALL MPL_END

CONTAINS

SUBROUTINE BMP (CDFILE, YDHDR, PDATA, YDGRID, YDDIST)

CHARACTER (LEN=*),   INTENT (IN)     :: CDFILE
TYPE (HDR_t),        INTENT (INOUT)  :: YDHDR
REAL (KIND=JPRB),    INTENT (OUT)    :: PDATA (:,:)
TYPE (GRID_t),       INTENT (IN)     :: YDGRID
TYPE (DIST_t),       INTENT (IN)     :: YDDIST

INTEGER (KIND=JPIM) :: IROW, ICOL, IROW1, IROW2, IR, IG, IB, IOFF
INTEGER*1, ALLOCATABLE :: IROWD (:)

IROW1 = 1+YDGRID%NDGLG-YDDIST%NLSTLAT  (MYPROC)
IROW2 = 1+YDGRID%NDGLG-YDDIST%NFRSTLAT (MYPROC)

OPEN (77, FILE=TRIM (CDFILE), FORM='UNFORMATTED', ACTION='READ', ACCESS='STREAM')

ALLOCATE (IROWD (3*YDHDR%ICOLS))

IOFF = YDHDR%ICOLS * (IROW2 - IROW1)
DO IROW = IROW1, IROW2

  READ (77, POS=(YDHDR%IOFF+1+(IROW-1)*3*YDHDR%ICOLS)) IROWD

  DO ICOL = 1, YDHDR%ICOLS
    IR = IROWD (3+(ICOL-1)*3); IF (IR < 0) IR = IR + 256
    IG = IROWD (2+(ICOL-1)*3); IF (IG < 0) IG = IG + 256
    IB = IROWD (1+(ICOL-1)*3); IF (IB < 0) IB = IB + 256
    PDATA (IOFF+ICOL, 1) = REAL (IR, JPRB) / 255._JPRB
    PDATA (IOFF+ICOL, 2) = REAL (IG, JPRB) / 255._JPRB
    PDATA (IOFF+ICOL, 3) = REAL (IB, JPRB) / 255._JPRB
  ENDDO
  IOFF = IOFF - YDHDR%ICOLS

ENDDO

CLOSE (77)


END SUBROUTINE 

SUBROUTINE HDR (CDFILE, YDHDR)

CHARACTER (LEN=*), INTENT (IN)  :: CDFILE
TYPE (HDR_t),      INTENT (OUT) :: YDHDR

INTEGER*1 :: IHEAD1 (54)
INTEGER*2 :: IHEAD2 (54)

OPEN (77, FILE=TRIM (CDFILE), FORM='UNFORMATTED', ACTION='READ', ACCESS='STREAM')
READ (77, POS=1) IHEAD1

WHERE (IHEAD1 >= 0)
  IHEAD2 = IHEAD1
ELSEWHERE
  IHEAD2 = IHEAD1 + 256
ENDWHERE

YDHDR%IOFF  = S4 (IHEAD2 (11:14)) 
YDHDR%ICOLS = S4 (IHEAD2 (19:22))
YDHDR%IROWS = S4 (IHEAD2 (23:26))

CLOSE (77)

END SUBROUTINE 

INTEGER FUNCTION S4 (H)
INTEGER*2 :: H (4)
S4 = H (1) + 256 * (H (2) + 256 * (H (3) + 256 * H (4)))
END FUNCTION

SUBROUTINE SQUARE (KN, KA, KB)

INTEGER (KIND=JPIM) :: KN, KA, KB

KB = INT (SQRT (REAL (KN))) + 1

DO
  KA = KN / KB
  IF (KA * KB == KN) EXIT
  KB = KB - 1
ENDDO

END SUBROUTINE SQUARE

END PROGRAM TEST

