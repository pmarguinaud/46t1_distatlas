PROGRAM TEST

USE MPL_END_MOD, ONLY : MPL_END
USE MPL_INIT_MOD, ONLY : MPL_INIT
USE PARKIND1, ONLY : JPIM, JPRB
USE MPL_MODULE, ONLY : MPL_MYRANK, MPL_GROUPS_CREATE, MPL_NUMPROC
USE MPL_ALLREDUCE_MOD, ONLY : MPL_ALLREDUCE
USE MPL_BARRIER_MOD, ONLY : MPL_BARRIER
USE MODD_IO_SURF_ARO,  ONLY : SURFEX_FIELD_BUF_CACHE,      &
                            & SURFEX_FIELD_BUF_GET2DF,     &
                            & SURFEX_FIELD_BUF_PREALLOC,   &
                            & SURFEX_FIELD_BUF_READ_MISC,  &
                            & SURFEX_FIELD_BUF_WRITE_MISC, &
                            & SURFEX_FIELD_BUF,            &
                            & SURFEX_FIELD_BUF_SELECT,     &
                            & SURFEX_FIELD_BUF_GET,        &
                            & SURFEX_FIELD_BUF_SET,        &
                            & SURFEX_FIELD_BUF_DEALLOC
USE IOFLDDESC_MOD, ONLY : IOFLDDESC
USE YOMHOOK, ONLY : DR_HOOK, LHOOK


USE HADES
USE atlas_module  , ONLY:atlas_griddistribution,atlas_structuredgrid,atlas_reducedgaussiangrid,atlas_structuredgrid

IMPLICIT NONE

CHARACTER (LEN=16) :: CLSKIP (9)   = [ 'LATGAUSS        ', 'LAT_G_XY        ', 'LATINF          ', &
                                     & 'LATSUP          ', 'LONGAUSS        ', 'LON_G_XY        ', &
                                     & 'LONINF          ', 'LONSUP          ', 'MESHGAUSS       ']
CHARACTER (LEN=16) :: CLCOVER (256)

INTEGER(KIND=JPIM) :: NPRGPNS, NPRGPEW, NPRTRW, NPRTRV, NPRINTLEV
INTEGER(KIND=JPIM) :: MYPROC, NPROC

INTEGER (KIND=JPIM) :: IA, IB
INTEGER (KIND=JPIM) :: IREP
INTEGER (KIND=JPIM) :: IDATEF (22)

TYPE (GRID_t) :: YLGRID1, YLGRID2
TYPE (DIST_t) :: YLDIST1, YLDIST2

TYPE (SHUFFLE_t)  :: YLSHFL12
TYPE (SHUFFLE4_t) :: YLSHFL12_4
TYPE (WEIGHTS4_t) :: YLWGHT12_4

CHARACTER (LEN=64) :: CLFILEPGD1, CLFILEPRE1, CLFILEPGD2, CLFILEPRE2
INTEGER (KIND=JPIM) :: NGPTOT1, NGPTOT2
TYPE (SURFEX_FIELD_BUF_CACHE) :: YLSFXC1, YLSFXC2PRE
TYPE (IOFLDDESC), POINTER     :: YLFLDSC1 (:)
REAL (KIND=JPRB), POINTER     :: ZFLD1 (:,:)
REAL (KIND=JPRB), ALLOCATABLE, TARGET :: ZFLD2 (:,:)

INTEGER (KIND=JPIM) :: INFLD1, INFLDPGD1, INFLDPRE1, IOFFPGD1, IOFFPRE1

REAL (KIND=JPRB) :: ZUNDEF


type (SHUFFLE_t)  :: ylshflatlas
type(atlas_structuredgrid):: dGrid
type(atlas_griddistribution):: dDist
type(atlas_structuredgrid):: eGrid
type(atlas_griddistribution):: eDist

NAMELIST / NAMPREP / NPRINTLEV, NPRGPNS, NPRGPEW, CLFILEPGD1, CLFILEPRE1, CLFILEPGD2, CLFILEPRE2

#include "setup_trans0.h"
#include "posnam.intfb.h"

REAL (KIND=JPRB) :: ZHOOK_HANDLE

BLOCK
  INTEGER (KIND=JPIM) :: J
  DO J = 1, 256
    WRITE (CLCOVER (J), '("COVER",I3.3)') J
  ENDDO
ENDBLOCK

ZUNDEF = HUGE (ZUNDEF)

OPEN (4, FORM='FORMATTED')

NPRINTLEV = 0

CALL POSNAM (4, 'NAMPREP')
READ (4, NAMPREP)

CALL MPL_INIT (LDENV = .FALSE.)

MYPROC = MPL_MYRANK ()
NPROC  = MPL_NUMPROC

CALL SQUARE (NPROC, IA, IB)

NPRGPNS = IA
NPRGPEW = IB
NPRTRW  = IA
NPRTRV  = IB

CALL MPL_GROUPS_CREATE (NPRTRW, NPRTRV)

CALL SETUP_TRANS0 (KOUT=0, KERR=0, KPRINTLEV=NPRINTLEV, KMAX_RESOL=3,  &
                 & KPRGPNS=NPRGPNS, KPRGPEW=NPRGPEW, KPRTRW=NPRTRW,    &
                 & LDEQ_REGIONS=.TRUE.)

CALL MPL_BARRIER()

IF (LHOOK) CALL DR_HOOK ('AAPREP',0,ZHOOK_HANDLE)

CALL CREATE_GRID_DIST_FA (YLGRID1, YLDIST1, CLFILEPGD1, GRID_OPTIONS_t (LGRIDONLY=.TRUE.))
CALL CREATE_GRID_DIST_FA (YLGRID2, YLDIST2, CLFILEPGD2, GRID_OPTIONS_t (LGRIDONLY=.TRUE.))

CALL CREATE_SHUFFLE (YLDIST1,dDist, YLGRID1,dGrid, YLDIST2,eDist, YLGRID2,eGrid, YLSHFL12,ylshflatlas)
CALL CREATE_SHUFFLE4 (YLDIST1, YLGRID1, YLDIST2, YLGRID2, YLSHFL12_4)
CALL CREATE_WEIGHTS4 (YLDIST1, YLGRID1, YLDIST2, YLGRID2, YLSHFL12_4, YLWGHT12_4)

NGPTOT1 = YLDIST1%NGPTOTL (MYPROC)
NGPTOT2 = YLDIST2%NGPTOTL (MYPROC)

! Read PGD & PREP #1

BLOCK
  INTEGER (KIND=JPIM), POINTER :: IFUNITS1 (:)  
  INTEGER (KIND=JPIM), PARAMETER :: IUNITPGD1 = 77, IUNITPRE1 = 88
  INTEGER (KIND=JPIM) :: JFLD1, JFLD2
  
  CALL OFA (IUNITPGD1, CLFILEPGD1, YLGRID1)
  CALL SURFEX_FIELD_BUF_READ_MISC (YLSFXC1, KUNIT=IUNITPGD1)
  CALL SURFEX_FIELD_BUF_SELECT (YLSFXC1, CDREMV=CLCOVER)
  CALL FAIRME (IREP, IUNITPGD1, 'KEEP')

  CALL OFA (IUNITPRE1, CLFILEPRE1, YLGRID1)
  CALL FADIEX (IREP, IUNITPRE1, IDATEF)
  CALL SURFEX_FIELD_BUF_READ_MISC (YLSFXC1, KUNIT=IUNITPRE1, KMERGE=2)
  CALL SURFEX_FIELD_BUF_READ_MISC (YLSFXC2PRE, KUNIT=IUNITPRE1)
  CALL FAIRME (IREP, IUNITPRE1, 'KEEP')
  
  CALL SURFEX_FIELD_BUF_PREALLOC (YLSFXC1, NGPTOT1)
  
  CALL SURFEX_FIELD_BUF_GET2DF (YLSFXC1, PFIELDS=ZFLD1, &
                              & YDFLDSC=YLFLDSC1, KFUNITS=IFUNITS1)

  INFLD1    = SIZE (ZFLD1, 2)
  INFLDPGD1 = COUNT (IFUNITS1 == IUNITPGD1)
  INFLDPRE1 = COUNT (IFUNITS1 == IUNITPRE1)

  IOFFPGD1 = 0
  IOFFPRE1 = INFLDPGD1

  JFLD1 = IOFFPGD1+1
  JFLD2 = IOFFPGD1+INFLDPGD1
  CALL RFA (ZFLD1 (1:NGPTOT1,JFLD1:JFLD2), NGPTOT1, INFLDPGD1, CLFILEPGD1, YLFLDSC1 (JFLD1:JFLD2)%CPREF, &
          & YLFLDSC1 (JFLD1:JFLD2)%ILEVG, YLFLDSC1 (JFLD1:JFLD2)%CSUFF, YLGRID1, YLDIST1, ZUNDEF)

  JFLD1 = IOFFPRE1+1
  JFLD2 = IOFFPRE1+INFLDPRE1
  CALL RFA (ZFLD1 (1:NGPTOT1,JFLD1:JFLD2), NGPTOT1, INFLDPRE1, CLFILEPRE1, YLFLDSC1 (JFLD1:JFLD2)%CPREF, &
          & YLFLDSC1 (JFLD1:JFLD2)%ILEVG, YLFLDSC1 (JFLD1:JFLD2)%CSUFF, YLGRID1, YLDIST1, ZUNDEF)

ENDBLOCK

! Read PGD #2

BLOCK
  INTEGER (KIND=JPIM), PARAMETER :: IUNITPGD2 = 77_JPIM
  INTEGER (KIND=JPIM) :: JFLD1, JFLD2, JFLD

  ALLOCATE (ZFLD2 (NGPTOT2, INFLD1))

  JFLD1 = IOFFPGD1+1
  JFLD2 = IOFFPGD1+INFLDPGD1
  CALL RFA (ZFLD2 (1:NGPTOT2,JFLD1:JFLD2), NGPTOT2, INFLDPGD1, CLFILEPGD2, YLFLDSC1 (JFLD1:JFLD2)%CPREF, &
          & YLFLDSC1 (JFLD1:JFLD2)%ILEVG, YLFLDSC1 (JFLD1:JFLD2)%CSUFF, YLGRID2, YLDIST2, ZUNDEF,        &
          & [(.FALSE., JFLD=1, INFLDPGD1)])

ENDBLOCK

BLOCK
  INTEGER (KIND=JPIM), PARAMETER :: JFRAC_SEA = 1, JFRAC_NATURE = 2, JFRAC_TOWN = 3, JFRAC_WATER = 4
  CHARACTER (LEN=16), PARAMETER :: CLMASK (0:4) = ['FULL            ', 'FRAC_SEA        ', 'FRAC_NATURE     ', &
                                                 & 'FRAC_TOWN       ', 'FRAC_WATER      ']
  LOGICAL :: LLMASK (0:4)
  INTEGER (KIND=JPIM) :: I, J, JFLD, JMSK, ICNTDEFN, ICNTMASK
  REAL (KIND=JPRB), POINTER :: ZMSK (:)
  REAL (KIND=JPRB) :: Z2INT4 (1:NGPTOT2,INFLDPRE1) ! 4-points
  REAL (KIND=JPRB) :: Z2INTA (1:NGPTOT2,INFLDPRE1) ! Average
  INTEGER (KIND=JPIM) :: IMASK2JFLD (0:4)

! Interpolate

  CALL DO_INTERPOLATION (YLDIST1, YLGRID1, YLDIST2, YLGRID2, ZFLD1 (1:NGPTOT1,IOFFPRE1+1:IOFFPRE1+INFLDPRE1), &
                       & Z2INTA, YDSHFL12=YLSHFL12, PUNDEF=[(ZUNDEF, I = 1, INFLDPRE1)])

  CALL DO_INTERPOLATION4 (YLDIST1, YLGRID1, YLDIST2, YLGRID2, ZFLD1 (1:NGPTOT1,IOFFPRE1+1:IOFFPRE1+INFLDPRE1), &
                        & Z2INT4, YLWGHT12_4, YDSHFL12=YLSHFL12_4, PUNDEF=[(ZUNDEF, I = 1, INFLDPRE1)])

  ZFLD2 (1:NGPTOT2,IOFFPRE1+1:IOFFPRE1+INFLDPRE1) = Z2INTA

  DO JFLD = 1, INFLDPRE1
    WHERE (ZFLD2 (:, IOFFPRE1+JFLD) == ZUNDEF)
      ZFLD2 (:, IOFFPRE1+JFLD) = Z2INT4 (:, JFLD)
    ENDWHERE
  ENDDO

! Find indices of fractions of SEA, NATURE, WATER, TOWN

  IMASK2JFLD = 0

  JFLD = 1

  DO I = 1, SIZE (YLSFXC1%P)
    DO J = 1, SIZE (CLMASK)
      IF (YLSFXC1%P (I)%NAME == CLMASK (J)) IMASK2JFLD (J) = JFLD
    ENDDO
    IF (YLSFXC1%P (I)%TYPE == 'X1') THEN
      JFLD = JFLD + 1
    ELSEIF (YLSFXC1%P (I)%TYPE == 'X2') THEN
      JFLD = JFLD + YLSFXC1%P (I)%NDIM (2)
    ENDIF
  ENDDO

  IF (ANY (IMASK2JFLD (1:4) == 0)) CALL ABOR1 ('SURFEX MASKS WERE NOT FOUND')

! For each field, guess its mask

  DO JFLD = IOFFPRE1+1, IOFFPRE1+INFLDPRE1

    IF (ANY (CLSKIP == YLFLDSC1 (JFLD)%CSUFF)) CYCLE

    ICNTDEFN = COUNT (ZFLD1 (:, JFLD) /= ZUNDEF)
    CALL MPL_ALLREDUCE (ICNTDEFN, 'SUM', CDSTRING='AAPREP:')


    IF (MYPROC == 1) &
    WRITE (0, '(" | ",A8," ",I5," ", A16," ",I10," ",I10, " | ")', ADVANCE='NO') YLFLDSC1 (JFLD)%CPREF, &
     & YLFLDSC1 (JFLD)%ILEVG, YLFLDSC1 (JFLD)%CSUFF, ICNTDEFN, YLGRID1%NGPTOTG

    LLMASK (0) = ICNTDEFN == YLGRID1%NGPTOTG

    DO J = 1, UBOUND (IMASK2JFLD, 1)
      ICNTMASK = COUNT (((ZFLD1 (:, JFLD) == ZUNDEF) .AND. (ZFLD1 (:, IMASK2JFLD (J)) >  0._JPRB)) &
                 & .OR. ((ZFLD1 (:, JFLD) /= ZUNDEF) .AND. (ZFLD1 (:, IMASK2JFLD (J)) == 0._JPRB)))
      CALL MPL_ALLREDUCE (ICNTMASK, 'SUM', CDSTRING='AAPREP:')
      IF (MYPROC == 1) &
      WRITE (0, '(A16," ",I10," | ")', ADVANCE='NO') CLMASK (J), ICNTMASK
      LLMASK (J) = ICNTMASK == 0
    ENDDO

    JMSK = -1

    IF (ANY (LLMASK)) THEN
      DO J = 0, UBOUND (CLMASK, 1)
        IF (LLMASK (J)) THEN
          JMSK = J
          IF (MYPROC == 1) &
          WRITE (0, '(A16)', ADVANCE='NO') CLMASK (J)
          EXIT
        ENDIF
      ENDDO
    ELSE
      IF (MYPROC == 1) &
      WRITE (0, '(16X)', ADVANCE='NO') 
    ENDIF

    IF (MYPROC == 1) &
    WRITE (0, '(" | ",5L2," | ")', ADVANCE='NO') LLMASK

! Fix fields on grid #2

    IF (JMSK >= 0) THEN
      BLOCK
        REAL (KIND=JPRB) :: ZSUM, ZAVG
        INTEGER (KIND=JPIM) :: ICNT
        ZSUM = 0._JPRB
        IF (JMSK == 0) THEN
          ZSUM = SUM (ZFLD1 (:, JFLD))
          ICNT = NGPTOT1
        ELSE
          ZSUM = SUM (ZFLD1 (:, JFLD), MASK=ZFLD1 (:, IMASK2JFLD (JMSK)) > 0._JPRB)
          ICNT = COUNT (ZFLD1 (:, IMASK2JFLD (JMSK)) > 0._JPRB)
        ENDIF
        CALL MPL_ALLREDUCE (ICNT, 'SUM', CDSTRING='AAPREP:')
        CALL MPL_ALLREDUCE (ZSUM, 'SUM', LDREPROD = .FALSE., CDSTRING='AAPREP:')
        ZAVG = ZSUM / REAL (ICNT, JPRB)

        IF (MYPROC == 1) &
        WRITE (0, '(" | ",E15.6," | ")', ADVANCE='NO') ZAVG

        WHERE ((ZFLD2 (:, JFLD) == ZUNDEF) .AND. (ZFLD2 (:, IMASK2JFLD (JMSK)) > 0._JPRB)) 
          ZFLD2 (:, JFLD) = ZAVG
        ENDWHERE
      ENDBLOCK
    ENDIF

    WRITE (0, *)
    


  ENDDO

ENDBLOCK

! Write PREP #2

BLOCK
  INTEGER (KIND=JPIM) :: JFLD1, JFLD2, JFLD
  LOGICAL :: LLMASK (INFLDPRE1)

  JFLD1 = IOFFPRE1+1
  JFLD2 = IOFFPRE1+INFLDPRE1

  DO JFLD = JFLD1, JFLD2
    LLMASK (JFLD-IOFFPRE1) = ANY (CLSKIP == YLFLDSC1 (JFLD)%CSUFF)
  ENDDO

  CALL WFA (ZFLD2 (1:NGPTOT2,IOFFPRE1+1:IOFFPRE1+INFLDPRE1), NGPTOT2, INFLDPRE1, CLFILEPRE2,          &
          & YLFLDSC1 (JFLD1:JFLD2)%CPREF, YLFLDSC1 (JFLD1:JFLD2)%ILEVG, YLFLDSC1 (JFLD1:JFLD2)%CSUFF, &
          & YLGRID2, YLDIST2, PUNDEF=ZUNDEF, LDMASK=LLMASK)

ENDBLOCK


BLOCK
! TYPE (SURFEX_FIELD_BUF),  POINTER :: P(:) 
! INTEGER (KIND=JPIM) :: INFLD, I, J
  INTEGER (KIND=JPIM) :: IREP
  INTEGER (KIND=JPIM), PARAMETER :: IUNIT = 77_JPIM

  CALL SURFEX_FIELD_BUF_SELECT (YLSFXC2PRE, CDREMV=CLSKIP)

#ifdef UNDEF
  P => YLSFXC2PRE%P
  NULLIFY (YLSFXC2PRE%P)

  INFLD = 0

  DO I = 1, SIZE (P)
    IF (ALL (CLSKIP /= P (I)%NAME)) INFLD = INFLD + 1
  ENDDO

  ALLOCATE (YLSFXC2PRE%P (INFLD))

  J = 1

  DO I = 1, SIZE (P)
    IF (ALL (CLSKIP /= P (I)%NAME)) THEN
      YLSFXC2PRE%P (J) = P (I)
      IF (YLSFXC2PRE%P (J)%TYPE == 'X1' &
   & .OR. YLSFXC2PRE%P (J)%TYPE == 'X2') THEN
        YLSFXC2PRE%P (J)%NDIM (1) = YLGRID2%NGPTOTG
      ENDIF
      J = J + 1
    ENDIF
  ENDDO

  YLSFXC2PRE%N = INFLD
  YLSFXC2PRE%NTOT = INFLD

#endif

  BLOCK
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%NGPTOTG, 'DIM_FULL')
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%RSTRETCH, 'CODIL')
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%RLONCENT * RAD2DEG, 'LOPO')
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%RLATCENT * RAD2DEG, 'LAPO')
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%NDGLG, 'NLATI')
    CALL SURFEX_FIELD_BUF_SET (YLSFXC2PRE, YLGRID2%NLOEN, 'NLOPA')
  ENDBLOCK

  IF (MYPROC == 1) THEN
    CALL OFA (IUNIT, '.'//TRIM (CLFILEPRE2), YLGRID2)
    CALL FANDAX (IREP, IUNIT, IDATEF)
    CALL SURFEX_FIELD_BUF_WRITE_MISC (YLSFXC2PRE, IUNIT)
    CALL FAIRME (IREP, IUNIT, 'KEEP')
  ENDIF

ENDBLOCK

CALL SURFEX_FIELD_BUF_DEALLOC (YLSFXC2PRE)
CALL SURFEX_FIELD_BUF_DEALLOC (YLSFXC1)
ZFLD1 => NULL ()


IF (LHOOK) CALL DR_HOOK ('AAPREP',1,ZHOOK_HANDLE)

CALL MPL_END

CONTAINS

SUBROUTINE SQUARE (KN, KA, KB)

INTEGER (KIND=JPIM) :: KN, KA, KB

KB = INT (SQRT (REAL (KN))) + 1

DO
  KA = KN / KB
  IF (KA * KB == KN) EXIT
  KB = KB - 1
ENDDO

END SUBROUTINE SQUARE

END PROGRAM TEST

