PROGRAM TEST

USE MPL_END_MOD, ONLY : MPL_END
USE MPL_INIT_MOD, ONLY : MPL_INIT
USE PARKIND1, ONLY : JPIM, JPRB
USE MPL_MODULE, ONLY : MPL_MYRANK, MPL_GROUPS_CREATE, MPL_NUMPROC
USE MPL_BARRIER_MOD, ONLY : MPL_BARRIER
USE YOMHOOK, ONLY : DR_HOOK, LHOOK

USE HADES

IMPLICIT NONE

INTEGER(KIND=JPIM) :: NPRGPNS, NPRGPEW, NPRTRW, NPRTRV, NPRINTLEV
INTEGER(KIND=JPIM) :: MYPROC, NPROC

#include "setup_trans0.h"
#include "trans_inq.h"
#include "dist_grid.h"
#include "gath_grid.h"
#include "dist_spec.h"
#include "gath_spec.h"
#include "dir_trans.h"
#include "inv_trans.h"
#include "posnam.intfb.h"
#include "faieno.h"
#include "facilo.h"


REAL (KIND=JPRB),   ALLOCATABLE :: ZSPBUFL (:,:) 
REAL (KIND=JPRB),   ALLOCATABLE :: ZSPBUFG (:,:) 
REAL (KIND=JPRB),   ALLOCATABLE :: ZGPBUFL (:,:,:) 
REAL (KIND=JPRB),   ALLOCATABLE :: ZGPBUFG (:,:) 

REAL (KIND=JPRB), PARAMETER :: RG = 1._JPRB / 0.101971621297793_JPRB
REAL (KIND=JPRB), PARAMETER :: XZSEPS = 1.E-2_JPRB
!   1234567890123456789012345678901234567890
CHARACTER(LEN=40), PARAMETER :: CDT (6) =      &
 & (/                                          &
 & " SPECSURFGEOPOTEN (ICMSHFCSTINIT)       ", &
 & " SPECSURFGEOPOTEN (Const.Clim)          ", &
 & " SURFGEOPOTENTIEL (Const.Clim)          ", &
 & " SFX.ZS                                 ", &
 & " SURFGEOPOTENTIEL (Const.Clim.1)        ", &
 & " SURFGEOPOTENTIEL (Const.Clim.2)        "  &
 & /)


TYPE (GRID_t) :: YLGRID
TYPE (DIST_t) :: YLDIST
TYPE (GRID_OPTIONS_t) :: YLGROPT
CHARACTER (LEN=64),  PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM), PARAMETER :: ILUN = 77
INTEGER (KIND=JPIM) :: INBARP, INBARI, IREP, INIMES, IVSET (1)
INTEGER (KIND=JPIM) :: I, J

REAL (KIND=JPRB) :: ZHOOK_HANDLE

CALL MPL_INIT (LDENV = .FALSE.)

MYPROC = MPL_MYRANK ()
NPROC  = MPL_NUMPROC

NPRGPNS = NPROC
NPRGPEW = 1
NPRTRW  = NPROC
NPRTRV  = 1

CALL MPL_GROUPS_CREATE (NPRTRW, NPRTRV)

NPRINTLEV = 0
CALL SETUP_TRANS0 (KOUT=0, KERR=0, KPRINTLEV=NPRINTLEV, KMAX_RESOL=3,  &
                 & KPRGPNS=NPRGPNS, KPRGPEW=NPRGPEW, KPRTRW=NPRTRW,    &
                 & LDEQ_REGIONS=.TRUE.)

CALL MPL_BARRIER()

IF (LHOOK) CALL DR_HOOK ('SPECSURFGEOPOTEN',0,ZHOOK_HANDLE)

YLGROPT%LGRIDONLY = .FALSE.
CALL CREATE_GRID_DIST_FA (YLGRID, YLDIST, 'ICMSHFCSTINIT', YLGROPT)


ALLOCATE (ZGPBUFL (YLDIST%NGPTOTL (MYPROC), 6, 1))
ALLOCATE (ZSPBUFL (1, YLDIST%NSPEC2L (MYPROC)))


CALL RFA (ZGPBUFL (:, 4, 1), YLDIST%NGPTOTL (MYPROC), 1_JPIM, 'Const.Clim.sfx', ['SFX.'], &
        & [0_JPIM], ['ZS'], YLGRID, YLDIST)

ZGPBUFL (:, 4, 1) = ZGPBUFL (:, 4, 1) * RG

CALL RFA (ZGPBUFL (:, 3, 1), YLDIST%NGPTOTL (MYPROC), 1_JPIM, 'Const.Clim', ['SURF'], &
        & [0_JPIM], ['GEOPOTENTIEL'], YLGRID, YLDIST)

CALL RFA (ZGPBUFL (:, 5, 1), YLDIST%NGPTOTL (MYPROC), 1_JPIM, 'Const.Clim.1', ['SURF'], &
        & [0_JPIM], ['GEOPOTENTIEL'], YLGRID, YLDIST)

CALL RFA (ZGPBUFL (:, 6, 1), YLDIST%NGPTOTL (MYPROC), 1_JPIM, 'Const.Clim.2', ['SURF'], &
        & [0_JPIM], ['GEOPOTENTIEL'], YLGRID, YLDIST)

IF (MYPROC == 1) THEN
  INBARI = 0; INBARP = 0; INIMES = 2
  CALL FAITOU (IREP, ILUN, .TRUE., 'ICMSHFCSTINIT', 'OLD', .TRUE., .TRUE., INIMES, INBARP, INBARI, CLNOMC)
  ALLOCATE (ZSPBUFG (1, YLGRID%NSPEC2G))
  CALL FACILO (IREP, ILUN, 'SPEC', 0, 'SURFGEOPOTEN', ZSPBUFG, .TRUE.)
  CALL DIST_SPEC (KFDISTG=1_JPIM, KFROM=[1_JPIM], KVSET=[1_JPIM], PSPEC=ZSPBUFL, LDIM1_IS_FLD=.TRUE., PSPECG=ZSPBUFG)
  DEALLOCATE (ZSPBUFG)
  CALL FAIRME (IREP, ILUN, 'KEEP')
ELSE
  CALL DIST_SPEC (KFDISTG=1_JPIM, KFROM=[1_JPIM], KVSET=[1_JPIM], PSPEC=ZSPBUFL, LDIM1_IS_FLD=.TRUE.)
ENDIF

CALL INV_TRANS (PSPSCALAR=ZSPBUFL, PGP=ZGPBUFL (:, 1:1, 1:1), KVSETSC=[1_JPIM])


IF (MYPROC == 1) THEN
  INBARI = 0; INBARP = 0; INIMES = 2
  CALL FAITOU (IREP, ILUN, .TRUE., 'Const.Clim', 'OLD', .TRUE., .TRUE., INIMES, INBARP, INBARI, CLNOMC)
  ALLOCATE (ZSPBUFG (1, YLGRID%NSPEC2G))
  CALL FACILO (IREP, ILUN, 'SPEC', 0, 'SURFGEOPOTEN', ZSPBUFG, .TRUE.)
  CALL DIST_SPEC (KFDISTG=1_JPIM, KFROM=[1_JPIM], KVSET=[1_JPIM], PSPEC=ZSPBUFL, LDIM1_IS_FLD=.TRUE., PSPECG=ZSPBUFG)
  DEALLOCATE (ZSPBUFG)
  CALL FAIRME (IREP, ILUN, 'KEEP')
ELSE
  CALL DIST_SPEC (KFDISTG=1_JPIM, KFROM=[1_JPIM], KVSET=[1_JPIM], PSPEC=ZSPBUFL, LDIM1_IS_FLD=.TRUE.)
ENDIF

CALL INV_TRANS (PSPSCALAR=ZSPBUFL, PGP=ZGPBUFL (:, 2:2, 1:1), KVSETSC=[1_JPIM])


DO I = 1, 6
DO J = 1, 6
CALL DIFF (CDT (I), CDT (J), ZGPBUFL (:, I, 1), ZGPBUFL (:, J, 1))
ENDDO
ENDDO


IF (LHOOK) CALL DR_HOOK ('SPECSURFGEOPOTEN',1,ZHOOK_HANDLE)

CALL MPL_END

CONTAINS

SUBROUTINE DIFF (CDT1, CDT2, PZ1, PZ2)

CHARACTER(LEN=*) :: CDT1, CDT2
REAL (KIND=JPRB) :: PZ1 (:), PZ2 (:)
INTEGER (KIND=JPIM) :: JGP, ICOUNT

WRITE (0, '(A," - ",A)') CDT1, CDT2

ICOUNT = 0

DO JGP = 1, YLDIST%NGPTOTL (MYPROC)
  IF ((ABS (PZ1 (JGP) - PZ2 (JGP)) > MAX (XZSEPS * ABS (PZ1 (JGP)), 10._JPRB * XZSEPS)) &
    & .AND. (PZ1 (JGP) > 0._JPRB)) THEN
    WRITE (0, '("DIFF ",I6," | ",2E12.4)') JGP, PZ1 (JGP), PZ2 (JGP)
    ICOUNT = ICOUNT + 1
    IF (ICOUNT > 100) EXIT
  ENDIF
ENDDO

END SUBROUTINE

END PROGRAM

